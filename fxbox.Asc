Set Buffer 50 : Set Stack 0 : Rem                                    *main 
Request Off : Rem                                                    *play 
'                                                                    *position 
Data "                         .." : Rem                             *draw
Data "  oec8888E          . uW8'" : Rem                              *zoom 
Data " @888888N  uL   ..  `t888             u.     uL   .." : Rem    *range
Data " 8`*88%  .@88b @88R 8888   .    ...ue888b  .@88b  @88R" : Rem  *file 
Data " 8b.    '`Y888k/`*P  9888.z88N  888R Y888r'`Y888k/`*P" : Rem   *edit
Data "u8888888>  Y888L     9888  888E 888R I888>   Y888L" : Rem      *mix
Data " 8888R      8888     9888  888E 888R I888>    8888" : Rem      *frame
Data " 8888P      `888N    9888  888E 888R I888>    `888N" : Rem     *volume
Data " *888>   .u./`888&   9888  888Eu8888cJ888  .u./`888&" : Rem    *resample 
Data " 4888   d888' Y888*'.8888  888' `*888*P'  d888' Y888*'" : Rem  *filter 
Data " '888   ` `Y   Y'    `%888*%'     `Y'     ` `Y   Y'" : Rem     *distort 
Data "  88R                   '`" : Rem                              *effects
Data "  88>" : Rem                                                   *bitwise 
Data "  48" : Rem                                                    *convert
Data "  '8                 Version 1.18",, : Rem                     *encoding
Data "              © 1991-2026 Marc Williams", : Rem                *process
'                                                                    *utils
'*********************************************************************direct** 
'                                                                    *memory 
_M1=7 : _M2=20 : _M3=15 : _AB=13 : _OB=21 : _TB=8 : _MR=64 : Rem     *help
_FL=256 : _FR=600 : _XC=8 : _RT=11 : _OP=29 : _CH=4 : _CM=32 : Rem   *macro 
'                                                                    *sys
'*********************************************************************mouse*** 
'                                                                    *choice 
Dim MENX(_M1),MENB$(10),MENM$(_M2-2) : Rem                           *menu
Dim BUTS$(_AB),BUTX(_AB),BUTY(_AB),BUTG$(_AB) : Rem                  *windows
Dim PAULA(7),CHAIN$(_XC),D_START(_CH),D_LENGTH(_CH) : Rem            *buttons
Dim OFFSET(_CH),LEFTOVER(_CH),ADV#(_CH),ADVPOS#(_CH) : Rem           *gadgets 
Dim TIME(_CH),XP(_CH),XP2(_CH) : Rem                                 *network 
'                                                                    *init 
Dim DEVS$(5),SETTING$(_OP),FILES$(_FR),FILEL(_FR) : Rem              *screen 
Dim FB$(_FL),FPATH$(_FL),FMACRO$(_MR) : Rem                          *error
'                                                                    *data 
Dim NAME$(_FL),CPYR$(_FL),AUTH$(_FL),ANNO$(_FL),COMP(_FL)
Dim ROT#(_RT),ROTX(_RT),ROTY(_RT),ROTX2(_RT),ROTY2(_RT)
MI=5 : Dim MACROT$(MI),MA(MI),MP(MI),MN(MI) : MI=-1
Dim CMD$(_CM),CMDG$(_CM),FIBO(15),CHROM$(13)
'
_UNNAMED$="Unnamed" : _HZ$="Hz" : _PERCENT$="%" : _LP$="¯" : _$=""
_SPACE$=" " : _SCORE$="_" : _COMMA$="," : _COLON$=":" : _MINUS$="-"
_NULL$=Chr$(0) : _NEWLINE$=Chr$(10) : _»$="»"
_TRUE$="-1" : _FALSE$="0" : _TOAST$="³^°¬" : _OK$="Ok" : _CANCEL$="Cancel"
_DOTS$="..." : _BPM$=" Bpm" : _KILO$="K" : _MEGA$="M"
_SPACER$=String$(_SPACE$,2) : _CHUNK$=String$(_NULL$,4) : SEQ$=_$
'
_BG=0 : _PANE=1 : _WHITE=2 : _GRAY=3
_BLACK=4 : _SHADOW=5 : _BRIGHT=6 : _HIGHLIGHT=7
'
_COL=_PANE : _COLP=_COL
'  
SCR_W=640 : SCR_W#=SCR_W : SCR_H=256 : SCR_R=Hires : SCR_D=8
BUT_W=103 : BUT_H=14
CLI_Y=92 : CLI_H=144
'
_MODE=1 : _MODEP=_MODE : _M3SEL=_MODE : B=1 : PB=B : LB=0 : UB=B
PBANK=5 : UBANK=2 : CBANK=6 : WBANK=7 : KMUL=0
_FXBUF=20480 : _FXCUR=32767
_CHANNEL=1 : _CHANNELS=1
FREQ=22448 : PFREQ=FREQ : MFREQ=96000 : _AMIHZ=True : VOL=64
_TIMEDIV#=16.0 : _TIMEBEAT#=SCR_W/_TIMEDIV#
'
REXX=False : SOCKSYNC=-1 : SOCKVST=-1
_CHAIN=1 : _REC=False : _LP=True : _LPMIX=True
_DIRECT=0 : _TEXT=False : _SPIN=0 : _GRAB=1
'
REALTIME=False : RENDERING=False : SEQUENCER=False
PLYING=False : BGPLYING=False : STPPED=True : RNGEPLY=False : DISPPLY=False
METRO=True : OSCILLO=False : ABOUT=False
PLYTIME=0 : STPTIME=0 : CALC=False : POS=1
'
_DITHER=1
'
ROT#(1)=13.3
'
_SCALEMIX=False
VN=60 : VA#=0.6
_DCOFFSETN=0
KDOWN=False
HIGHN=80 : HIGHF=4000
RC#=0.0 : DT#=0.0 : ALPHA#=0.0
Dim HBUF(_CH)
BANDN=80 : BANDF=4000
Dim BBUF0(_CH),BBUF1(_CH)
LOWN=80 : LOWF=4000
Dim LBUF(_CH)
RESN=45 : _RF#=0.225 : _RQ#=0.6666674 : _RFB#=1.52688
ROT#(2)=9.6 : ROT#(3)=11.7
Dim RBUF0#(_CH),RBUF1#(_CH)
Dim SMOBUF(_CH)
ALIN=12 : ALIN2=ALIN
TUBEN=96 : TUBEV#=1.04
SATN=120 : SATV#=1.2
FOLDN=4 : FOLDV#=0.015625
PHAN=0
Dim PHBUF#(_CH),PHBUF0(_CH),PHBUF1(_CH)
_EFFN=0 : _ECHON=0
RVN=20 : RVV#=0.6
Dim RVBUF0(_CH),RVBUF1(_CH)
TREMN=82 : TREMN#=2.5625
Dim TBUF(_CH)
_THRESHOLDN#=50 : ROT#(4)=11
_SLOPEN#=50 : ROT#(5)=11
_WINDOWN#=0.08323326 : ROT#(6)=8.9
_LOOKAHEADN#=25 : ROT#(7)=9.7
_ATTACKN#=0.625 : ROT#(8)=8.8
_RELEASEN#=156.25 : ROT#(9)=10
_ATT#=0.0 : _REL#=0.0
_OFFSET=0 : _RMS=0
Dim CBUFENV#(_CH),CBUFSUMM#(_CH)
_MSB=False
'
_2PI#=Pi#*2
T0NEN=0 : T0NEV#=5.0 : T0NEA=98
ROT#(10)=8.5 : ROT#(11)=12.2
'
Dim RANDRSL(255),MM(255)
'
Trap Lib Open 1,"mathtrans.library",0
If Not Errtrap Then MATH=True Else MATH=False
Trap Lib Open 2,"bsdsocket.library",0
If Not Errtrap Then BSD=True Else BSD=False
Dreg(1)=Doscall(-60)
If Dreg(1)
   I=0
   Repeat 
      Read T$
      T$=Space$(4)+T$+_NEWLINE$
      Dreg(2)=Varptr(T$) : Dreg(3)=Len(T$) : N=Doscall(-48)
      Inc I
   Until I>18
End If 
On Error Proc TR4P
'
PROGDIR$=Dir$ : PPROGDIR$=PROGDIR$
MACRODIR$="macros" : SEQDIR$="sequences" : CFGFILE$="fxbox.cfg"
FXBOX_REXX$="FXBOX_REXX" : OCTAMED_REXX$="OCTAMED_REXX"
'
Def Fn DIALZONE=CX>ROTX(_DIAL)-14 and CX<ROTX2(_DIAL) and CY>ROTY(_DIAL)-9 and CY<ROTY2(_DIAL)
Def Fn KEY_D=C=34
Def Fn KEY_SPACE=C=64
Def Fn KEY_BACKSPACE=C=65
Def Fn KEY_RETURN=C=68
Def Fn KEY_ESC=C=69
Def Fn KEY_DEL=C=70
Def Fn KEY_UPCUR=C=76
Def Fn KEY_DOWNCUR=C=77
Def Fn KEY_RIGHTCUR=C=78
Def Fn KEY_LEFTCUR=C=79
Def Fn KEY_SHIFT=S=1 or S=5
Def Fn KEY_CAPSLOCK=S=4
Def Fn KEY_LEFTAMIGA=S=64 or S=68
Def Fn KEY_RIGHTAMIGA=S=128 or S=132
Def Fn KEY_LEFTAMIGAALT=S=80 or S=84
Def Fn KEY_RIGHTAMIGAALT=S=160 or S=164
Def Fn CHUNKY(C)=Int(Int(C/_TIMEBEAT#)*_TIMEBEAT#)
Def Fn BUFFER=Free-5
Def Fn MEMORY=Fast Free+Chip Free
Def Fn A500=_AMIVER<3
Def Fn A1200=_AMIVER<5
Def Fn WANDZOOM=XD>0 or XD2<SCR_W
Def Fn MONO=_MODE=1
Def Fn STEREO=_MODE=2
Def Fn QUAD=_MODE=3
Def Fn STEREOORQUAD= Fn STEREO or Fn QUAD
Def Fn MULTI=_MODE=4
Def Fn MULTIOFF=_MODE-4
'
Gosub _INIT
If Prg State=-1 Then Break Off 
'
'
'
'***************************************************************************** 
'*main************************************************************************ 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'    .        :    :::.      :::  ::.    :::.
'    ;;,.    ;;;   ;;`;;     ;;;  ;;;;,  `;;;
'    [[[[, ,[[[[, ,[[ '[[,   [[[   [[[[[. '[[
'    $$$$$$$$"$$$c$$$cc$$$c  $$$,  $$$ "Y$c$$
'   888 Y88" 888o 888   888,o88888888    Y88 
'   MM  M'  "MMMM YMM   ""` MMM"MMMM     YM
'
'
'
Do 
   Gosub GVAL
   If C>0
      Gosub KCHOOSE
   Else If M=1
      Rem mouse zone does not allow 640  
      If CX=SCR_W
         If CY>HDR_H and CY<WVE_H+HDR_H
            Z=73
         End If 
      End If 
      If CALC
         If CX>CAL_X and CX<CAL_X+CAL_W and CY>CAL_Y and CY<CAL_Y+CAL_H
            Z=0 : Rem protect range beneath              
            Gosub CAL_DRW
         End If 
      End If 
      If Z>0
         Gosub BCHOOSE
      End If 
      Gosub DRWDIALS
   Else If M=2
      Gosub DRWMENU
   Else If _MENUON
      Gosub DRWMENUC
   Else If PZ-Z
      Gosub ZCHOOSE
   End If 
   If _CHOICE Then Gosub MCHOOSE
   '
   '********************
   If REXX
      If Arexx
         T$=Upper$(Arexx$(0))
         Arexx Answer 0,Lower$(_OK$)
         MACRO=True
         Gosub CMD
         MACRO=False
      End If 
   End If 
   '********************
   '
   If Not PLYING
      If Not STPPED
         _COL=_PANE
         BZN=1 : Gosub ZBTN
         BZN=2 : Gosub ZBTN
         BZN=3 : Gosub ZBTN
         STPPED=True : BGPLYING=False : RNGEPLY=False : DISPPLY=False
         Gosub METROC
         ' save final buffer
         If _REC
            Add BSIZE,-LEFTOVER(1)
            Gosub REC
            Add BSIZE,LEFTOVER(1)
         End If 
      End If 
   End If 
Loop 
'
'
'
'***************************************************************************** 
'*play************************************************************************ 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'   ::::::::.   :::      :::.     -:.     ::-.:    
'   `;;```.;;;  ;;;      ;;`;;    ';;.   ;;;;''    
'   `]]nnn]]'   [[[     ,[[ '[[,     '[[,[[['      
'    $$$""      $$'    c$$$cc$$$c      c$$"        
'    888o     o88oo,.__ 888   888,   ,8P"`         
'    YMMMb    """YUMMMM YMM   ""`   mM"            
'
'  
'
_PLAYRANGE:
If RL=0 Then Goto FLSHSCR
STA=RST : If RL=1 Then FI=P_LENGTH Else FI=P_LENGTH-RFIN
If Not PLYING
   Gosub CLRPOS
Else If DISPPLY
   _COL=_PANE
   BZN=3 : Gosub ZBTN
End If 
DISPPLY=False
RNGEPLY=True
Goto _PLAY
'
_PLAYDISPLAY:
STA=ST : FI=P_LENGTH-FIN
If STA>0 and FI>0
   If Fn STEREO
      STA=STA/2
      FI=FI/2
   Else If Fn QUAD
      STA=STA/4
      FI=FI/4
   End If 
End If 
If PLYING and RNGEPLY
   _COL=_PANE
   BZN=2 : Gosub ZBTN
End If 
DISPPLY=True
RNGEPLY=False
Goto _PLAY
'
_PLAYALL:
STA=0 : FI=P_LENGTH
_COL=_PANE
If RNGEPLY
   RNGEPLY=False
   BZN=2 : Gosub ZBTN
End If 
If DISPPLY
   DISPPLY=False
   BZN=3 : Gosub ZBTN
End If 
PWC=1
Repeat 
   OFFSET(PWC)=0
   LEFTOVER(PWC)=0
   Inc PWC
Until PWC>4
'
_PLAY:
If Not PLYING Then PLYTIME=Timer
BGPLYING=True : STPPED=False
If Fn MULTI
   If B<5
      OFFSET(B)=STA
   End If 
Else 
   OFFSET(1)=STA
End If 
PWC=1
Gosub CLRPLY
Repeat 
   TIME(PWC)=0
   Inc PWC
Until PWC>4
If _TOAST$=_$ Then _TOAST$=_LP$
If Not PLYING
   Gosub CLRPOS
   PLYING=True
   BBUFF=Max(BSIZE,257)
   Sam Loop On : TGL=True : _STARTPLAY=True
   Gosub PLYING
End If 
Gosub PLYBTN
Gosub POSCAL
If STA+FI=0 Then FI=P_LENGTH
Return 
'
_REWIND:
If Fn MULTI
   If B<5
      OFFSET(B)=0
   End If 
Else 
   OFFSET(1)=0
End If 
BEAT=0 : SBEAT=0 : PBEAT=0
Return 
'  
_REWINDALL:
PB=B
B=4
Repeat 
   Gosub _REWIND
   Dec B
Until B<1
B=PB
Return 
'
_STOP:
Every 2 Gosub N0TPLYING
_FULLSTOP:
Sam Loop Off 
If BGPLYING Then Gosub NUDGEPOS
PLYING=False
STPTIME=Timer
If OCTAMED and SETTING$(23)<>_FALSE$ Then Exec RXDIR$+' "address '+OCTAMED_REXX$+' PL_STOP"'
If NETWORK and BSD
   MSG$="/t_stop"+_NULL$+_COMMA$+String$(_NULL$,23)
   I=1
   Repeat 
      Gosub SOCK_SENDSYNC
      Inc I
   Until I>10
End If 
Ink _PANE
Goto DRWLEV
'
PLYING:
If TGL
   P_BUF=P_STARTB2 : P_SWP=P_STARTB1 : TGL=False
Else 
   P_BUF=P_STARTB1 : P_SWP=P_STARTB2 : TGL=True
End If 
If _STARTPLAY
   If Not _VST
      _STARTED=False
      Sam Raw PAULA(7),P_SWP,BBUFF,FREQ
      Goto PF
   End If 
End If 
If Fn MULTI
   If((RNGEPLY and RL) or DISPPLY)
      If B<5 and Length(B+7)>0
         B_START=Start(B+7)
         BBUFF=(BSIZE*(B))-BSIZE
         DID=B
         If OFFSET(B)>=FI or OFFSET(B)<STA
            If _LP
               OFFSET(B)=STA+LEFTOVER(B)
               LEFTOVER(B)=0
               _TOAST$=_LP$
            Else 
               Goto _STOP
            End If 
            Goto PB4
         Else 
            If OFFSET(B)+BSIZE>FI and OFFSET(B)-FI
               Copy B_START+OFFSET(B),B_START+FI To P_SWP+BBUFF
               LEFTOVER(B)=BSIZE-(FI-OFFSET(B))
               If _LP
                  Copy B_START+STA,B_START+STA+LEFTOVER(B) To P_SWP+BBUFF+(BSIZE-LEFTOVER(B))
               Else 
                  Fill P_SWP+BSIZE-LEFTOVER(1) To P_SWP+BSIZE,0
               End If 
            Else 
               PB4:
               Copy B_START+OFFSET(B),B_START+OFFSET(B)+BSIZE To P_SWP+BBUFF
            End If 
         End If 
      End If 
   End If 
   BBUFF=0
   SBCOUNT=1
   Repeat 
      B_LENGTH=Length(SBCOUNT+7)
      If B_LENGTH>0 and SBCOUNT-DID
         B_START=Start(SBCOUNT+7)
         If OFFSET(SBCOUNT)>=B_LENGTH
            Copy B_START+LEFTOVER(SBCOUNT),B_START+LEFTOVER(SBCOUNT)+BSIZE To P_SWP+BBUFF
            If _LP
               OFFSET(SBCOUNT)=LEFTOVER(SBCOUNT)
               LEFTOVER(SBCOUNT)=0
               _TOAST$=_LP$
            Else 
               Goto _STOP
            End If 
         Else 
            If OFFSET(SBCOUNT)+BSIZE>B_LENGTH
               Copy B_START+OFFSET(SBCOUNT),B_START+B_LENGTH To P_SWP+BBUFF
               LEFTOVER(SBCOUNT)=BSIZE-(B_LENGTH-OFFSET(SBCOUNT))
               If _LP
                  Copy B_START,B_START+LEFTOVER(SBCOUNT) To P_SWP+BBUFF+(BSIZE-LEFTOVER(SBCOUNT))
               Else 
                  Fill P_SWP+BSIZE-LEFTOVER(1) To P_SWP+BSIZE,0
               End If 
            Else 
               Copy B_START+OFFSET(SBCOUNT),B_START+OFFSET(SBCOUNT)+BSIZE To P_SWP+BBUFF
            End If 
         End If 
      End If 
      Add BBUFF,BSIZE
      Inc SBCOUNT
   Until SBCOUNT>4
   DID=0
Else If P_LENGTH>0
   If((RNGEPLY and RL) or DISPPLY)
      If(OFFSET(1)>=FI) or OFFSET(1)<STA
         If _LP
            OFFSET(1)=STA+LEFTOVER(1)
            LEFTOVER(1)=0
            _TOAST$=_LP$
         Else 
            Goto _STOP
         End If 
      Else 
         If(OFFSET(1)+BSIZE>FI) and OFFSET(1)-FI
            P_OFFSET=P_START+OFFSET(1)
            Copy P_OFFSET,P_START+FI To P_SWP
            LEFTOVER(1)=BSIZE-(FI-OFFSET(1))
            If _LP
               Copy P_START+STA,P_START+STA+LEFTOVER(1) To P_SWP+(BSIZE-LEFTOVER(1))
            Else 
               Fill P_SWP+BSIZE-LEFTOVER(1) To P_SWP+BSIZE,0
            End If 
            If Fn STEREOORQUAD
               Copy P_OFFSET+P_LENGTH,P_START+P_LENGTH+FI To P_SWP+BSIZE
               Copy P_START+STA+P_LENGTH,P_START+STA+P_LENGTH+LEFTOVER(1) To P_SWP+BSIZE+(BSIZE-LEFTOVER(1))
            End If 
            If Fn QUAD
               Copy P_OFFSET+P_LENGTH+P_LENGTH,P_START+P_LENGTH+P_LENGTH+FI To P_SWP+BSIZE+BSIZE
               Copy P_START+STA+P_LENGTH+P_LENGTH,P_START+STA+P_LENGTH+P_LENGTH+LEFTOVER(1) To P_SWP+BSIZE+BSIZE+(BSIZE-LEFTOVER(1))
               Copy P_OFFSET+P_LENGTH+P_LENGTH+P_LENGTH,P_START+P_LENGTH+P_LENGTH+P_LENGTH+FI To P_SWP+BSIZE+BSIZE+BSIZE
               Copy P_START+STA+P_LENGTH+P_LENGTH+P_LENGTH,P_START+STA+P_LENGTH+P_LENGTH+P_LENGTH+LEFTOVER(1) To P_SWP+BSIZE+BSIZE+BSIZE+(BSIZE-LEFTOVER(1))
            End If 
            Goto PF
         End If 
      End If 
   End If 
   If OFFSET(1)>=P_LENGTH
      If _LP
         OFFSET(1)=LEFTOVER(1)
         LEFTOVER(1)=0
         _TOAST$=_LP$
      Else 
         Goto _STOP
      End If 
      Goto PB
   Else 
      If(OFFSET(1)+BSIZE>P_LENGTH)
         P_OFFSET=P_START+OFFSET(1)
         Copy P_OFFSET,P_START+P_LENGTH To P_SWP
         LEFTOVER(1)=BSIZE-(P_LENGTH-OFFSET(1))
         If _LP
            Copy P_START,P_START+LEFTOVER(1) To P_SWP+(BSIZE-LEFTOVER(1))
         Else 
            Fill P_SWP+BSIZE-LEFTOVER(1) To P_SWP+BSIZE,0
         End If 
         If Fn STEREOORQUAD
            Copy P_OFFSET+P_LENGTH,P_START+P_LENGTH+P_LENGTH+LEFTOVER(1) To P_SWP+BSIZE
            Copy P_START+P_LENGTH,P_START+P_LENGTH+LEFTOVER(1) To P_SWP+BSIZE+(BSIZE-LEFTOVER(1))
         End If 
         If Fn QUAD
            Copy P_OFFSET+P_LENGTH+P_LENGTH,P_START+P_LENGTH+P_LENGTH+P_LENGTH+LEFTOVER(1) To P_SWP+BSIZE+BSIZE
            Copy P_START+P_LENGTH+P_LENGTH,P_START+P_LENGTH+P_LENGTH+LEFTOVER(1) To P_SWP+BSIZE+BSIZE+(BSIZE-LEFTOVER(1))
            Copy P_OFFSET+P_LENGTH+P_LENGTH+P_LENGTH,P_START+P_LENGTH+P_LENGTH+P_LENGTH+P_LENGTH+LEFTOVER(1) To P_SWP+BSIZE+BSIZE+BSIZE
            Copy P_START+P_LENGTH+P_LENGTH+P_LENGTH,P_START+P_LENGTH+P_LENGTH+P_LENGTH+LEFTOVER(1) To P_SWP+BSIZE+BSIZE+BSIZE+(BSIZE-LEFTOVER(1))
         End If 
      Else 
         PB:
         P_OFFSET=P_START+OFFSET(1)
         Copy P_OFFSET,P_OFFSET+BSIZE To P_SWP
         If Fn STEREOORQUAD
            Copy P_OFFSET+P_LENGTH,P_OFFSET+BSIZE+P_LENGTH To P_SWP+BSIZE
         End If 
         If Fn QUAD
            Copy P_OFFSET+P_LENGTH+P_LENGTH+P_LENGTH,P_OFFSET+BSIZE+P_LENGTH+P_LENGTH+P_LENGTH To P_SWP+BSIZE+BSIZE+BSIZE
            Copy P_OFFSET+P_LENGTH+P_LENGTH,P_OFFSET+BSIZE+P_LENGTH+P_LENGTH To P_SWP+BSIZE+BSIZE
         End If 
      End If 
   End If 
End If 
PF:
If _STARTPLAY
   _STARTPLAY=False
   Every 1 Gosub PLYING
   If _VST
      Gosub SOCK_SENDVSTUP
   End If 
   If OCTAMED and SETTING$(23)<>_FALSE$
      Exec RXDIR$+' "address '+OCTAMED_REXX$+' PL_PLAYBLOCK"'
   End If 
   If NETWORK
      PKTCOUNT=0
      MSG$="/t_start"+String$(_NULL$,4)+",iff"+String$(_NULL$,16)
      Gosub SOCK_SENDSYNC
      Mid$(MSG$,0,8)="/t_pulse"
      Loke Varptr(MSG$)+20,PKTCOUNT
   End If 
   If Not _VST
      If Fn MONO
         Sam Swap PAULA(7) To P_SWP,BSIZE
      Else If Fn STEREO
         Sam Swap PAULA(5) To P_SWP,BSIZE
         Sam Swap PAULA(6) To P_SWP+BSIZE,BSIZE
      Else 
         BBUFF=0 : SBCOUNT=1
         SBCOUNT=1
         Repeat 
            Sam Swap PAULA(SBCOUNT) To P_SWP+BBUFF,BSIZE
            Add BBUFF,BSIZE
            Inc SBCOUNT
         Until SBCOUNT>4
      End If 
   End If 
   Return 
End If 
If Not _VST
   If Fn MONO
      Sam Swap PAULA(7) To P_BUF,BSIZE
   Else If Fn STEREO
      Sam Swap PAULA(5) To P_BUF,BSIZE
      Sam Swap PAULA(6) To P_BUF+BSIZE,BSIZE
   Else 
      BBUFF=0 : SBCOUNT=1
      SBCOUNT=1
      Repeat 
         Sam Swap PAULA(SBCOUNT) To P_BUF+BBUFF,BSIZE
         Add BBUFF,BSIZE
         Inc SBCOUNT
      Until SBCOUNT>4
   End If 
End If 
If _STARTED
   Gosub POS
   Gosub SYNC
   If TGL
      Gosub LEV
   Else 
      Gosub PROBAR
      Gosub SCR0LL
      Gosub LEVC
   End If 
   If Fn MULTI
      SBCOUNT=1
      Repeat 
         OFFSET(SBCOUNT)=OFFSET(SBCOUNT)+BSIZE
         TIME(SBCOUNT)=TIME(SBCOUNT)+BSIZE
         Inc SBCOUNT
      Until SBCOUNT>4
   Else 
      OFFSET(1)=OFFSET(1)+BSIZE
      TIME(1)=TIME(1)+BSIZE
   End If 
   If _DEBUG
      If Not _VST
         SBCOUNT=0
         Repeat 
            If Sam Swapped(SBCOUNT)
               _TOAST$="µ" : Rem buffer underrun
               Exit 
            End If 
            Inc SBCOUNT
         Until SBCOUNT>3
      End If 
   End If 
   '  
   '********************
   If REALTIME
      If _PREVIEW
         SBCOUNT=1
         M_SWP=P_SWP
         Repeat 
            If CHAIN$(SBCOUNT)<>_$
               _CHANNEL=1
               If Fn STEREO
                  _CHANNEL=1
                  Repeat 
                     Gosub CHAIN$(SBCOUNT)
                     P_SWP=P_SWP+BSIZE
                     Inc _CHANNEL
                  Until _CHANNEL>2
               Else If Fn QUAD or( Fn MULTI and( Fn KEY_CAPSLOCK or S=5))
                  _CHANNEL=1
                  M_SWP=P_SWP
                  Repeat 
                     Gosub CHAIN$(SBCOUNT)
                     P_SWP=P_SWP+BSIZE
                     Inc _CHANNEL
                  Until _CHANNEL>4
               Else 
                  If Fn MULTI
                     P_SWP=P_SWP+(BSIZE*(B-1))
                  End If 
                  Gosub CHAIN$(SBCOUNT)
               End If 
            Else 
               Exit 
            End If 
            P_SWP=M_SWP
            Inc SBCOUNT
         Until SBCOUNT>_XC
      End If 
   End If 
   '******************* 
   '
   If _VST
      Gosub SOCK_SENDVST
   End If 
   If _REC
      Gosub REC
   End If 
Else 
   _STARTED=True
End If 
Gosub T0ASTWAITER
Every On 
Return 
'
N0TPLYING:
Gosub PROBAR
Gosub SCR0LL
Gosub T0ASTWAITER
Every On 
Return 
'
'
'
'***************************************************************************** 
'*position******************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'   ::::::::.     ...      .::::::.  :::  :::::::::: :::      ...     ::.    :::.
'   `;;```.;;; .;;;;;;;.  ;;;`    `; ;;;  ;;;;;;;''' ;;;   .;;;;;;;.  ;;;;,  `;;;
'   `]]nnn]]' ,[[     \[[,'[==/[[[[, [[[      [[     [[[  ,[[     \[[, [[[[[. '[[
'    $$$""    $$$,     $$$  '''    $ $$$,     $$     $$$, $$$,     $$$ $$$ "Y$c$$
'    888o     "888,_ _,88P,88b    dPo88888    88,   o88888"888,_ _,88P888    Y88 
'    YMMMb      "YMMMMMP"   "YMmMY" MMM"MM    MMM   MMM"MM  "YMMMMMP" MM     YM
'
'
'
POS:
If SETTING$(16)=_FALSE$ Then Return 
If _MENUDO Then Return 
If Fn MULTI
   POS_Y=HDR_H
   POS_H=(WVE_H/4)
   Gosub GFXSET
   POSCOUNT=1
   Repeat 
      If ADVPOS#(POSCOUNT)>0
         XP(POSCOUNT)=Int((OFFSET(POSCOUNT)+1)/ADVPOS#(POSCOUNT))-1
         If Fn WANDZOOM
            XP(POSCOUNT)=XP(POSCOUNT)-(ST/ADVPOS#(POSCOUNT))
         End If 
         If Not RENDERING
            If XP2(POSCOUNT)-XP(POSCOUNT)
               Ink _WHITE
               Gr Writing %10
               Draw XP(POSCOUNT),POS_Y To XP(POSCOUNT),POS_Y+POS_H
               If XP2(POSCOUNT)>-1
                  Ink _WHITE
                  Gr Writing %10
                  Draw XP2(POSCOUNT),POS_Y To XP2(POSCOUNT),POS_Y+POS_H
               End If 
               XP2(POSCOUNT)=XP(POSCOUNT)
            End If 
         End If 
      End If 
      POS_Y=POS_Y+POS_H
      Inc POSCOUNT
   Until POSCOUNT>4
   Gr Writing %1
   Gosub GFXRESTORE
Else 
   If ADVPOS#(1)>0 and OFFSET(1)>0
      If STA>0
         XP(1)=Int(OFFSET(1)/ADVPOS#(1))
         If Fn WANDZOOM
            If ST>0
               XP(1)=Int(XP(1)-(ST/ADVPOS#(1)))
            End If 
         Else 
            If STA>0 and RST>0
               XP(1)=Int(XP(1)-(STA/ADVPOS#(1))+(RST/ADVPOS#(1)))
            End If 
         End If 
      Else 
         If(OFFSET(1)-ST)>0
            XP(1)=Int(((OFFSET(1)-ST)/ADVPOS#(1)))
         Else 
            XP(1)=0
         End If 
      End If 
      If Not RENDERING
         If XP(1)-XP2(1)
            Gr Writing %10
            Draw XP(1),POS_T To XP(1),WVE_H+HDR_H-1
            Ink _WHITE
            If XP2(1)>-1
               Draw XP2(1),POS_T To XP2(1),WVE_H+HDR_H-1
            End If 
            Gr Writing %1
            XP2(1)=XP(1)
         End If 
      End If 
   End If 
End If 
Return 
'  
SYNC:
If CON#<=0 Then Return 
POSCOUNT=PBANK-7
If POSCOUNT<0
   POSCOUNT=1
Else If POSCOUNT>_MODE
   POSCOUNT=_MODE
End If 
POS#=TIME(POSCOUNT)/CON#
If SEQ$<>_$
   If Not _TEXT
      BEAT=Int(POS#/(SCR_W/_TIMEDIV#))
      If SBEAT-BEAT
         SBEAT=BEAT
         CL=Instr(SEQ$,_NEWLINE$)
         If CL>0
            T$=Left$(SEQ$,CL-1)
            SEQ$=Mid$(SEQ$,CL+1)
            If T$<>_$
               SEQUENCER=True : MACRO=True
               SET$=SETTING$(27)
               SETTING$(27)=_FALSE$
               Repeat 
                  CL=Instr(T$,_COMMA$)
                  If CL>0
                     D$=Mid$(T$,CL+1)
                     T$=Upper$(Left$(T$,CL-1))
                     Gosub CMD
                     T$=D$
                  End If 
               Until CL=0
               SETTING$(27)=SET$
               SEQUENCER=False : MACRO=False
            End If 
         End If 
      End If 
   End If 
End If 
If NETWORK
   BEAT=Int(POS#/13.3333)
   If Btst(0,BEAT)
      If NBEAT-BEAT
         Add PKTCOUNT,4
         Loke Varptr(MSG$)+20,PKTCOUNT
         Gosub SOCK_SENDSYNC
         NBEAT=BEAT
      End If 
   End If 
End If 
If RENDERING or _MENUDO Then Return 
If _METRO
   BEAT=Int(POS#/(SCR_W/(_TIMEDIV#/2)))
   If Btst(0,BEAT)
      If METRO
         Gosub METROC : METRO=False
      End If 
   Else If Not METRO
      Gosub METRO : METRO=True
   End If 
End If 
Return 
'
POSCAL:
P_LENGTH=Length(PBANK)
If P_LENGTH=0 Then Return 
P_START=Start(PBANK)
If _MODE<4
   If Fn STEREO
      P_LENGTH=P_LENGTH/2
   Else If Fn QUAD
      P_LENGTH=P_LENGTH/4
   End If 
   ADVPOS#(1)=(L-BSIZE)/SCR_W#
Else 
   PAC=1
   Repeat 
      ADVPOS#(PAC)=(D_LENGTH(PAC)-BSIZE-ST-FIN)/SCR_W#
      Inc PAC
   Until PAC>4
End If 
Return 
'
NUDGEPOS:
If Not BGPLYING Then Return 
Gosub GFXSET
If Fn MULTI
   NPSTEP=1
   NPY=HDR_H
   NPH=(WVE_H/4)
   NPCOUNT=1
   Repeat 
      Ink _WHITE
      Gr Writing %10
      Draw XP(NPCOUNT),NPY To XP(NPCOUNT),NPY+NPH
      NPY=NPY+NPH
      Inc NPCOUNT
   Until NPCOUNT>4
Else 
   Gr Writing %10
   Draw XP2(1),POS_T To XP2(1),WVE_H+HDR_H-1
End If 
Gr Writing %1
Gosub GFXRESTORE
Return 
'  
NUDGE4POS:
If Not BGPLYING Then Return 
If B<5
   Gosub GFXSET
   NPSTEP=1
   NPY=HDR_H
   NPH=(WVE_H/4)
   NPY=NPY+(NPH*(B-1))
   Ink _WHITE
   Gr Writing %10
   Draw XP(B),NPY To XP(B),NPY+NPH
   NPY=NPY+NPH
   Gr Writing %1
   Gosub GFXRESTORE
End If 
Return 
'
POSSET:
RNG_T=1
If Fn MULTI
   WVE_D=WVE_H/4
   If CY>HDR_H and CY<=HDR_H+WVE_D
      B=1
   Else If CY>HDR_H+WVE_D and CY<=HDR_H+WVE_D+WVE_D
      B=2
   Else If CY>HDR_H+WVE_D+WVE_D and CY<=HDR_H+WVE_D+WVE_D+WVE_D
      B=3
   Else If CY>HDR_H+WVE_D+WVE_D+WVE_D and CY<=HDR_H+WVE_H
      B=4
   End If 
End If 
Return 
'
CLRPOS:
D=1
Repeat 
   XP(D)=0 : XP2(D)=-1
   Inc D
Until D>4
Return 
'
CLR4POS:
If Fn MULTI and B<5
   XP(B)=0 : XP2(B)=-1
End If 
Return 
'
'  
'
'***************************************************************************** 
'*draw************************************************************************ 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'  
'   ::::::-.  :::::::..    :::.     .::    .   .:: 
'   ;;,   `';,;;;;``;;;;   ;;`;;    ';;,  ;;  ;;;' 
'   `[[     [[\[[[,/[[['  ,[[ '[[,   '[[, [[, [['  
'    $$,    $$"$$$$$$c   c$$$cc$$$c    Y$c$$$c$P   
'    888_,o8P',888b "88bo 888   888,    "88"888    
'   "MMMMP"`  "MMMM   "W" YMM   ""`      "M "M"    
'
'
'
_DRAWALL:
RENDERING=True
Gosub _DRAW
Gosub FRESHSEL
RENDERING=False
Return 
'
_SHOWALL:
Gosub SETPBANK
XD=0 : XD2=SCR_W : ST=0 : FIN=0 : L=0
Gosub CLRPOS
RENDERING=True
Gosub _DRAW
Gosub MAGICWAND
Gosub FRESHSEL
RENDERING=False
Gosub HEADER
Goto DRWTIME
'
_DRAW:
D=HDR_H+WVE_H
Repeat 
   Cls _PANE,0,D--_DSTEP To SCR_W,D+1
   Add D,_DSTEP
Until D<HDR_H-_DSTEP
Ink _SHADOW
WVE_D=HDR_H+(WVE_H/2)
Draw 0,WVE_D To SCR_W,WVE_D
If _MODE>1
   WVE_D=HDR_H+(WVE_H/4)
   DCY=WVE_D+(WVE_H/2)
   Draw 0,WVE_D To SCR_W,WVE_D
   Draw 0,DCY To SCR_W,DCY
End If 
If _MODE>2
   WVE_D=WVE_H/4
   DCY=-(WVE_H/8)
   D=1
   Repeat 
      Draw 0,HDR_H+WVE_D+DCY To SCR_W,HDR_H+WVE_D+DCY
      DCY=DCY+WVE_D
      Inc D
   Until D>4
End If 
If Fn MULTIOFF and P_LENGTH=0 Then Goto C0PYWAVE
Ink _WHITE
If SCR_H<256 Then DRWDIV=270 Else DRWDIV=192
On _MODE Goto D1,D2,D3,D4
D1:
DRWSUB=(127*128)/DRWDIV
DRWSUB2=-(DRWSUB*2)
If SCR_H>200 Then Add DRWSUB2,-2
WVE_D=HDR_H+(WVE_H/2)
ADV#=(P_LENGTH-ST-FIN)/SCR_W#
D_START(1)=P_START+ST
DN=(Peek(D_START(1))*128)/DRWDIV
If DN>DRWSUB Then Add DN,DRWSUB2
Gr Locate 0,DN+WVE_D
DI=0
Repeat 
   DN=Int(DI*ADV#)
   DN=(Peek(D_START(1)+DN)*128)/DRWDIV
   If DN>DRWSUB Then Add DN,DRWSUB2
   Draw To DI,DN+WVE_D
   Add DI,_STEP
Until DI>WVE_W
Goto C0PYWAVE
D2:
DRWDIV=(DRWDIV*2)-3
DRWSUB=(127*128)/DRWDIV
DRWSUB2=-DRWSUB*2
If SCR_H>200 Then Add DRWSUB2,-1
WVE_D=WVE_H/2
ADV#=(P_LENGTH-ST-FIN)/SCR_W#
D_START(1)=P_START+ST
D_START(2)=D_START(1)+P_LENGTH
DCY=HDR_H+(WVE_D/2)
DC=1
Repeat 
   DN=(Peek(D_START(DC))*128)/DRWDIV
   If DN>DRWSUB-2 Then Add DN,DRWSUB2
   Gr Locate 0,DN+DCY
   DI=1
   Repeat 
      DN=Int(DI*ADV#)
      DN=(Peek(D_START(DC)+DN)*128)/DRWDIV
      If DN>DRWSUB Then Add DN,DRWSUB2
      Draw To DI,DN+DCY
      Add DI,_STEP
   Until DI>WVE_W
   Add DCY,WVE_D
   Inc DC
Until DC>2
Goto C0PYWAVE
D3:
DRWDIV=(DRWDIV*4)+7
DRWSUB=(127*128)/DRWDIV
DRWSUB2=-(DRWSUB*2)-2
WVE_D=WVE_H/4
ADV#=(P_LENGTH-ST-FIN)/SCR_W#
DI=0
DC=1
Repeat 
   D_START(DC)=P_START+ST+DI
   Add DI,P_LENGTH
   Inc DC
Until DC>4
DCY=HDR_H+(WVE_D/2)
DC=1
Repeat 
   DN=(Peek(D_START(DC))*128)/DRWDIV
   If DN>DRWSUB Then Add DN,DRWSUB2
   Gr Locate 0,DN+DCY
   DI=1
   Repeat 
      DN=Int(DI*ADV#)
      DN=(Peek(D_START(DC)+DN)*128)/DRWDIV
      If DN>DRWSUB Then Add DN,DRWSUB2
      Draw To DI,DN+DCY
      Add DI,_STEP
   Until DI>WVE_W
   Add DCY,WVE_D
   Inc DC
Until DC>4
Goto C0PYWAVE
D4:
DRWDIV=(DRWDIV*4)+7
DRWSUB=(127*128)/DRWDIV
DRWSUB2=-(DRWSUB*2)-2
WVE_D=WVE_H/4
DC=1
Repeat 
   D_LENGTH(DC)=Length(DC+7)
   If D_LENGTH(DC)>0
      ADV#(DC)=(D_LENGTH(DC)-ST-FIN)/SCR_W#
      D_START(DC)=Start(DC+7)+ST
   Else 
      ADV#(DC)=0
   End If 
   Inc DC
Until DC>4
DCY=HDR_H+(WVE_D/2)
DC=1
Repeat 
   If ADV#(DC)>0
      DN=(Peek(D_START(DC)+1)*128)/DRWDIV
      If DN>DRWSUB
         Add DN,DRWSUB2
      End If 
      Gr Locate 0,DN+DCY
      DI=1
      Repeat 
         DN=Int(DI*ADV#(DC))
         DN=(Peek(D_START(DC)+DN)*128)/DRWDIV
         If DN>DRWSUB
            Add DN,DRWSUB2
         End If 
         Draw To DI,DN+DCY
         Add DI,_STEP
      Until DI>WVE_W
   End If 
   Add DCY,WVE_D
   Inc DC
Until DC>4
C0PYWAVE:
If M=0
   Gosub DRWDIV
   D=HDR_H+WVE_H
   Repeat 
      Screen Copy 0,0,D--_DSTEP,WVE_W,D To 1,0,(D--_DSTEP)-HDR_H
      Add D,_DSTEP
   Until D<HDR_H-_DSTEP
End If 
Return 
'
SH0WWAVE:
D=WVE_H
Repeat 
   Screen Copy 1,0,D--_DSTEP,WVE_W,D To 0,0,HDR_H+D--_DSTEP
   Add D,_DSTEP
Until D<0
If CALC Then Gosub CAL_BCK
If SCR_H<256 Then Goto CONTROLS
Return 
'
DRWTIME:
If RENDERING Then Return 
DRWTIMENOW:
T_LENGTH=P_LENGTH
Gosub TIME_ALL
Ink _SHADOW,_PANE
If DIV#>0
   If DIV#>9
      T$=Str$(DIV#)-_SPACE$+T$
   Else 
      T$=Str$(DIV#)+T$
   End If 
   If Len(T$)<16
      T$=Space$(22-Len(T$))+T$
   End If 
Else 
   T$=Space$(22)
End If 
Text 600-Text Length(T$),WVE_H+24+16,T$
If Len(T$)>22 : Rem mix loop button fix  
   _NUDGETIME=True
Else If _NUDGETIME
   _NUDGETIME=False
   Gosub LPMIXBTN
End If 
DRWHZ:
Ink _SHADOW,_PANE
T$=Str$(FREQ)+_SPACE$+_HZ$
Text 108-Text Length(T$),WVE_H+24+16,T$
If CALC Then Gosub CAL_BCK
Return 
'
HEADER:
H= Fn MEMORY
If H>1000000
   H=H/1048576
   T$=_MEGA$
Else 
   H=H/1024
   T$=_KILO$
End If 
T$="Free:"+Str$(H)-_SPACE$+T$
H=Max(0,10-Len(T$))
Ink _PANE
Bar 0,0 To SCR_W,HDR_H-1
Ink _SHADOW,_PANE
If Len(F$)<19+H Then Text 6,8,Str$(B)-_SPACE$+_COLON$+F$ Else Text 6,8,Str$(B)-_SPACE$+_COLON$+Left$(F$,17+H)+_DOTS$
H=H*8
Text SCR_W-83+H,8,T$
HEADERCORDS:
Ink _SHADOW,_PANE
T$=Str$(P_LENGTH)-_SPACE$
Text 186+H,8,"Size:"+T$+Space$(10-Len(T$))
T$=Str$(RST)-_SPACE$
Text 304+H,8,"Start:"+T$+Space$(10-Len(T$))
T$=Str$(RL)-_SPACE$
Text 430+H,8,"Range:"+T$+Space$(10-Len(T$))
Return 
'  
'
'
'***************************************************************************** 
'*zoom*wand******************************************************************* 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'   ::::::::     ...         ...      .        :   
'   `````;;;  .;;;;;;;.   .;;;;;;;.   ;;,.    ;;;  
'       .n[[',[[     \[[,,[[     \[[, [[[[, ,[[[[, 
'     ,$$P"  $$$,     $$$$$$,     $$$ $$$$$$$$"$$$ 
'   ,888bo,_ "888,_ _,88P"888,_ _,88P888 Y88" 888o 
'    `""*UMM   "YMMMMMP"   "YMMMMMP" MM  M'  "MMMM 
'
'
'
WHEELZOOM:
If SETTING$(22)=_FALSE$ Then Return 
If CY>HDR_H and CY<WND_H
   If P_LENGTH=0 or RENDERING
      Return 
   End If 
   If Fn MULTI and B<5
      _MIN=B
   Else 
      _MIN=1
   End If 
   _MIN=Int(ADVPOS#(_MIN)/2)
   _MIN=Min(10,_MIN) : _MIN=Max(1,_MIN)
   If C=122 : Rem zoom in
      If XD<XD2
         If XD2>XD+40
            XD=XD+Int((CX-XD)/_MIN)
            If XD2>0
               XD2=XD2-Int((XD2-CX)/_MIN)
            End If 
         Else If XD+2<XD2
            Add XD,1
            If XD2>0
               Add XD2,-1
            End If 
         End If 
      End If 
   Else If C=123 : Rem zoom out 
      If XD2>XD+40
         If CX>XD+40
            XD=XD-Int((CX-XD)/_MIN)
         Else 
            XD=XD-20
         End If 
         If XD2>CX+40
            XD2=XD2+Int((XD2-CX)/_MIN)
         Else 
            XD2=XD2+20
         End If 
      Else 
         Add XD,-1
         Add XD2,1
      End If 
      If XD<0
         XD=0
      End If 
      If XD2>SCR_W
         XD2=SCR_W
      End If 
   End If 
   Gosub KEY
   If C=0 : Rem check if still scrolling 
      Goto WREFRSH
   End If 
End If 
Return 
'
MAGICWAND:
If DISPPLY
   If ST+FIN=0
      STA=0 : FI=P_LENGTH
   Else 
      STA=ST : FI=P_LENGTH-FIN
   End If 
End If 
Gosub SETCORDS
Gosub SETZ0NES
If XD+43<XD2-43
   Cls _PANE,0,WND_Y To XD,WND_H
   Cls _WHITE,XD,WND_Y To XD+43,WND_H
   Cls _BLACK,XD+43,WND_Y To XD2-43,WND_H
   Cls _WHITE,XD2-43,WND_Y To XD2,WND_H
   Cls _PANE,XD2,WND_Y To SCR_W,WND_H
   Set Zone 70,XD,WND_Y To XD+43,WND_H
   Set Zone 72,XD+43,WND_Y To XD2-43,WND_H
   Set Zone 71,XD2-43,WND_Y To XD2,WND_H
   ZXD=XD
Else If XD2<SCR_W and XD<SCR_W-43-23-23
   Cls _PANE,0,WND_Y To XD,WND_H
   Cls _HIGHLIGHT,XD,WND_Y To XD+23,WND_H
   Cls _BLACK,XD+23,WND_Y To XD+23+43,WND_H
   Cls _HIGHLIGHT,XD+43+23,WND_Y To XD+43+23+23,WND_H
   Cls _PANE,XD+43+46,WND_Y To SCR_W,WND_H
   Set Zone 70,XD,WND_Y To XD+23,WND_H
   Set Zone 72,XD+23,WND_Y To XD+23+43,WND_H
   Set Zone 71,XD+43+23,WND_Y To XD+43+23+23,WND_H
   ZXD=XD
Else 
   Cls _PANE,0,WND_Y To SCR_W-43-23-23,WND_H
   Cls _HIGHLIGHT,SCR_W-43-23-23,WND_Y To SCR_W-43-23,WND_H
   Cls _BLACK,SCR_W-23-43,WND_Y To SCR_W-23,WND_H
   Cls _HIGHLIGHT,SCR_W-23,WND_Y To SCR_W,WND_H
   Set Zone 70,SCR_W-43-23-23,WND_Y To SCR_W-43-23,WND_H
   Set Zone 72,SCR_W-43-23,WND_Y To SCR_W-23,WND_H
   Set Zone 71,SCR_W-23,WND_Y To SCR_W,WND_H
   ZXD=XD
End If 
Return 
'  
LEFTWAND:
RENDERING=True
MX=CX : PXD=XD
_SLOW= Fn KEY_SHIFT
Repeat 
   PX=CX
   Gosub TESTMOVE
   Gosub KCHOOSE
   Exit If _SLOW<> Fn KEY_SHIFT
   If PX-CX
      Gosub LREFRESH
   Else If Fn KEY_RIGHTCUR
      Inc XD
      Gosub LRREFRSH
   Else If Fn KEY_LEFTCUR
      Dec XD
      Gosub LRREFRSH
   End If 
Until M=0
Goto AFTERWAND
'
LREFRESH:
If Fn KEY_SHIFT
   XD=PXD-((MX-CX)/3)
   Gosub SLOW
Else 
   XD=PXD-(MX-CX)
End If 
If XD<0 Then XD=0
If XD>SCR_W-2 Then XD=SCR_W-2
If XD>XD2-1 Then XD=XD2-1
Goto WANDDRAW
'
RIGHTWAND:
RENDERING=True
MX=CX : PXD2=XD2
_SLOW= Fn KEY_SHIFT
Repeat 
   PX=CX
   Gosub TESTMOVE
   Gosub KCHOOSE
   Exit If _SLOW<> Fn KEY_SHIFT
   If PX-CX
      Gosub RREFRESH
   Else If Fn KEY_RIGHTCUR
      Inc XD2
      Gosub LRREFRSH
   Else If Fn KEY_LEFTCUR
      Dec XD2
      Gosub LRREFRSH
   End If 
Until M=0
Goto AFTERWAND
'
RREFRESH:
If Fn KEY_SHIFT
   XD2=PXD2-((MX-CX)/3)
   Gosub SLOW
Else 
   XD2=PXD2-(MX-CX)
End If 
If XD2>SCR_W Then XD2=SCR_W
If XD2<1 Then XD2=1
If XD2<XD+1 Then XD2=XD+1
Goto WANDDRAW
'
MOVEWAND:
RENDERING=True
MX=CX : PXD=XD : PXD2=XD2
_SLOW= Fn KEY_SHIFT
Repeat 
   PX=CX
   Gosub TESTMOVE
   Gosub KCHOOSE
   Exit If _SLOW<> Fn KEY_SHIFT
   If PX-CX
      Gosub MREFRESH
   Else If Fn KEY_RIGHTCUR
      Inc XD : Inc XD2
      Gosub LRREFRSH
   Else If Fn KEY_LEFTCUR
      Dec XD : Dec XD2
      Gosub LRREFRSH
   End If 
Until M=0
AFTERWAND:
If _SLOW<> Fn KEY_SHIFT Then Return 
Gosub SETSELV
Gosub SETCORDS
Gosub SETZ0NES
Gosub _DRAW
Gosub FRESHSEL
Gosub CLRPOS
If CALC
   Gosub CAL_BCK
End If 
RENDERING=False
Return 
'
MREFRESH:
If Fn KEY_SHIFT
   XD=PXD-((MX-CX)/3)
   XD2=PXD2-((MX-CX)/3)
   Gosub SLOW
Else 
   XD=PXD-(MX-CX)
   XD2=PXD2-(MX-CX)
End If 
If XD2<1 Then XD2=1
LRREFRSH:
If XD<0 Then XD=0
If XD>SCR_W-2 Then XD=SCR_W-2
If XD2>SCR_W Then XD2=SCR_W
WANDDRAW:
Gosub MAGICWAND
If SETTING$(20)<>_FALSE$
   Gosub SETSELV
   Gosub SETCORDS
   Gosub _DRAW
   Gosub DRWMARKS
End If 
Return 
'
WREFRSH:
Gosub MAGICWAND
RENDERING=True
Goto AFTERWAND
'  
SETCORDS:
If CON#>0
   If RL>0 and RST>0
      XR=Int((RST/CON#)-(ST/CON#))
      If RL=1
         XR2=XR
      Else 
         XR2=Int(SCR_W-((RFIN/CON#)-(FIN/CON#)))
      End If 
   Else If RST=0
      XR=0
      If RL>1
         XR2=Int(SCR_W-((RFIN/CON#)-(FIN/CON#)))
      Else 
         XR2=0
      End If 
   End If 
End If 
If XR<0
   XR=0
Else If XR>SCR_W
   XR=SCR_W
End If 
If XR2>SCR_W
   XR2=SCR_W
Else If XR2<0
   XR2=0
End If 
CON#=P_LENGTH/SCR_W#
ST=Int(XD*CON#)
FIN=Int((SCR_W-XD2)*CON#)
L=P_LENGTH-ST-FIN
Goto POSCAL
'
'
'
'***************************************************************************** 
'*range*********************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'   :::::::..    :::.     ::.    :::. .,-:::::/  .,::::::
'   ;;;;``;;;;   ;;`;;    ;;;;,  `;;;;;-'````'   ;;;;''''
'   \[[[,/[[['  ,[[ '[[,   [[[[[. '[[[[   [[[[[[/ [[cccc 
'   "$$$$$$c   c$$$cc$$$c  $$$ "Y$c$$$$c.    "$$  $$"""" 
'   ,888b "88bo 888   888,888    Y88 `Y8bo,,,o88  888oo,_
'   "MMMM   "W" YMM   ""` MM     YM    `'YMUP"YMM """"YUM
'
'
'
_RANGEALL:
Gosub SETPBANK
If FX_LENGTH=0 Then Goto FLSHSCR
Gosub PBANKLEN
STA=0 : RL=FI : ST=0 : RST=0 : RFIN=0 : XR=0 : XR2=0
If Fn MULTIOFF or(B<5 and Fn MULTI)
   Gosub SETCORDS
   RENDERING=True
   Gosub FRESHSEL
   If Fn MULTIOFF
      Gosub NUDGEPOS
   Else 
      Gosub NUDGE4POS
   End If 
   RENDERING=False
End If 
Gosub SETZ0NES
Goto HEADERCORDS
'
_RANGEDISPLAY:
Gosub SETPBANK
If FX_LENGTH=0 Then Goto FLSHSCR
If B>_CH and Fn MULTI Then Goto FLSHSCR
XR=0 : XR2=SCR_W
Gosub SETSELCORDS
STA=RST : FI=RST+RL
RENDERING=True
Gosub FRESHSEL
If Fn MULTI
   Gosub CLR4POS
Else 
   Gosub CLRPOS
End If 
RENDERING=False
Gosub SETZ0NES
Goto HEADERCORDS
'
_RANGENONE:
If RL=0 Then Return 
RENDERING=True
Gosub NUDGEPOS
If PASS=0
   If RNGEPLY
      STA=0 : ST=0 : FI=P_LENGTH
   End If 
   Gosub CLRRNG
   If Fn MULTI
      Gosub NUDGEPOS
   End If 
   XR=0 : XR2=0 : LXR=0 : LXR2=0
   RST=0 : RL=0 : RFIN=0
   Gosub HEADERCORDS
   Gosub SETZ0NES
   If Fn MULTIOFF
      Gosub NUDGEPOS
   End If 
   RENDERING=False
End If 
Inc PASS
Return 
'
_SHOWRANGE:
If RL<2 Then Goto FLSHSCR
Gosub SETPBANK
If FX_LENGTH=0 Then Goto FLSHSCR
ST=RST : FIN=RFIN
XD=ST/CON#
XD2=(P_LENGTH-FIN)/CON#
Gosub MAGICWAND
RENDERING=True
Goto AFTERWAND
Gosub CLRPOS
RENDERING=True
Gosub _DRAW
Gosub SETCORDS
Gosub FRESHSEL
RENDERING=False
Return 
'
RANGE:
Gosub NUDGEPOS
If Fn MULTI
   Gosub CLRPOS
   Gosub POSSET
   Gosub SETBANK
   Gosub DRWTIME
   If Fn MULTI
      If XP(B)>XR and XP(B)<XR2
         Gosub NUDGEPOS
      End If 
   End If 
Else 
   Gosub CLRRNG
End If 
RENDERING=True
MX=CX
If Fn KEY_SHIFT Then MX= Fn CHUNKY(MX)
XR=MX : LXR=XR
XR2=XR : LXR2=XR2
Gosub DRWRNG
RENDERING=True
Repeat 
   PX=CX
   Gosub TESTMOVE
   Gosub KCHOOSE
   If PX-CX
      Gosub SMREFRESH
   Else If Fn KEY_RIGHTCUR
      Inc XR2
      Gosub MKEYREFRSH
   Else If Fn KEY_LEFTCUR
      Dec XR2
      Gosub MKEYREFRSH
   End If 
Until M=0
Gosub XRSWAP
Gosub SETSELCORDS
Gosub SETZ0NES
If RL<=0 or XR=XR2 Then RL=1
If RNGEPLY and RL=1
   If Fn MULTI
      OFFSET(B)=RST
   Else 
      OFFSET(1)=RST
   End If 
   If XR=XR2
      STA=RST
      FI=P_LENGTH
   End If 
End If 
Gosub HEADERCORDS
Gosub CLRPOS
Gosub NUDGEPOS
RENDERING=False
Return 
'
R0LLLEFT:
If _MENUDO or RL<2 Then Return 
If XR2>XR+10
   Ink _HIGHLIGHT
   Bar XR+1,RNG_T+HDR_H To XR+10,RNG_B+HDR_H-1
End If 
Return 
'  
UNR0LLLEFT:
If _MENUDO or RL<2 Then Return 
If XR2-XR>12 and XR2>XR Then Screen Copy 1,XR+1,RNG_T-1,XR+HDR_H,RNG_B To 0,XR+1,RNG_T+HDR_H-1,%110000
If CALC and XR+12>CAL_X Then Gosub CAL_BCK
Return 
'
R0LLRIGHT:
If _MENUDO or RL<2 Then Return 
If XR2>10 and XR2>XR+10
   Ink _HIGHLIGHT
   Bar XR2-10,RNG_T+HDR_H To XR2-1,RNG_B+HDR_H-1
End If 
Return 
'
UNR0LLRIGHT:
If _MENUDO or RL<2 Then Return 
If XR2>12 and XR2>XR+12 Then Screen Copy 1,XR2-11,RNG_T-1,XR2,RNG_B To 0,XR2-11,RNG_T+HDR_H-1,%110000
If CALC and XR2>CAL_X Then Gosub CAL_BCK
Return 
'
SMREFRESH:
If Fn KEY_SHIFT
   CX= Fn CHUNKY(CX)
   If CX=MX
      Return 
   End If 
End If 
XR2=XR-(MX-CX)
MKEYREFRSH:
If XR2<0 Then XR2=0
If XR2>SCR_W Then XR2=SCR_W
Gosub SETSELCORDS
Goto DRWRNG
'
SELLEFT:
RENDERING=True
Gosub NUDGEPOS
Gosub UNR0LLLEFT
RENDERING=True
MX=CX
If Fn KEY_SHIFT Then MX= Fn CHUNKY(MX)
PXD=XR
LXR=XR
Repeat 
   PX=CX
   Gosub TESTMOVE
   Gosub KCHOOSE
   If PX-CX
      Gosub SLREFRESH
   Else If Fn KEY_RIGHTCUR
      Inc XR : Inc PXD
      Gosub LKEYREFRSH
   Else If Fn KEY_LEFTCUR
      Dec XR : Dec PXD
      Gosub LKEYREFRSH
   End If 
Until M=0
Gosub XRSWAP
Gosub R0LLLEFT
Gosub HEADERCORDS
Gosub SETZ0NES
Gosub CLRPOS
If Fn MULTIOFF
   If XP(1)=XP2(1)
      Gosub NUDGEPOS
   End If 
Else 
   Gosub NUDGEPOS
End If 
RENDERING=False
Return 
'  
SLREFRESH:
If Fn KEY_SHIFT
   CX= Fn CHUNKY(CX)
   If CX=MX
      Return 
   End If 
   PXD= Fn CHUNKY(PXD)
End If 
XR=PXD-(MX-CX)
LKEYREFRSH:
If XR<0 Then XR=0
If XR>SCR_W Then XR=SCR_W
Gosub SETLSELCORDS
Goto DRWRNGL
'
SELRIGHT:
RENDERING=True
Gosub NUDGEPOS
Gosub UNR0LLRIGHT
RENDERING=True
MX=CX
If Fn KEY_SHIFT Then MX= Fn CHUNKY(MX)
PXD=XR2
LXR=XR
LXR2=XR2
Repeat 
   PX=CX
   Gosub TESTMOVE
   Gosub KCHOOSE
   If PX-CX
      Gosub SRREFRESH
   Else If Fn KEY_RIGHTCUR
      Inc XR2 : Inc PXD
      Gosub RKEYREFRSH
   Else If Fn KEY_LEFTCUR
      Dec XR2 : Dec PXD
      Gosub RKEYREFRSH
   End If 
Until M=0
Gosub XRSWAP
Gosub R0LLRIGHT
Gosub HEADERCORDS
Gosub SETZ0NES
Gosub CLRPOS
If Fn MULTIOFF
   If XP(1)=XP2(1)
      Gosub NUDGEPOS
   End If 
Else 
   Gosub NUDGEPOS
End If 
RENDERING=False
Return 
'
SRREFRESH:
If Fn KEY_SHIFT
   CX= Fn CHUNKY(CX)
   If CX=MX
      Return 
   End If 
   PXD= Fn CHUNKY(PXD)
End If 
XR2=PXD-(MX-CX)
RKEYREFRSH:
If XR2<0 Then XR2=0
If XR2>SCR_W Then XR2=SCR_W
Gosub SETRSELCORDS
Goto DRWRNG
'
FRESHSEL:
If RL=0 Then Return 
If XR=XR2 or XR+1=XR2 Then Goto DRWMARK
D=RNG_B
Repeat 
   Screen Copy 1,XR,D--_DSTEP,XR2,D To 0,XR,HDR_H+(D--_DSTEP),%110000
   Add D,_DSTEP
Until D<RNG_T
DRWMARKS:
Ink _BG
Draw XR,RNG_T+HDR_H To XR,RNG_B+HDR_H-1
DRWMARK:
Ink _BG
Draw XR2,RNG_T+HDR_H To XR2,RNG_B+HDR_H-1
Return 
'
DRWRNG:
If RNGEPLY
   If RST+RFIN=0
      FI=P_LENGTH
   Else 
      STA=RST
      FI=P_LENGTH-RFIN
   End If 
End If 
If XR>XR2
   If LXR2>XR
      D=RNG_B
      Repeat 
         Screen Copy 1,XR,D--_DSTEP,LXR2+1,D To 0,XR,HDR_H+(D--_DSTEP)
         Add D,_DSTEP
      Until D<RNG_T
      LXR2=XR
   End If 
   If XR2<LXR
      D=RNG_B
      Repeat 
         Screen Copy 1,XR2,D--_DSTEP,LXR+1,D To 0,XR2,HDR_H+(D--_DSTEP),%110000
         Add D,_DSTEP
      Until D<RNG_T
      LXR=XR2
   End If 
   If XR2>LXR
      D=RNG_B
      Repeat 
         Screen Copy 1,LXR,D--_DSTEP,XR2,D To 0,LXR,HDR_H+(D--_DSTEP)
         Add D,_DSTEP
      Until D<RNG_T
      LXR=XR2
   End If 
Else 
   If LXR<XR
      D=RNG_B
      Repeat 
         Screen Copy 1,LXR,D--_DSTEP,XR,D To 0,LXR,HDR_H+(D--_DSTEP)
         Add D,_DSTEP
      Until D<RNG_T
      LXR=XR
   End If 
   If XR2>LXR2
      D=RNG_B
      Repeat 
         Screen Copy 1,LXR2,D--_DSTEP,XR2,D To 0,LXR2,HDR_H+(D--_DSTEP),%110000
         Add D,_DSTEP
      Until D<RNG_T
      LXR2=XR2
   End If 
   If XR2<LXR2
      D=RNG_B
      Repeat 
         Screen Copy 1,XR2,D--_DSTEP,LXR2+1,D To 0,XR2,HDR_H+(D--_DSTEP)
         Add D,_DSTEP
      Until D<RNG_T
      LXR2=XR2
   End If 
End If 
Gosub DRWMARKS
Gosub SETZ0NES
If CALC and RL>1 and M=0
   Gosub CAL_BCK
   RENDERING=True
End If 
Goto HEADERCORDS
'
DRWRNGL:
If RNGEPLY
   If RST+RFIN=0
      FI=P_LENGTH
   Else 
      STA=RST
      FI=P_LENGTH-RFIN
   End If 
End If 
If XR>XR2
   If LXR<XR2
      D=RNG_B
      Repeat 
         Screen Copy 1,LXR,D--_DSTEP,XR2,D To 0,LXR,HDR_H+(D--_DSTEP)
         Add D,_DSTEP
      Until D<RNG_T
      LXR=XR2
   End If 
   If XR>LXR2
      D=RNG_B
      Repeat 
         Screen Copy 1,LXR2,D--_DSTEP,XR,D To 0,LXR2,HDR_H+(D--_DSTEP),%11000
         Add D,_DSTEP
      Until D<RNG_T
      LXR2=XR
   End If 
   If XR<LXR2
      D=RNG_B
      Repeat 
         Screen Copy 1,XR,D--_DSTEP,LXR2+1,D To 0,XR,HDR_H+(D--_DSTEP)
         Add D,_DSTEP
      Until D<RNG_T
      LXR2=XR
   End If 
Else 
   If LXR2>XR2
      D=RNG_B
      Repeat 
         Screen Copy 1,XR2,D--_DSTEP,LXR2+1,D To 0,XR2,HDR_H+(D--_DSTEP)
         Add D,_DSTEP
      Until D<RNG_T
      LXR2=XR2
   End If 
   If XR>LXR
      D=RNG_B
      Repeat 
         Screen Copy 1,LXR,D--_DSTEP,XR,D To 0,LXR,HDR_H+(D--_DSTEP)
         Add D,_DSTEP
      Until D<RNG_T
      LXR=XR
   End If 
   If XR<LXR
      D=RNG_B
      Repeat 
         Screen Copy 1,XR,D--_DSTEP,LXR+1,D To 0,XR,HDR_H+(D--_DSTEP),%110000
         Add D,_DSTEP
      Until D<RNG_T
      LXR=XR
   End If 
End If 
Gosub DRWMARKS
Gosub SETZ0NES
If CALC and RL>1 and M=0
   Gosub CAL_BCK
   RENDERING=True
End If 
Goto HEADERCORDS
'
CLRRNG:
D=RNG_B
Repeat 
   Screen Copy 1,XR,D--_DSTEP,XR2+1,D To 0,XR,HDR_H+(D--_DSTEP)
   Add D,_DSTEP
Until D<RNG_T
Return 
'
XRSWAP:
If XR>XR2
   XR=XR2 xor XR : XR2=XR2 xor XR : XR=XR xor XR2
   Gosub SETSELCORDS
End If 
Return 
'
SETSELV:
P_LENGTH=Length(PBANK)
If Fn STEREO
   P_LENGTH=P_LENGTH/2
Else If Fn QUAD
   P_LENGTH=P_LENGTH/4
End If 
If L>0 Then CON#=L/SCR_W# Else CON#=P_LENGTH/SCR_W#
Return 
'
SETSELCORDS:
Gosub SETSELV
RST#=XR*CON#
RFIN#=(SCR_W-XR2)*CON#
CON#=P_LENGTH/SCR_W#
ST#=XD*CON#
FIN#=(SCR_W-XD2)*CON#
RST#=ST#+RST#
RST=Int(RST#)
ST=Int(ST#)
FIN=Int(FIN#)
L=P_LENGTH-ST-FIN
RFIN#=FIN#+RFIN#
RFIN=Int(RFIN#)
If RFIN<0 Then RFIN=0
RL=P_LENGTH-RST-RFIN
Return 
'
SETLSELCORDS:
Gosub SETSELV
RST#=XR*CON#
CON#=P_LENGTH/SCR_W#
ST#=XD*CON#
RST#=ST#+RST#
RST=Int(RST#)
ST=Int(ST#)
L=P_LENGTH-ST-FIN
If XR=0 and XR=XR2 Then RL=0 Else RL=P_LENGTH-RST-RFIN
Return 
'
SETRSELCORDS:
Gosub SETSELV
RFIN#=(SCR_W-XR2)*CON#
CON#=P_LENGTH/SCR_W#
ST#=XD*CON#
FIN#=(SCR_W-XD2)*CON#
FIN=Int(FIN#)
L=P_LENGTH-ST-FIN
RFIN#=FIN#+RFIN#
RFIN=Int(RFIN#)
If RFIN<0 Then RFIN=0
If XR=0 and XR=XR2 Then RL=0 Else RL=P_LENGTH-RST-RFIN
Return 
'
BANKSET:
If Fn MULTI
   If B=1
      RNG_T=1 : RNG_B=WVE_D
   Else If B=2
      RNG_T=WVE_D+1 : RNG_B=WVE_D+WVE_D
   Else If B=3
      RNG_T=WVE_D+WVE_D+1 : RNG_B=WVE_D+WVE_D+WVE_D
   Else If B=4
      RNG_T=WVE_D+WVE_D+WVE_D+1 : RNG_B=WVE_H
   End If 
Else 
   RNG_T=0 : RNG_B=WVE_H
End If 
Return 
'
_RANGESET:
PRST=RST
PRFIN=RFIN
PRL=RL
Return 
'
_RANGERESTORE:
RST=PRST
RFIN=PRFIN
RL=PRL
Return 
'
'
'
'***************************************************************************** 
'*file************************************************************************ 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'    .-:::::' :::    :::    .,:::::: 
'    ;;;'''', ;;;    ;;;    ;;;;'''' 
'    [[[,,==[ [[[    [[[     [[cccc  
'    `$$$"``" $$$,   $$'     $$""""  
'   _ 888    o88888o88oo,.__ 888oo,_ 
'   MM"MM,   MMM"MM"""YUMMMM """"YUM 
'
'
'
_NEW:
BTXT1$="New" : TXT1$="Clear the current" : TXT2$="waveform?" : Gosub _REQUEST
If Not REQ Then Return 
_ERASE:
If B=UB Then Gosub _CLEARUNDO
If Fn MULTI Then Gosub CLRPLY Else Gosub _STOP
Erase PBANK
F$=_$ : Gosub SETFILENAME
XD=0 : XD2=SCR_W : XR=0 : XR2=0 : ST=0 : FIN=0 : RST=0 : RFIN=0 : RL=0 : STA=0 : FI=0
Gosub SETPBANK
RENDERING=True
Gosub _DRAW
RENDERING=False
Gosub HEADER
Gosub MAGICWAND
Return 
'
_OPEN:
PB=B
_APPEND=B-1 : Rem always insert
WTXT$="Open"
Goto _OPENP
_OPENA:
WTXT$="Append"
_OPENP:
WTXT$=WTXT$+": raw iff aif wav cat abk med"
PF$=F$ : F$=LF$ : _RST=0 : _RL=0
Gosub FREQUEST
If Not FRQ
   Swap B,PB
   If B-PB
      Gosub SETBANK
      PF$=F$
   End If 
   F$=PF$
   Return 
End If 
_OPENF:
If Not Exist(F$)
   Gosub CANTFIND
   F$=PF$
   Return 
End If 
RENDERING=True : _TEXT=True
If B=UB Then Gosub _CLEARUNDO
_CHANNELS=0 : _RATE=0 : _TAG=0 : _BITS=0
BANKNAME$=_$ : _CAT=False : _ABK=False : _MMD=False
_IFF=False : _WAV=False : _FLOAT=False : _INTERLEAVED=True
_NAME$=_$ : _CPYR$=_$ : _AUTH$=_$ : _ANNO$=_$ : _UNTY=$10000
Trap Open In 1,F$
If Errtrap Then Goto FILEERROR
LF=Lof(1)
If LF=0
   F$=PF$
   Close 1
   Return 
End If 
Gosub SETFILENAME
FH$=Input$(1,4)
If FH$="FORM"
   _IFF=True
Else If FH$="RIFF"
   _WAV=True
Else If FH$="CAT "
   _CAT=True
Else If FH$="AmBk"
   _ABK=True
Else If Mid$(FH$,0,3)="MMD"
   _MMD=True
End If 
If _MMD
   BANKNAME$=F$ : NN=0 : N=B : PB=PBANK
   Pof(1)=8
   S$=Input$(1,4) : P=Leek(Varptr(S$)) : Rem song pointer 
   Pof(1)=P+504
   S$=Input$(1,2) : NUMBLOCKS=Deek(Varptr(S$)) : Rem numblocks
   Pof(1)=32
   S$=Input$(1,4) : P=Leek(Varptr(S$)) : Rem exp pointer
   Pof(1)=P+20
   S$=Input$(1,4) : P=Leek(Varptr(S$)) : Rem iinfo pointer  
   S$=Input$(1,2) : ENTRIES=Deek(Varptr(S$)) : Rem entries
   S$=Input$(1,2) : ENTSIZE=Deek(Varptr(S$)) : Rem entrsz   
   Pof(1)=P
   For I=1 To ENTRIES
      S$=Input$(1,ENTSIZE)
      P=Peek(Varptr(S$))
      If P>0
         For I2=1 To Text Length(S$)
            CN=Peek(Varptr(S$)+I2)
            Exit If CN=0
         Next I2
         FILES$(I)=Mid$(S$,0,I2)
      Else 
         FILES$(I)=_UNNAMED$
      End If 
   Next I
   Pof(1)=24
   S$=Input$(1,4) : DP=Leek(Varptr(S$)) : Rem pointer to smplarr pointer 
   FX_START=0
   FX_LENGTH=Lof(1)
   For I=1 To ENTRIES
      Pof(1)=DP : S$=Input$(1,4) : DP=Pof(1) : P=Leek(Varptr(S$))
      If P>0
         B=N
         Pof(1)=P
         S$=Input$(1,4) : SZ=Leek(Varptr(S$)) : Rem sample length
         S$=Input$(1,2) : T=Deek(Varptr(S$)) : Rem sample type
         If T=16
            T=0 : Rem 16bit mono 
            _BITS=16
         Else If T=32
            T=0 : Add SZ,SZ : Rem stereo 
            _BITS=8
         Else If T=48
            T=0 : Add SZ,SZ : Rem 16bit stereo 
            _BITS=16
         Else 
            _BITS=8
         End If 
         If T=0 and SZ>0
            Trap Reserve As Work 7+N,SZ
            If Errtrap
               Close 1
               F$=_$
               Goto NOMEMORY
            End If 
            AC=Start(7+N)
            Sload 1 To AC,SZ
            FX=FX+SZ
            F$=FILES$(I)
            Gosub SETFILENAME
            Gosub SETMETA
            If(PL/2)*2<PL : Rem odd size file
               S$=Input$(1,1)
            End If 
            If _BITS=16
               PBANK=B+7
               Gosub _16BITTO8BIT
            End If 
            Inc N
            Inc NN
         End If 
      End If 
   Next I
   FX=FXE
   If SETTING$(28)<>_FALSE$ : Rem more magic 
      Pof(1)=16
      S$=Input$(1,4) : DP=Leek(Varptr(S$)) : Rem pointer to blockarr pointer      
      For I=1 To NUMBLOCKS
         Pof(1)=DP : S$=Input$(1,4) : DP=Pof(1) : P=Leek(Varptr(S$))
         Pof(1)=P
         S$=Input$(1,2) : TRACKS=Deek(Varptr(S$))
         S$=Input$(1,2) : LINES=Deek(Varptr(S$)) : Inc LINES
         S$=Input$(1,4) : Rem blockinfo pointer
         SZ=(TRACKS*4)*LINES
         Print 
         If SZ>0
            S$=Input$(1,SZ)
            NCUT=0
            For N=0 To Len(S$)-1 Step 4
               CN=Varptr(S$) : Add CN,N
               'xnnnnnnn xxiiiiii cccccccc dddddddd 
               _NOTE=Peek(CN) : Inc CN : Rem 0 - $7F
               _INST=Peek(CN) : Inc CN : Rem 0 - $3F
               _CMND=Peek(CN) : Inc CN : Rem 0 - $FF
               _DATA=Peek(CN) : Rem 0 - $FF
               If _NOTE>0
                  CN=_NOTE/12
                  CN2=_NOTE-(CN*12)
                  If CN2=0
                     Dec _NOTE : Dec CN
                  End If 
                  LINE$=LINE$+CHROM$(CN2)
                  LINE$=LINE$+Str$(CN+1)-_SPACE$ : Rem octave 
               Else 
                  LINE$=LINE$+"---"
               End If 
               If _INST<32
                  LINE$=LINE$+_SPACE$
               Else 
                  LINE$=LINE$+"1"
                  Add _INST,-32
               End If 
               If _INST<10
                  LINE$=LINE$+(Str$(_INST)-_SPACE$)
               Else 
                  LINE$=LINE$+(Chr$(_INST-10+65)-_SPACE$)
               End If 
               If _CMND<16
                  LINE$=LINE$+"0"
               End If 
               LINE$=LINE$+(Hex$(_CMND)-"$")
               If _DATA<16
                  LINE$=LINE$+"0"
               End If 
               LINE$=LINE$+(Hex$(_DATA)-"$")
               Inc NCUT
               If NCUT=TRACKS
                  Print Left$(LINE$,72) : Rem 8 channels 
                  LINE$=_$
                  NCUT=0
               End If 
            Next N
         End If 
      Next I
   End If 
   Close 1
   If Fn MULTIOFF
      Bank Swap 5,8+_APPEND
   End If 
   Gosub CLRFILES
   PBANK=PB : B=1+_APPEND : PB=B : F$=FB$(B)
   If Not MACRO
      TXT1$="Octamed module loaded" : TXT2$="with"+Str$(NN)+" samples" : Gosub _NOTICE
   End If 
   If SETTING$(28)<>_FALSE$
      Gosub CONTROLS
   End If 
   _TEXT=True
Else If _ABK
   BANKNAME$=F$
   If LF<22
      Close 1
      F$=PF$
      TXT1$="Amos bank invalid" : TXT2$=_$ : Goto _ERROR
   End If 
   S$=Input$(1,4) : Rem (1,2) 5 (1,2) 0
   S$=Input$(1,4)
   If( Fn MEMORY)<(Leek(Varptr(S$)) xor $80000000)
      Close 1
      F$=PF$
      Goto NOMEMORY
   End If 
   S$=Input$(1,8)
   If S$<>"Samples "
      Close 1
      F$=PF$
      TXT1$="Not a sample bank" : TXT2$=_$ : Goto _ERROR
   End If 
   S$=Input$(1,2) : NN=Deek(Varptr(S$))
   If Eof(1)
      Close 1
      F$=PF$
      TXT1$="Empty sample bank" : TXT2$=_$ : Goto _NOTICE
   End If 
   SF$=Input$(1,4) : SL=Leek(Varptr(SF$)) : If SL>6 : SF$=SF$+Input$(1,SL-6) : End If 
   FX=0 : FX_START=0 : FX_LENGTH=Lof(1) : FXE=FX_LENGTH
   For N=1 To NN
      If Leek(Varptr(SF$)+(N-1)*4)
         If Eof(1)
            TXT1$="Error loading"+Str$(N)+Str$(NN) : TXT2$="bank, continuing..." : Gosub _ERROR
            _TEXT=True
            Exit 
         End If 
         S$=Input$(1,14)
         For CHR=1 To 7
            If Peek(Varptr(S$)+CHR)=0
               Exit 
            End If 
         Next CHR
         F$=Left$(S$,CHR)
         B=N+_APPEND : Gosub SETFILENAME
         Gosub SETMETA
         If _APPEND=0
            FREQ=Deek(Varptr(S$)+8)
         End If 
         SZ=Leek(Varptr(S$)+10)
         If SZ mod 2=1
            SZ=SZ+1
         End If 
         If SZ>0
            Trap Reserve As Work 7+N+_APPEND,SZ
            If Errtrap
               Close 1
               F$=_$
               Goto NOMEMORY
            End If 
            AC=Start(7+N+_APPEND)
            P=0
            Sload 1 To AC,SZ
            FX=FX+SZ
         End If 
      End If 
   Next 
   FX=FXE
   Close 1
   If Fn MULTIOFF
      Bank Swap PBANK,8+_APPEND
   End If 
   B=1+_APPEND : PB=B : F$=FB$(B)
   If _APPEND=0
      Gosub SETHZ
      Gosub DIALMAIN
   End If 
   If Not MACRO
      TXT1$="Amos Bank loaded" : TXT2$="with"+Str$(NN)+" samples" : Gosub _NOTICE
   End If 
   _TEXT=True
Else If _CAT
   BANKNAME$=F$
   N=B : PB=PBANK : NN=0
   S$=Input$(1,4)
   SL=Leek(Varptr(S$))
   SZ=12
   Repeat 
      PBANK=7+N
      Trap Reserve As Work PBANK,1024
      If Errtrap
         Close 1
         F$=_$
         Goto NOMEMORY
      End If 
      Pof(1)=SZ
      Sload 1 To PBANK,1024
      _IFF=True
      Gosub PARSEHEADER
      If _IFF
         S_START=S_START-Start(PBANK)
         Trap Reserve As Work PBANK,S_LENGTH
         If Errtrap
            Close 1
            F$=_$
            Goto NOMEMORY
         End If 
         Pof(1)=S_START+SZ
         Sload 1 To PBANK,S_LENGTH
         B=N : F$=_NAME$ : Gosub SETFILENAME
         Gosub SETMETA
         If _APPEND=0
            FREQ=_RATE
         End If 
         If _TAG=1
            If _CHANNELS>1
               _MODE=2
            End If 
            Gosub SETPBANK
            RST=0 : RL=P_LENGTH : PASS=0
            Gosub _DECODEFIBONACCIDELTA
            RL=0
            _TAG=0
         End If 
         _IFF=False
         Inc N : Inc NN
      Else 
         Erase PBANK
      End If 
      SZ=SZ+F_LENGTH
   Until SZ>=SL
   Close 1
   PBANK=PB
   If Fn MULTIOFF
      Bank Swap 5,8+_APPEND
   End If 
   B=1+_APPEND : PB=1+_APPEND : F$=FB$(1+_APPEND)
   If _APPEND=0
      Gosub SETHZ
      Gosub DIALMAIN
   End If 
   If Not MACRO
      TXT1$="Iff CAT loaded" : TXT2$="with"+Str$(NN)+" samples" : Gosub _NOTICE
   End If 
   _TEXT=True
Else 
   If Fn MULTI and _APPEND>0
      PBANK=_APPEND+8
   End If 
   Trap Reserve As Work PBANK,1024
   If Errtrap
      Close 1
      F$=_$
      Goto NOMEMORY
   End If 
   Pof(1)=0
   Sload 1 To PBANK,1024
   Gosub PARSEHEADER
   Gosub SETMETA
   S_START=S_START-Start(PBANK)
   If S_LENGTH=0
      S_LENGTH=LF-S_START
   End If 
   Trap Reserve As Work PBANK,S_LENGTH
   If Errtrap
      Close 1
      F$=_$
      Goto NOMEMORY
   End If 
   Pof(1)=S_START
   Sload 1 To PBANK,S_LENGTH
   Close 1
   B=1+_APPEND : PB=1+_APPEND : F$=FB$(1+_APPEND)
   _TOAST$=Str$(_BITS)-_SPACE$+"bit "+_TOAST$
   If _RATE>0
      _TOAST$=_TOAST$+Str$(_RATE)+_HZ$+_SPACE$
   Else 
      _TOAST$="8bit Raw PCM "
   End If 
   Gosub T0AST
End If 
S$=_$
PMACRO=MACRO
MACRO=True
If _IFF
   If _BITS>8
      Gosub _STOP
   Else If _TAG=1
      _TOAST$=_TOAST$+"Fibonacci Delta "
   End If 
   If _BITS=16
      Gosub _16BITTO8BIT
   Else If _BITS=24
      Gosub _24BITTO8BIT
   Else If _BITS=32 and _FLOAT
      Gosub _32BITTO8BIT
   End If 
Else If _WAV
   Gosub _STOP
   If _BITS=8
      Gosub _8BITUNSIGNEDTO8BIT
   Else If _BITS=16
      Gosub _16BITTO8BITFLIP
   Else If _BITS=24
      Gosub _24BITTO8BITFLIP
   Else If _BITS=32 and _FLOAT
      Gosub _32BITTO8BITFLIP
   Else If _TAG=0
      TXT1$="Encoding not" : TXT2$="decoded" : Gosub _NOTICE
      _CHANNELS=1
   End If 
End If 
_MODEP=_MODE
If _CHANNELS>1
   If _INTERLEAVED
      Gosub _DEINTERLEAVE
   End If 
   If _CHANNELS>2
      If _INTERLEAVED
         Gosub _DEINTERLEAVE
      End If 
      If Fn MULTIOFF
         _MODE=3
      End If 
   Else If Fn MULTIOFF or _TAG=1 : Rem fibonacci delta
      _MODE=2
   End If 
Else If(_ABK or _CAT) and _APPEND=0
   _MODE=4
   Gosub R4INSET
Else If _CHANNELS=1 and Fn MULTIOFF
   _MODE=1
End If 
MACRO=PMACRO
PASS=0
XD=0 : XD2=SCR_W : XR=0 : XR2=0 : ST=0 : FIN=0 : RST=0 : RFIN=0 : RL=0
STA=0 : Gosub PBANKLEN
RENDERING=False
If _MODEP-_MODE
   _M3SEL=_MODE
   _MODE=_MODEP
   Gosub M0DEMENU
   Gosub SETZ0NES
   Gosub HEADER
Else 
   Gosub _SHOWALL
End If 
Gosub SETPBANK
If Fn MULTI
   Gosub SETBANK
   Gosub POSCAL
End If 
If _IFF
   If _TAG=1 : Rem fibonacci delta
      RST=0 : RL=P_LENGTH : PASS=0
      Gosub _DECODEFIBONACCIDELTA
      RL=0
      Gosub _SHOWALL
   End If 
   If HI_REPEAT>0
      RST=_RST
      RL=_RL
      Gosub _SHOWALL
      Gosub FRESHSEL
   End If 
End If 
COMP(B)=0
ROT#(1)=(VOL/13.3333)+8.5 : Gosub SETVOL
_TEXT=False
Return 
'
_OPENASRAW:
WTXT$="Open As: raw"
PF$=F$ : F$=_$
Gosub FREQUEST
If Not FRQ
   F$=PF$
   Return 
Else If Not Exist(F$)
   Gosub CANTFIND
   F$=PF$
   Return 
End If 
Gosub SETFILENAME
Gosub _STOP
If B=UB Then Gosub _CLEARUNDO
Trap Open In 1,F$
If Errtrap Then Goto FILEERROR
LF=Lof(1)
If LF=0
   Close 1
   Return 
End If 
XD=0 : XD2=SCR_W : XR=0 : XR2=0 : ST=0 : FIN=0 : RST=0 : RFIN=0 : RL=0 : STA=0 : FI=0
FH$=Input$(1,4)
_IFF=False : _WAV=False
_NAME$=_$ : _CPYR$=_$ : _AUTH$=_$ : _ANNO$=_$ : _UNTY=0
If FH$="FORM"
   _IFF=True
Else If FH$="RIFF"
   _WAV=True
End If 
Trap Reserve As Work PBANK,1024
If Errtrap
   Close 1
   F$=_$
   Goto NOMEMORY
End If 
_TEXT=True
Pof(1)=0
Sload 1 To PBANK,1024
Gosub PARSEHEADER
If _IFF
   S_START=Hunt(H_POS To H_LENGTH,"BODY")
   Add S_START,4 : S_LENGTH=Leek(S_START) : Add S_START,4
End If 
S_START=S_START-Start(PBANK)
If S_LENGTH=0
   S_LENGTH=LF-S_START
End If 
Trap Reserve As Work PBANK,S_LENGTH
If Errtrap
   Close 1
   F$=_$
   Goto NOMEMORY
End If 
Pof(1)=S_START
Sload 1 To PBANK,S_LENGTH
Close 1
TXT1$=Str$(_BITS)-" "+"bit "+_TOAST$
If _BITS>0
   TXT2$=Str$(_RATE)+_HZ$
End If 
If _CHANNELS=1
   TXT2$="Mono"+TXT2$
Else If _CHANNELS=2
   TXT2$="Stereo"+TXT2$
Else If _CHANNELS=4
   TXT2$="Quad"+_TXT2$
End If 
If _BITS>0
   _TOAST$="Header Removed "
   Gosub _NOTICE
   If _MODE-1
      _M3SEL=1
      Gosub M0DEMENU
   End If 
Else 
   _TOAST$="8bit Raw PCM "
End If 
Gosub _SHOWALL
COMP(B)=0
_TEXT=False
Return 
'
_APPEND:
PB=B
_APPEND=B-1
If Fn MULTI Then PBANK=B+7
N=Length(PBANK)
While N>0
   Inc B
   Inc _APPEND
   N=Length(B+7)
Wend 
If PB-B
   N=PB : Gosub SETBANK : PB=N
End If 
Goto _OPENA
'  
_REVERT:
If F$=_$ and BANKNAME$=_$ Then Goto FLSHSCR
BTXT1$=_OK$ : TXT1$="Are you sure?" : TXT2$="You will loose any edits" : Gosub _REQUEST
If Not REQ Then Return 
If FB$(B)=_UNNAMED$ or FPATH$(B)=_$ Then Goto FLSHSCR
If Fn A500 Then Gosub _STOP
If BANKNAME$<>_$ Then F$=BANKNAME$
PPROGDIR$=Dir$
Dir$=FPATH$(B)
_APPEND=B-1
Gosub _OPENF
Dir$=PPROGDIR$
Gosub SETPBANK
Return 
'
_SAVEASIFF:
_ASIFF=True
WTXT$="iff"
Goto _SAVE
_SAVEAS:
_ASIFF=False
WTXT$="raw"
_SAVE:
If Length(PBANK)=0 Then Goto FLSHSCR
_TEXT=True
PF$=F$
WTXT$="Save As: "+WTXT$
Gosub FREQUEST
If Not FRQ
   F$=PF$
   Return 
End If 
Gosub CLEANFILENAME
If Exist(F$)
   Gosub OVERWRITE
   If Not REQ
      F$=PF$
      Return 
   End If 
End If 
Trap Open Out 1,F$
If Errtrap
   F$=PF$
   Goto FILEERROR
End If 
If _ASIFF
   S_LENGTH=Length(PBANK)
   Gosub _IFFHEADER
   Ssave 1,Varptr(T$) To Varptr(T$)+Len(T$)
End If 
Ssave 1,Start(PBANK) To Start(PBANK)+Length(PBANK)
If _PAD : Rem odd sizes have a pad byte
   Ssave 1,Varptr(_NULL$) To Varptr(_NULL$)+1
End If 
Close 1
Gosub SETFILENAME
Gosub HEADER
_TEXT=False
Return 
'
_SAVEASCAT:
_TEXT=True
PF$=F$
If BANKNAME$<>_$ Then F$=BANKNAME$ Else F$="samples.cat"
WTXT$="Save As: cat"
If Right$(Upper$(F$),4)=".ABK" Then Right$(F$,4)=".cat"
Gosub FREQUEST
If Not FRQ
   F$=PF$
   Return 
End If 
Gosub CLEANFILENAME
If Right$(Upper$(F$),4)<>".CAT" Then F$=F$+".cat"
If Exist(F$)
   Gosub OVERWRITE
   If Not REQ
      F$=PF$
      Return 
   End If 
End If 
BANKNAME$=F$
If Fn MULTIOFF
   Bank Swap PBANK,B+7
End If 
PB=B : B=1 : NN=0
Trap Open Out 1,F$
If Errtrap Then Goto FILEERROR
T$="CAT     8SVX"
Ssave 1,Varptr(T$) To Varptr(T$)+12
F_LENGTH=0
S_LENGTH=Length(B+7)
Repeat 
   Gosub _IFFHEADER
   Ssave 1,Varptr(T$) To Varptr(T$)+Len(T$)
   Ssave 1,Start(B+7) To Start(B+7)+S_LENGTH
   If _PAD : Rem odd sizes have a pad byte
      Ssave 1,Varptr(_NULL$) To Varptr(_NULL$)+1
   End If 
   F_LENGTH=F_LENGTH+S_LENGTH+Len(T$)
   Inc B : Inc NN
   S_LENGTH=Length(B+7)
Until S_LENGTH=0
Add F_LENGTH,4
Pof(1)=4
Ssave 1,Varptr(F_LENGTH) To Varptr(F_LENGTH)+4
Close 1
If Fn MULTIOFF and Length(PBANK)=0 Then Bank Swap PBANK,PB+7
F$=PF$ : B=PB
TXT1$="Iff Cat saved" : TXT2$="with"+Str$(NN)+" samples" : Gosub _NOTICE
Return 
'
_SAVEASBANK:
_TEXT=True
PF$=F$
If BANKNAME$<>_$ Then F$=BANKNAME$ Else F$="samples.abk"
WTXT$="Save As: abk"
If Right$(Upper$(F$),4)=".CAT" Then Right$(F$,4)=".abk"
Gosub FREQUEST
If Not FRQ
   F$=PF$
   Return 
End If 
Gosub CLEANFILENAME
If Exist(F$)
   Gosub OVERWRITE
   If Not REQ
      F$=PF$
      Return 
   End If 
End If 
If Fn MULTIOFF
   Bank Swap PBANK,B+7
End If 
PB=B : B=1
'***************************************************************************** 
'   Sample Bank Maker by François Lionet, AMOS version by P.J.Hickman    
'   © Copyright 1990 Mandarin Software 
'
ODD=False
For FIN=1 To 248
   S_LENGTH=Length(FIN+7)
   If(S_LENGTH/2)*2<S_LENGTH
      ODD=True
   End If 
   Exit If S_LENGTH=0
Next FIN
If ODD
   BTXT1$=_OK$ : TXT1$="Ignore the detected" : TXT2$="odd size?" : Gosub _REQUEST
   If Not REQ
      F$=PF$
      Return 
   End If 
End If 
TL=0 : TN=0
For N=1 To FIN
   If Length(7+N)>0
      TL=TL+Length(7+N)+4+14
      Inc TN
   End If 
Next 
S_LENGTH=TL+22
Trap Reserve As Work WBANK,S_LENGTH
If Errtrap Then Goto NOMEMORY
AD=Start(WBANK)
T$="AmBk"
For I=1 To Len(T$)
   Poke AD+I-1,Asc(Mid$(T$,I,1))
Next I
AD=AD+4
Doke AD,5
AD=AD+2
Doke AD,0
AD=AD+2
Loke AD,(TL+8) or $80000000
AD=AD+4
T$="Samples "
For I=1 To Len(T$)
   Poke AD+I-1,Asc(Mid$(T$,I,1))
Next I
AD=AD+8
ACALC=AD
Doke AD,TN
AD=AD+2
AOFF=AD
APOKE=AOFF+TN*4
NN=0
For N=1 To 248
   WEIGHTING=0
   If Length(7+N)>0
      Inc NN
      Loke AOFF,APOKE-ACALC
      AOFF=AOFF+4
      If FB$(N)=_$
         T$=Left$(_UNNAMED$,8)
      Else 
         T$=Left$(FB$(N),8)
      End If 
      AD=APOKE
      For I=1 To Len(T$)
         Poke AD+I-1,Asc(Mid$(T$,I,1))
      Next I
      Doke APOKE+8,FREQ
      Loke APOKE+10,Length(7+N)
      APOKE=APOKE+14
      A_START=Start(7+N)
      PP=Varptr(P)
      For I=0 To Length(7+N)
         P=Peek(A_START+I)+WEIGHTING
         Poke APOKE+I,Peek(PP+3)
      Next I
      APOKE=APOKE+Length(7+N)
      If Btst(0,APOKE)
         Inc APOKE
      End If 
   End If 
Next N
'
'***************************************************************************** 
If Right$(Upper$(F$),4)<>".ABK" Then F$=F$+".abk"
BANKNAME$=F$
Trap Open Out 1,F$
If Errtrap
   F$=PF$
   Goto FILEERROR
End If 
Ssave 1,Start(WBANK) To Start(WBANK)+S_LENGTH
Close 1
If Fn MULTIOFF and Length(PBANK)=0 Then Bank Swap PBANK,PB+7
F$=PF$ : B=PB
Gosub _CLEARWORK
Gosub SETBANK
TXT1$="Amos Bank saved" : TXT2$="with"+Str$(NN)+" samples" : Gosub _NOTICE
Return 
'
_PROPERTIES:
_TEXT=True
WTXT$=S$ : WX=149 : WY=28 : WX2=490 : WY2=127 : _COL=_WHITE : Gosub WIN
Ink _BLACK,_WHITE
Text 272,56,"Name: "+NAME$(B)
Text 288,64,"(c) "+CPYR$(B)
Text 256,72,"Author: "+AUTH$(B)
Text 224,80,"Annotation: "+ANNO$(B)
Text 240,88,"Channels:"+Str$(_CHANNELS)
Text 216,96,"Compression:"
If COMP(B)=0
   Text 320,96,"none"
Else If COMP(B)=1
   Text 320,96,"fibonacci delta"
End If 
Gosub WINWAITC
_TEXT=False
Return 
'
_QUIT:
If _STOP
   _STOP=False
   Return 
End If 
If OSCILLO and _REPEAT$<>"_QUIT" Then Goto _TGLOSCILLOSCOPE
BTXT1$="Quit" : TXT1$="Are you sure" : TXT2$="you want to quit?" : Gosub _REQUEST
If Not REQ Then Return 
¯QUIT:
Gosub _FULLSTOP
Every Off 
Erase Temp 
I=1
Repeat 
   Trap Close I
   Inc I
Until I>7
Rainbow Del 0
If REXX Then Arexx Close 
If MATH Then Lib Close 1
If BSD
   Gosub SOCK_CLOSESYNC
   Lib Close 2
End If 
Trap Dir$=PROGDIR$
End 
'
_IFFHEADER:
T$="FORM"+_CHUNK$+"8SVXVHDR"+String$(_NULL$,24)
CN=Varptr(T$)+16
Loke CN,20 : Add CN,4
If COMP(B)=1
   If _CHANNELS=1
      FF_LENGTH=(S_LENGTH-2)*2
   Else If _CHANNELS=2
      FF_LENGTH=S_LENGTH-4
   Else If _CHANNELS=4
      FF_LENGTH=(S_LENGTH-8)/2
   End If 
Else 
   FF_LENGTH=S_LENGTH
End If 
Loke CN,FF_LENGTH : Add CN,4 : Rem oneShotHiSamples   
Loke CN,0 : Add CN,4 : Rem repeatHiSamples
Loke CN,32 : Add CN,4 : Rem samplesPerHiCycle
Doke CN,FREQ : Add CN,2 : Rem samplesPerSec
Poke CN,1 : Inc CN : Rem octave 
Poke CN,COMP(B) : Inc CN : Rem compression
Loke CN,VOL*1024 : Rem unity $00010000 $0000FFFF  
If NAME$(B)=_$
   S$=FB$(B)
Else 
   S$=NAME$(B)
End If 
Gosub PROPLEN
_NAME$="NAME"+_CHUNK$+S$+_CHUNK$
Loke Varptr(_NAME$)+4,Len(_NAME$)-8
If CPYR$(B)<>_$
   S$=CPYR$(B)
   Gosub PROPLEN
   _CPYR$="(c) "+_CHUNK$+S$+_CHUNK$
   Loke Varptr(_CPYR$)+4,Len(_CPYR$)-8
Else 
   _CPYR$=_$
End If 
If AUTH$(B)<>_$
   S$=AUTH$(B)
   Gosub PROPLEN
   _AUTH$="AUTH"+_CHUNK$+S$+_CHUNK$
   Loke Varptr(_AUTH$)+4,Len(_AUTH$)-8
Else 
   _AUTH$=_$
End If 
If ANNO$(B)<>_$
   S$=ANNO$(B)
Else 
   S$="FxBox"
End If 
Gosub PROPLEN
_ANNO$="ANNO"+_CHUNK$+S$+_CHUNK$
Loke Varptr(_ANNO$)+4,Len(_ANNO$)-8
If Fn STEREOORQUAD
   _CHAN$="CHAN"+_CHUNK$+_CHUNK$
   Loke Varptr(_CHAN$)+4,4
   If Fn STEREO
      Loke Varptr(_CHAN$)+8,6 : Rem STEREO 6L  
   Else If Fn QUAD
      Loke Varptr(_CHAN$)+8,30 : Rem QUAD 30L ea-iff-85.CHAN does not define quad     
   End If 
Else 
   _CHAN$=_$
End If 
_BODY$="BODY"+_CHUNK$
If(S_LENGTH/2)*2<S_LENGTH Then _PAD=1 Else _PAD=0 : Rem odd sizes have a pad byte  
Loke Varptr(_BODY$)+4,S_LENGTH+_PAD
T$=T$+_NAME$+_CPYR$+_AUTH$+_ANNO$+_CHAN$+_BODY$
Loke Varptr(T$)+4,S_LENGTH+_PAD+Len(T$)
Return 
'
PROPLEN:
H_LENGTH=Len(S$)
If(H_LENGTH/2)*2<H_LENGTH
   S$=S$+Chr$(0)
End If 
Return 
'
PARSEHEADER:
_CHANNELS=1
S_START=Start(PBANK) : S_LENGTH=0 : H_LENGTH=Start(PBANK)+Length(PBANK)
If _IFF
   H_POS=S_START
   S_START=Hunt(S_START To H_LENGTH,"FORM")
   If S_START=0
      S_START=H_POS
      _IFF=False
      Return 
   End If 
   Add S_START,4 : F_LENGTH=Leek(S_START) : Add S_START,4
   If F_LENGTH<Length(PBANK)
      H_LENGTH=S_START+F_LENGTH
   End If 
   H_POS=S_START
   S_START=Hunt(S_START To H_LENGTH,"8SVX")
   If S_START=0
      S_START=H_POS
      S_START=Hunt(S_START To H_LENGTH,"16SV")
      If S_START=0
         S_START=H_POS
         S_START=Hunt(S_START To H_LENGTH,"24SV")
         If S_START=0
            S_START=H_POS
            S_START=Hunt(S_START To H_LENGTH,"IESV")
            If S_START>0
               _BITS=32
               _FLOAT=True
            End If 
         Else 
            _BITS=24
         End If 
      Else 
         _BITS=16
      End If 
   Else 
      _BITS=8
   End If 
   If S_START>0
      T$="VHDR" : _FULLCHUNK=True : Gosub PARSECHUNK
      CN=Varptr(T$)
      HI_LENGTH=Leek(CN) : Add CN,4 : Rem oneShotHiSamples 
      HI_REPEAT=Leek(CN) : Add CN,8 : Rem repeatHiSamples
      _RATE=Deek(CN) : Add CN,2
      If _RATE=30464
         _RATE=96000
         Ink _PANE : Gosub AMIHZDIAL : _HIGHDIAL=False
         _AMIHZ=False
      End If 
      _OCTAVE=Peek(CN) : Inc CN
      _TAG=Peek(CN) : Inc CN
      _UNTY=Leek(CN)
      T$="NAME" : Gosub PARSECHUNK : _NAME$=T$
      T$="(c) " : Gosub PARSECHUNK : _CPYR$=T$
      T$="AUTH" : Gosub PARSECHUNK : _AUTH$=T$
      T$="ANNO" : Gosub PARSECHUNK : _ANNO$=T$
      T$="CHAN" : _FULLCHUNK=True : Gosub PARSECHUNK
      If T$<>_$
         _CHANNELS=Leek(Varptr(T$))
         If _CHANNELS=4 or _CHANNELS=2 : Rem RIGHT 4L LEFT 2L 
            _CHANNELS=1
         Else If _CHANNELS=6 : Rem STEREO 6L  
            _CHANNELS=2
            _INTERLEAVED=False
         Else If _CHANNELS=30 : Rem QUAD 30L 
            _CHANNELS=4
            _INTERLEAVED=False
         End If 
      Else 
         S_START=H_POS
      End If 
      H_POS=S_START
      S_START=Hunt(S_START To H_LENGTH,"BODY") : Add S_START,4
      If _OCTAVE>3
         S_START=S_START+((HI_LENGTH+HI_REPEAT)*3)
         _RST=HI_LENGTH*4
         HI_LENGTH=(HI_LENGTH+HI_REPEAT)*4
         _RL=HI_LENGTH-_RST
      Else If _OCTAVE>1
         S_START=S_START+HI_LENGTH+HI_REPEAT
         _RST=HI_LENGTH*2
         HI_LENGTH=(HI_LENGTH+HI_REPEAT)*2
         _RL=HI_LENGTH-_RST
      Else 
         _RST=HI_LENGTH
         HI_LENGTH=HI_LENGTH+HI_REPEAT
         _RL=HI_LENGTH-_RST
      End If 
      If _TAG=1
         If _CHANNELS=1
            HI_LENGTH=(HI_LENGTH/2)+2
         Else If _CHANNELS=2
            HI_LENGTH=HI_LENGTH+4
         Else If _CHANNELS=4
            HI_LENGTH=(HI_LENGTH*2)+8
         End If 
      Else If _CHANNELS=2
         HI_LENGTH=HI_LENGTH*2
      Else If _CHANNELS=4
         HI_LENGTH=HI_LENGTH*4
      End If 
      If HI_LENGTH>0
         S_LENGTH=HI_LENGTH
      Else 
         S_LENGTH=Leek(S_START)
      End If 
      Add S_START,4
      _TOAST$="Iff"
   Else 
      _IFF=False
      S_START=H_POS
      S_START=Hunt(S_START To H_LENGTH,"AIFF")
      If S_START=0
         Add H_POS,4
         S_START=H_POS
         H_LENGTH=Leek(S_START)
         Add H_LENGTH,8
         S_LENGTH=H_LENGTH
         _IFF=False
         Return 
      End If 
      T$="COMM" : _FULLCHUNK=True : Gosub PARSECHUNK
      _CHANNELS=Deek(Varptr(T$))
      _BITS=Deek(Varptr(T$)+6)
      _RATE=Deek(Varptr(T$)+10)
      T$="IEEE" : Gosub PARSECHUNK
      If T$<>_$
         _TOAST$=_TOAST$+" Float"
         _FLOAT=True
      Else 
         S_START=H_POS
      End If 
      S_START=Hunt(S_START To H_LENGTH,"SSND")
      Add S_START,4 : S_LENGTH=Leek(S_START) : Add S_START,4
      _TOAST$="Aif"
   End If 
Else If _WAV
   S_START=Hunt(S_START To H_LENGTH,"fmt ")
   Add S_START,8 : _TAG=Peek(S_START)
   If _TAG=3
      _FLOAT=True
      _TOAST$=_TOAST$+" Float"
   Else If _TAG-1
      _TOAST$=_TOAST$+Str$(_TAG)
      _TAG=0
   End If 
   Add S_START,2 : _CHANNELS=Peek(S_START) : Add S_START,2
   Poke Varptr(_RATE)+3,Peek(S_START) : Inc S_START
   Poke Varptr(_RATE)+2,Peek(S_START) : Inc S_START
   Poke Varptr(_RATE)+1,Peek(S_START) : Inc S_START
   Poke Varptr(_RATE),Peek(S_START) : Inc S_START
   Add S_START,6 : _BITS=Peek(S_START)
   S_START=Hunt(S_START To H_LENGTH,"data") : Add S_START,4
   Poke Varptr(S_LENGTH)+3,Peek(S_START) : Inc S_START
   Poke Varptr(S_LENGTH)+2,Peek(S_START) : Inc S_START
   Poke Varptr(S_LENGTH)+1,Peek(S_START) : Inc S_START
   Poke Varptr(S_LENGTH),Peek(S_START) : Inc S_START
   _TOAST$="Wav"
End If 
Return 
'
PARSECHUNK:
H_POS=S_START
S_START=Hunt(S_START To H_LENGTH,T$)
If S_START>0
   Add S_START,4 : C_LENGTH=Leek(S_START) : Add S_START,4
   T$=String$(_NULL$,C_LENGTH)
   Copy S_START,S_START+C_LENGTH To Varptr(T$)
   If Not _FULLCHUNK
      Repeat 
         Exit If Mid$(T$,C_LENGTH,1)<>Chr$(0)
         Dec C_LENGTH
      Until C_LENGTH<0
      If C_LENGTH>0
         T$=Left$(T$,C_LENGTH)
      Else 
         T$=_$
      End If 
   End If 
Else 
   T$=_$
   S_START=H_POS
End If 
_FULLCHUNK=False
Return 
'
SETMETA:
NAME$(B)=_NAME$
CPYR$(B)=_CPYR$
AUTH$(B)=_AUTH$
ANNO$(B)=_ANNO$
COMP(B)=_TAG
If _UNTY>0
   If _UNTY<65536
      VOL=_UNTY/1023
   Else 
      VOL=64
   End If 
End If 
Return 
'
SETFILENAMELENGTH:
Set Dir 105,_$
Return 
'
SETFILENAME:
FB$(B)=F$
If F$<>_UNNAMED$ Then FPATH$(B)=Dir$ Else FPATH$(B)=_$
If B<11
   Mid$(MENB$(B),7)=Space$(16)
   If F$<>_$
      Mid$(MENB$(B),7)=Left$(FB$(B),8)
   End If 
End If 
Return 
'
CLEANFILENAME:
I=Len(F$)
Repeat 
   Exit If Mid$(F$,I,1)<>_SPACE$
   Dec I
Until I<0
If I<1
   F$=_$
End If 
If I>0 and I<Len(F$)
   F$=Left$(F$,I)
End If 
Return 
'
'
'
'***************************************************************************** 
'*edit************************************************************************ 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'   .,::::::::::::-.   :::  :::::::::: 
'   ;;;;'''';;,   `';, ;;;  ;;;;;;;''' 
'    [[cccc `[[     [[ [[[      [[     
'    $$""""  $$,    $$ $$$,     $$     
'    888oo,_ 888_,o8P'o88888    88,    
'    """"YUM"MMMMP"`  MMM"MM    MMM  
'
'
'  
_CUT:
If RL=0 Then Goto FLSHSCR
Gosub _UNDOSET
Gosub _COPYSET
If RNGEPLY
   RNGEPLY=False
   STA=0
End If 
If RL=1 or RL=P_LENGTH
   Gosub _CLEARCOPY
   Bank Swap CBANK,PBANK
   If Fn MULTI
      Gosub CLRPLY
   Else 
      Gosub _STOP
   End If 
   Gosub SETPBANK
   If B<5
      ADVPOS#(B)=0
   End If 
Else 
   S_LENGTH=Length(PBANK)-CLENGTHN
   Trap Reserve As Work WBANK,S_LENGTH
   If Errtrap
      Goto NOMEMORY
   End If 
   Gosub _CLEARCOPY
   Trap Reserve As Work CBANK,CLENGTHN
   If Errtrap
      Goto NOMEMORY
   End If 
   S_START=Start(CBANK)
   CCOUNT=0 : CLENGTH=0
   Repeat 
      Copy P_START+CLENGTH+RST,P_START+CLENGTH+RST+RL To S_START+(RL*CCOUNT)
      CLENGTH=CLENGTH+P_LENGTH
      Inc CCOUNT
   Until CCOUNT>CCOUNTN
   S_START=Start(WBANK)
   CCOUNT=0 : CLENGTH=0
   Repeat 
      If RST+CLENGTH>CLENGTH
         Copy P_START+CLENGTH,P_START+CLENGTH+RST To S_START+CLENGTH-(RL*CCOUNT)
      End If 
      If P_LENGTH>RST+RL
         Copy P_START+CLENGTH+RST+RL,P_START+CLENGTH+P_LENGTH To S_START+CLENGTH+RST-(RL*CCOUNT)
      End If 
      CLENGTH=CLENGTH+P_LENGTH
      Inc CCOUNT
   Until CCOUNT>CCOUNTN
   Gosub _WORKSWAP
   Gosub _RESIZED
End If 
Gosub _RANGENONE
Gosub CLRPOS
RENDERING=True
Gosub _DRAW
If Fn MULTIOFF Then Gosub NUDGEPOS
RENDERING=False
Return 
'
_CROP:
If RL<2 or RL=P_LENGTH
   Gosub FLSHSCR
Else 
   Gosub _UNDOSET
   Gosub _COPYSET
   Trap Reserve As Work WBANK,CLENGTHN
   If Errtrap
      Goto NOMEMORY
   End If 
   S_START=Start(WBANK)
   CCOUNT=0 : CLENGTH=0
   Repeat 
      Copy P_START+CLENGTH+RST,P_START+CLENGTH+RST+RL To S_START+(RL*CCOUNT)
      CLENGTH=CLENGTH+P_LENGTH
      Inc CCOUNT
   Until CCOUNT>CCOUNTN
   Gosub _WORKSWAP
   Gosub _RANGENONE
   Gosub _SHOWALL
End If 
Return 
'
_COPY:
If P_LENGTH=0 Then Goto FLSHSCR
If RL<2
   If Fn MULTI and B<5
      Gosub NUDGE4POS
   End If 
   Gosub _RANGEALL
   If MACRO
      _CHOICE=3 : Rem after the macro clear the range  
   End If 
End If 
Gosub _COPYSET
Gosub _CLEARCOPY
Trap Reserve As Work CBANK,CLENGTHN
If Errtrap Then Goto NOMEMORY
S_START=Start(CBANK)
CCOUNT=0 : CLENGTH=0
Repeat 
   Copy P_START+CLENGTH+RST,P_START+CLENGTH+RST+RL To S_START+(RL*CCOUNT)
   CLENGTH=CLENGTH+P_LENGTH
   Inc CCOUNT
Until CCOUNT>CCOUNTN
Return 
'
_PASTE:
If Length(CBANK)=0 Then Goto NOCOPY
If Length(PBANK)=0
   S_START=Start(CBANK)
   S_LENGTH=Length(CBANK)
   Trap Reserve As Work PBANK,S_LENGTH
   If Errtrap
      Goto NOMEMORY
   End If 
   Copy S_START,S_START+S_LENGTH To Start(PBANK)
   RL=S_LENGTH
   If F$=_$
      F$=_UNNAMED$ : Gosub SETFILENAME
   End If 
   Gosub SETPBANK
   Gosub SETCORDS
   If Fn MULTI and B<5
      Goto _SHOWALL
   Else If Fn MULTIOFF
      Goto _SHOWALL
   Else 
      Goto HEADER
   End If 
End If 
Gosub _COPYSET
Gosub _UNDOSET
If Fn STEREO
   RL=Length(CBANK)/2
Else If Fn QUAD
   RL=Length(CBANK)/4
Else 
   RL=Length(CBANK)
End If 
S_LENGTH=Length(PBANK)+Length(CBANK)
Trap Reserve As Work WBANK,S_LENGTH
If Errtrap
   Goto NOMEMORY
End If 
CCOUNT=0
Repeat 
   If RST>0
      Copy Start(PBANK)+(P_LENGTH*CCOUNT),Start(PBANK)+(P_LENGTH*CCOUNT)+RST To Start(WBANK)+(P_LENGTH*CCOUNT)+(RL*CCOUNT)
   End If 
   Copy Start(CBANK)+(RL*CCOUNT),Start(CBANK)+(RL*CCOUNT)+RL To Start(WBANK)+(P_LENGTH*CCOUNT)+RST+(RL*CCOUNT)
   If RST<P_LENGTH
      Copy Start(PBANK)+(P_LENGTH*CCOUNT)+RST,Start(PBANK)+(P_LENGTH*CCOUNT)+P_LENGTH To Start(WBANK)+RST+(P_LENGTH*CCOUNT)+(RL*CCOUNT)+RL
   End If 
   Inc CCOUNT
Until CCOUNT>CCOUNTN
Gosub _WORKSWAP
Gosub _RESIZED
Gosub MAGICWAND
If Not Fn WANDZOOM
   XR=RST/CON#
   XR2=XR+(RL/CON#)
End If 
Gosub SETSELCORDS
If Fn MULTIOFF or( Fn MULTI and B<5)
   RENDERING=True
   Gosub _DRAW
   Gosub FRESHSEL
   Gosub CLRPOS
   RENDERING=False
End If 
Return 
'
_SWAP:
Gosub _OPENFXWIN
REALTIME=False
TXT1$="Swap"+Str$(B)+" with:"
Gosub _RUNFXTEXTWIN
If T$=_$ Then REQ=False
If REQ
   If Not MACRO
      MACRO=True : Gosub ¯SWAP : MACRO=False
   Else 
      Gosub ¯SWAP
   End If 
End If 
_TEXT=False
Return 
'
_SIZE:
Gosub _OPENFXWIN
REALTIME=False
TXT1$="From"+Str$(P_LENGTH)+_COLON$
Gosub _RUNFXTEXTWIN
If T$=_$ Then REQ=False
If REQ
   Gosub _UNDOSET
   MACRO=True
   Gosub ¯SIZE
   MACRO=False
End If 
_TEXT=False
Return 
'
_COPYSWAP:
If Length(6)=0 Then Goto NOCOPY
Bank Swap PBANK,6
XR=0 : XR2=0 : XD=0 : XD2=SCR_W : L=0 : ST=0 : FIN=0 : RST=0 : RFIN=0 : RL=0
Gosub PBANKLEN
Gosub POSCAL
Gosub _SHOWALL
Return 
'  
_CLEARCOPY:
If Length(CBANK)>0 Then Erase CBANK
Return 
'
_COPYSET:
Gosub SETPBANK
Gosub SETCORDS
If Fn STEREO
   CLENGTHN=RL*2
   CCOUNTN=1
Else If Fn QUAD
   CLENGTHN=RL*4
   CCOUNTN=3
Else 
   CLENGTHN=RL
   CCOUNTN=0
End If 
Return 
'
_UNDO:
If Length(UBANK)=0 Then Goto NOUNDO
If UB-B Then Goto WRONGUNDO
Bank Swap PBANK,UBANK
Gosub SETPBANK
If Length(PBANK)-Length(UBANK)
   Gosub _RANGENONE
   Gosub _RESIZED
End If 
Gosub _DRAWALL
_TOAST$="_UNDO" : Gosub T0AST
Return 
'
_UNDOSET:
If MACRO or PASS>0 or P_LENGTH=0 Then Return 
Gosub _CLEARUNDO
S_LENGTH=Length(PBANK)
Trap Reserve As Work UBANK,S_LENGTH
If Errtrap Then Return 
Copy Start(PBANK),Start(PBANK)+S_LENGTH To Start(UBANK)
UB=B
Return 
'
_CLEARUNDO:
If Length(UBANK)>0 Then Erase UBANK
Return 
'
_WORKSWAP:
Bank Swap PBANK,WBANK
Return 
'
_CLEARWORK:
If Length(WBANK)>0 Then Erase WBANK
Return 
'
_RESIZED:
Gosub _CLEARWORK
Gosub PBANKLEN
If Fn MULTI
   If B<5
      D_LENGTH(B)=Length(B+7)
      If D_LENGTH(B)>0
         ADV#(B)=(D_LENGTH(B)-ST-FIN)/SCR_W#
         D_START(B)=Start(B+7)+ST
      Else 
         ADV#(B)=0
      End If 
   End If 
End If 
Gosub SETSELV
RFIN=P_LENGTH-RST-RL
Gosub SETCORDS
Gosub HEADER
Gosub DRWTIME
Return 
'
'
'
'***************************************************************************** 
'*mix************************************************************************* 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'    .        :   :::  :.,::      .:.
'    ;;,.    ;;;  ;;;   `;;;,  .,;;
'    [[[[, ,[[[[, [[[      '[[,,[['
'    $$$$$$$$"$$$ $$$,      Y$$$P
'   888 Y88" 888oo88888   oP"``"Yo,
'   MM  M'  "MMMMMMM"MM,m"       "Mm,
'
'
'
_MIX:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Gosub MIXSET
If FX_LENGTH2=0 Then Goto NOCOPY
Gosub _UNDOSET
FX2=0
FX_START2=Start(CBANK)+(FX_LENGTH2*PASS)
Repeat 
   CN=Peek(FX)
   CN2=Peek(FX_START2+FX2)
   Gosub SIGN2
   If CN<0 and CN2<0
      CN=(CN+CN2)-((CN*CN2)/-127)
   Else If CN>0 and CN2>0
      CN=(CN+CN2)-((CN*CN2)/127)
   Else 
      Add CN,CN2
   End If 
   Inc FX2
   If FX2>FX_LENGTH2
      If _LPMIX
         FX2=0
      Else 
         FX=FXE
      End If 
   End If 
   Gosub UNSIGN
   Poke FX,CN
   Inc FX
Until FX>FXE
Return 
'
_ADD:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Gosub MIXSET
If FX_LENGTH2=0 Then Goto NOCOPY
Gosub _UNDOSET
FX_START2=Start(CBANK)+(FX_LENGTH2*PASS)
FX2=0
Repeat 
   CN=Peek(FX)
   CN2=Peek(FX_START2+FX2)
   Gosub SIGN2
   CN=CN+CN2
   Inc FX2
   If FX2>FX_LENGTH2
      If _LPMIX
         FX2=0
      Else 
         FX=FXE
      End If 
   End If 
   Gosub UNSIGN
   Poke FX,CN
   Inc FX
Until FX>FXE
Return 
'
_SUBTRACT:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Gosub MIXSET
If FX_LENGTH2=0 Then Goto NOCOPY
Gosub _UNDOSET
FX_START2=Start(CBANK)+(FX_LENGTH2*PASS)
FX2=0
Repeat 
   CN=Peek(FX)
   CN2=Peek(FX_START2+FX2)
   Gosub SIGN2
   CN=CN-CN2
   Inc FX2
   If FX2>FX_LENGTH2
      If _LPMIX
         FX2=0
      Else 
         FX=FXE
      End If 
   End If 
   Gosub UNSIGN
   Poke FX,CN
   Inc FX
Until FX>FXE
Return 
'
_MODULATE:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Gosub MIXSET
If FX_LENGTH2=0 Then Goto NOCOPY
Gosub _UNDOSET
FX_START2=Start(CBANK)+(FX_LENGTH2*PASS)
FX2=0
Repeat 
   CN=Peek(FX)
   CN2=Peek(FX_START2+FX2)
   Gosub SIGN2
   CN#=CN2
   CN#=CN#/128
   CN=CN*CN#
   Inc FX2
   If FX2>FX_LENGTH2
      If _LPMIX
         FX2=0
      Else 
         FX=FXE
      End If 
   End If 
   Gosub UNSIGN
   Poke FX,CN
   Inc FX
Until FX>FXE
Return 
'
_XOR:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Gosub MIXSET
If FX_LENGTH2=0 Then Goto NOCOPY
Gosub _UNDOSET
FX_START2=Start(CBANK)+(FX_LENGTH2*PASS)
FX2=0
Repeat 
   CN=Peek(FX)
   CN2=Peek(FX_START2+FX2)
   Gosub SIGN2
   CN=CN xor CN2
   Inc FX2
   If FX2>FX_LENGTH2
      If _LPMIX
         FX2=0
      Else 
         FX=FXE
      End If 
   End If 
   Gosub UNSIGN
   Poke FX,CN
   Inc FX
Until FX>FXE
Return 
'
MIXSET:
If Fn STEREO
   FX_LENGTH2=Length(CBANK)/2
Else If Fn QUAD
   FX_LENGTH2=Length(CBANK)/4
Else 
   FX_LENGTH2=Length(CBANK)
End If 
Return 
'
'
'
'***************************************************************************** 
'*frame*********************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'    .-:::::':::::::..    :::.      .        :  .,:::::: 
'    ;;;'''',;;;;``;;;;   ;;`;;     ;;,.    ;;; ;;;;'''' 
'    [[[,,==[\[[[,/[[['  ,[[ '[[,   [[[[, ,[[[[, [[cccc
'    `$$$"``""$$$$$$c   c$$$cc$$$c  $$$$$$$$"$$$ $$""""
'   _ 888    ,888b "88bo 888   888,888 Y88" 888o 888oo,_ 
'   MM"MM,   "MMMM   "W" YMM   ""` MM  M'  "MMMM """"YUM 
'
'
'
_SAMPLELEFT:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
FX_LENGTH2=1
LEFT:
Trap Reserve As Work WBANK,FX_LENGTH2
If Errtrap Then Goto NOMEMORY
Gosub _UNDOSET
Copy FX,FX+FX_LENGTH2 To Start(WBANK)
Copy FX+FX_LENGTH2,FXE To FX
Copy Start(WBANK),Start(WBANK)+FX_LENGTH2 To FXE-FX_LENGTH2
Gosub _CLEARWORK
REQ=True
Return 
'
_SAMPLERIGHT:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
FX_LENGTH2=1
RIGHT:
Trap Reserve As Work WBANK,FX_LENGTH2
If Errtrap Then Goto NOMEMORY
Gosub _UNDOSET
Copy FXE-FX_LENGTH2,FXE To Start(WBANK)
Copy FX,FXE-FX_LENGTH2 To FX+FX_LENGTH2
Copy Start(WBANK),Start(WBANK)+FX_LENGTH2 To FX
Gosub _CLEARWORK
REQ=True
Return 
'
_BEATLEFT:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
FX_LENGTH2=P_LENGTH/_TIMEDIV#
Goto LEFT
'
_BEATRIGHT:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
FX_LENGTH2=P_LENGTH/_TIMEDIV#
Goto RIGHT
'
_TIMEDIVISION:
Gosub _OPENFXWIN
REALTIME=False
TXT1$="From"+Str$(_TIMEDIV#)+_COLON$
Gosub _RUNFXTEXTWIN
_TEXT=True
If T$=_$ Then REQ=False
If REQ
   TIMEDIV:
   CN#=Val(T$)
   If CN#<2 or CN#>SCR_W
      Goto FLSHSCR
   End If 
   _TIMEDIV#=CN#
   _TIMEBEAT#=SCR_W/_TIMEDIV#
End If 
_TEXT=False
Return 
'
DRWDIV:
Ink _SHADOW
DI#=_TIMEBEAT#
If(DI#<=40) Then DI#=DI#-1
DCY=HDR_H+WVE_H
INF_H=1
Repeat 
   DI=Int(DI#)
   Draw DI,HDR_H To DI,HDR_H+INF_H
   Draw DI,DCY-INF_H To DI,DCY
   DI#=DI#+_TIMEBEAT#
   If INF_H=1 Then INF_H=2 Else INF_H=1
Until DI#>=SCR_W-_TIMEBEAT#+1
If Fn MULTIOFF Then Return 
DI#=_TIMEBEAT#
If(DI#<=40) Then DI#=DI#-1
INF_H=2
Repeat 
   DI=Int(DI#)
   Draw DI,HDR_H+WVE_D To DI,HDR_H+WVE_D+INF_H
   Draw DI,HDR_H+(WVE_D*3)-INF_H To DI,HDR_H+(WVE_D*3)
   DI#=DI#+_TIMEBEAT#
   If INF_H=2 Then INF_H=4 Else INF_H=2
Until DI#>=SCR_W-_TIMEBEAT#+1
Return 
'
'
'
'***************************************************************************** 
'*volume********************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'   :::      .::.    ...       :::     ...    ::: .        :  .,:::::: 
'   ';;,   ,;;;'  .;;;;;;;.    ;;;    ';;     ;;; ;;,.    ;;; ;;;;'''' 
'    \[[  .[[/   ,[[     \[[,  [[[    [['     [[[ [[[[, ,[[[[, [[cccc  
'     Y$c.$$"    $$$,     $$$  $$'    $$      $$$ $$$$$$$$"$$$ $$""""  
'      Y88P      "888,_ _,88Po88oo,.__88    .d888888 Y88" 888o 888oo,_ 
'       MP         "YMMMMMP" """YUMMMM "YmmMMMM""MM  M'  "MMMM """"YUM 
'  
'
'
_VOLUME:
Gosub SETREALTIME
If FXSHOW
   Inc PASS
   If PASS<2
      Gosub SETPBANK
      If FX_LENGTH<2
         Goto FLSHSCR
      End If 
      CHAIN$(1)="_VOLUMEP"
      Gosub _OPENFXWIN
      FXN=VN : FXSN=VN : FXS$=_PERCENT$ : Gosub FXDRW
      Gosub VODRW
      FXRUN$="VODRW"
      Gosub _RUNFXWIN
   End If 
   If Not REQ
      Return 
   End If 
End If 
_VOLUMEP:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Gosub _UNDOSET
Repeat 
   CN=Peek(FX)
   Gosub SIGN
   CN=Int(CN*VA#)
   Gosub UNSIGN
   Poke FX,CN
   Inc FX
Until FX>FXE
Return 
'
VODRW:
VN=FXN
VA#=VN*0.01
FXSN=VN
Return 
'
_FADEIN:
CN2=0
Goto _FADE
_FADEOUT:
CN2=1
_FADE:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
DIV#=FX_LENGTH/100.0
If DIV#=0 Then Goto FLSHSCR
Gosub _UNDOSET
FX2=FX_LENGTH*CN2
Repeat 
   CN=Peek(FX)
   Gosub SIGN
   CN#=(FX2/DIV#)/100.0
   CN=Int(CN*CN#)
   Gosub UNSIGN
   Poke FX,CN
   Inc FX
   If CN2=0 Then Inc FX2 Else Dec FX2
Until FX>FXE
Return 
'
_HALF:
VA#=0.5
Goto _VOLUMEP
'
_DOUBLE:
VA#=2.0
Goto _VOLUMEP
'
_SILENCE:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Gosub _UNDOSET
Repeat 
   FX_LENGTH2=Min(FX_LENGTH-(FX-FX_START),1048576)
   Fill FX To FX+FX_LENGTH2,0
   FX=FX+FX_LENGTH2
Until FX>FXE
Return 
'
_MAXIMIZE:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
CN2=0
If PASS=0
   Repeat 
      CN=Peek(FX)
      Gosub SIGN
      If CN>CN2
         CN2=CN
      Else If CN<0 and CN<-CN2
         CN2=-CN
      End If 
      Inc FX
   Until FX>FXE
   If CN2=0 or CN2=127
      Return 
   End If 
   VA#=127.0/CN2
End If 
Goto _VOLUMEP
'
_DCOFFSET:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Gosub _OPENFXWIN
REALTIME=False
BTXT1$="Auto" : BX=267 : BY=71 : BZN=48 : Gosub BTN
T$=_$ : _EXIT=False : _TEXT=True : KDOWN=False
TXT1$="-127 to 127:"
TXT_X=340 : TXT_Y=Y+68 : N=0 : _MAX=9
Repeat 
   Gosub _TEXTINPUT
   Repeat 
      Gosub GVAL
      If Z=48 and M=1
         PZN=Z : BX=267 : BY=71 : Gosub PRESS
         If _ON
            Gosub WINC
            Reset Zone 48
            REQ=True
            Goto _DCSCAN
         End If 
      Else If Z=60 and M=1 or Fn KEY_RETURN
         PZN=Z : BX=RX : BY=87 : Gosub PRESS
         If _ON
            REQ=True
            _EXIT=True
         End If 
      Else If Z=61 and M=1 or Fn KEY_ESC
         PZN=Z : BX=RX+107 : BY=87 : Gosub PRESS
         If _ON
            REQ=False
            _EXIT=True
         End If 
      End If 
   Until _EXIT or C
Until _EXIT
_TEXT=False
Gosub WINC
Reset Zone 48
CN=Val(T$)
If T$=_$ or CN=0 and REQ
   REQ=False
   Gosub FLSHSCR
End If 
If Not REQ Then Return 
Gosub CHECKCLIP
_DCOFFSETN=CN
_DCOFFSETP:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Gosub _UNDOSET
_REPEAT$="_DCOFFSETP"
G$=_REPEAT$
_OFFSET:
Repeat 
   CN=Peek(FX)
   Gosub SIGN
   CN=CN+_DCOFFSETN
   Gosub UNSIGN
   Poke FX,CN
   Inc FX
Until FX>FXE
Return 
'
_DCSCAN:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
CN2=0 : CN3=0
Repeat 
   CN=Peek(FX)
   Gosub SIGN
   If CN>0
      If CN>CN2
         CN2=CN
      End If 
   Else If CN<0
      If CN<CN3
         CN3=CN
      End If 
   End If 
   Inc FX
Until FX>FXE
_DCOFFSETN=(-CN3-CN2)/2
Gosub SETPBANK
Gosub _UNDOSET
_REPEAT$="_DCOFFSETP"
G$=_REPEAT$
Goto _OFFSET
'
'
'
'***************************************************************************** 
'*resample******************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'   :::::::..  .,:::::: .::::::.   :::.      .        :  ::::::::.   :::    .,:::: 
'   ;;;;``;;;; ;;;;'''';;;`    `;  ;;`;;     ;;,.    ;;; `;;```.;;;  ;;;    ;;;;'' 
'   \[[[,/[[['  [[cccc '[==/[[[[, ,[[ '[[,   [[[[, ,[[[[,`]]nnn]]'   [[[     [[ccc 
'   "$$$$$$c    $$""""   '''    $c$$$cc$$$c  $$$$$$$$"$$$ $$$""      $$'     $$""" 
'   ,888b "88bo 888oo,_,88b    dP 888   888,888 Y88" 888o 888o     o88oo,.__ 888oo 
'   "MMMM   "W" """"YUM  "YMmMY"  YMM   ""` MM  M'  "MMMM YMMMb    """YUMMMM """"Y 
'  
'
'
_SCALESAMPLE:
N#=N2#/N#
FX_LENGTH2=Int(FX_LENGTH/N#)
If FX_LENGTH2<2 Then Goto FLSHSCR
If RL<2 Then _SCALEMIX=False Else _SCALEMIX=True
Trap Reserve As Work WBANK,FX_LENGTH2
If Errtrap Then Goto NOMEMORY
Gosub _UNDOSET
FX_START2=Start(WBANK)
FX2=0
FX3=FX_START2
FXE2=FX_START2+FX_LENGTH2-1
CN#=N#
Repeat 
   CN=Int(CN#)
   CN#=FX2*N#
   FX=FX_START+CN
   CN=Peek(FX)
   Poke FX3,CN
   Inc FX2 : Inc FX3
Until FX3>=FXE2 or FX=FXE
FX=FXE
If _SCALEMIX
   If FX_LENGTH2>=FX_LENGTH
      FX_LENGTH3=FX_LENGTH
   Else 
      FX_LENGTH3=FX_LENGTH2
   End If 
   If Not MACRO
      MACRO=True : Gosub _SILENCE : MACRO=False
   Else 
      Gosub _SILENCE
   End If 
   Copy FX_START2,FX_START2+FX_LENGTH3 To FX_START
Else 
   Gosub _WORKSWAP
   Gosub _RESIZED
End If 
Return 
'
_BYRATE:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
If PASS=0
   Gosub _OPENFXWIN
   REALTIME=False
   BTXT1$=_$ : BX=267 : BY=71 : BZN=48 : Gosub BTN
   Ink _BLACK
   Bar BX+9,BY+2 To BX+93,BY+11
   Paste Bob BX+9,BY+2,3
   T$=_$ : _EXIT=False : _TEXT=True : KDOWN=False
   TXT1$="From "+Str$(FREQ)-_SPACE$+_COLON$
   TXT_X=340 : TXT_Y=Y+68 : N=0 : _MAX=9
   Repeat 
      Gosub _TEXTINPUT
      Repeat 
         Gosub GVAL
         If Z=48 and M
            Repeat 
               Gosub GVAL
               If Z=48 and KDOWN=False
                  PZN=Z : BX=267 : BY=71 : Gosub HIGHBTN
                  N=0
                  T$=_$
                  KDOWN=True
               End If 
               If C>0 and KDOWN
                  Gosub PIANO
                  If KEYC<>-1
                     T$=Str$(KEYF)-_SPACE$
                     N=Len(T$)
                     C=0
                     Gosub _TEXTINPUT
                  Else 
                     Gosub FLSHSCR
                  End If 
               End If 
               If KDOWN and(M=0 or PZN<>Z)
                  PZN=Z : BX=267 : BY=71 : Gosub HIGHBTN
                  KDOWN=False
               End If 
            Until M=0
         Else If Z=60 and M=1 or Fn KEY_RETURN
            PZN=Z : BX=RX : BY=RY+28 : Gosub PRESS
            If _ON
               REQ=True
               _EXIT=True
            End If 
         Else If Z=61 and M=1 or Fn KEY_ESC
            PZN=Z : BX=RX+107 : BY=RY+28 : Gosub PRESS
            If _ON
               REQ=False
               _EXIT=True
            End If 
         End If 
      Until _EXIT or C
   Until _EXIT
   Gosub WINC
   Reset Zone 48
   If Not REQ
      Return 
   End If 
   _BYRATEP:
   _TEXT=True
   Gosub SETPBANK
   If FX_LENGTH<2
      Goto FLSHSCR
   End If 
   _TEXT=False
End If 
N2#=Val(T$)
If N2#<600 Then Goto FLSHSCR
N#=FREQ
Goto _SCALESAMPLE
'
_BYSIZE:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
If PASS=0
   Gosub _OPENFXWIN
   REALTIME=False
   TXT1$="From "+Str$(FX_LENGTH)-_SPACE$+_COLON$
   Gosub _RUNFXTEXTWIN
   If Not REQ
      Return 
   End If 
   _TEXT=True
   Gosub SETPBANK
   If FX_LENGTH<2
      Goto FLSHSCR
   End If 
   _TEXT=False
End If 
N=Val(T$)
If N<2 Then Goto FLSHSCR
N#=N
N2#=FX_LENGTH
Goto _SCALESAMPLE
'  
_BYBPM:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
If PASS=0
   Gosub _OPENFXWIN
   REALTIME=False
   DIV#=FREQ
   DIV#=((DIV#/Length(PBANK))*60)*4
   If Fn STEREO
      DIV#=DIV#*2
   Else If Fn QUAD
      DIV#=DIV#*4
   End If 
   TXT1$="From "+Str$(DIV#)-_SPACE$+_COLON$
   Gosub _RUNFXTEXTWIN
   If Not REQ
      Return 
   End If 
   _TEXT=True
   Gosub SETPBANK
   If FX_LENGTH<2
      Goto FLSHSCR
   End If 
   _TEXT=False
End If 
N2#=Val(T$)
If N2#<1 Then Goto FLSHSCR
N#=DIV#
Goto _SCALESAMPLE
'
_OCTAVEUP:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Gosub _UNDOSET
If RL<2 Then _SCALEMIX=False Else _SCALEMIX=True
FX_LENGTH2=FX_LENGTH/2
Trap Reserve As Work WBANK,FX_LENGTH2
If Errtrap Then Goto NOMEMORY
FX_START2=Start(WBANK)
FX2=0
Repeat 
   If FX>FX_START Then CNL=Peek(FX-1)
   CN=Peek(FX)
   If FX<FXE Then CN2=Peek(FX+1)
   If FX<FXE-1 Then CNR=Peek(FX+2)
   A0=CNR-CN2-CNL+CN
   A1=CNL-CN-A0
   A2=CN2-CNL
   A3=CN
   Gosub SIGN
   CN=A0+A1+A2+A3
   If FX_START2+FX_LENGTH2>FX_START2+FX2
      Poke FX_START2+FX2,CN : Inc FX2
   End If 
   Inc FX : Inc FX
Until FX>FXE
If _SCALEMIX
   If Not MACRO
      MACRO=True : Gosub _SILENCE : MACRO=False
   Else 
      Gosub _SILENCE
   End If 
   Copy FX_START2,FX_START2+FX_LENGTH2/2 To FX_START
Else 
   Gosub _WORKSWAP
   Gosub _RESIZED
   If BGPLYING
      If Fn MULTI and B<5
         OFFSET(B)=OFFSET(B)/2
      Else If Fn MULTIOFF
         OFFSET(1)=OFFSET(1)/2
      End If 
   End If 
End If 
Return 
'
_OCTAVEDOWN:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Gosub _UNDOSET
If RL<2 Then _SCALEMIX=False Else _SCALEMIX=True
FX_LENGTH2=FX_LENGTH*2
Trap Reserve As Work WBANK,FX_LENGTH2
If Errtrap Then Goto NOMEMORY
FX_START2=Start(WBANK)
FX2=0
Repeat 
   If FX>FX_START Then CNL=Peek(FX-1)
   CN=Peek(FX)
   If FX<FXE Then CN2=Peek(FX+1)
   If FX<FXE-1 Then CNR=Peek(FX+2)
   A0=CNR-CN2-CNL+CN
   A1=CNL-CN-A0
   A2=CN2-CNL
   A3=CN
   Gosub SIGN
   CN2=A0+A1+A2+A3
   Poke FX_START2+FX2,CN : Inc FX2
   Poke FX_START2+FX2,CN2 : Inc FX2
   Inc FX
Until FX>FXE
If _SCALEMIX
   If Not MACRO
      MACRO=True : Gosub _SILENCE : MACRO=False
   Else 
      Gosub _SILENCE
   End If 
   Copy FX_START2,FX_START2+FX_LENGTH2 To FX_START
Else 
   Gosub _WORKSWAP
   Gosub _RESIZED
   If BGPLYING
      If Fn MULTI and B<5
         OFFSET(B)=OFFSET(B)*2
      Else If Fn MULTIOFF
         OFFSET(1)=OFFSET(1)*2
      End If 
   End If 
End If 
Return 
'
_NOTEUP:
PFREQ=FREQ
FREQ=16730
T$="17734"
Gosub _BYRATEP
FREQ=PFREQ
Goto SETHZ
'
_NOTEDOWN:
PFREQ=FREQ
FREQ=17734
T$="16730"
Gosub _BYRATEP
FREQ=PFREQ
Goto SETHZ
'
_FINEUP:
T$=Str$(FREQ+50)
Goto _BYRATEP
'  
_FINEDOWN:
T$=Str$(FREQ-50)
Goto _BYRATEP
'
'
'
'***************************************************************************** 
'*filter********************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'    .-:::::' :::    :::    ::::::::::.,:::::::::::::..  
'    ;;;'''', ;;;    ;;;    ;;;;;;;''';;;;'''';;;;``;;;; 
'    [[[,,==[ [[[    [[[        [[     [[cccc \[[[,/[[[' 
'    `$$$"``" $$$,   $$'        $$     $$"""" "$$$$$$c 
'   _ 888    o88888o88oo,.__    88,    888oo,_,888b "88bo  
'   MM"MM,   MMM"MM"""YUMMMM    MMM    """"YUM"MMMM   "W"  
'  
'
'
_HIGHPASS:
Gosub SETREALTIME
If FXSHOW
   Inc PASS
   If PASS<2
      Gosub SETPBANK
      If FX_LENGTH<2
         Goto FLSHSCR
      End If 
      FXBC=1
      Repeat 
         HBUF(FXBC)=0
         Inc FXBC
      Until FXBC>_CH
      CHAIN$(1)="_HIGHPASSP"
      Gosub _OPENFXWIN
      FXN=HIGHN : FXSN=HIGHF : FXS$=_HZ$ : Gosub FXDRW
      Gosub HISET
      FXRUN$="HIDRW"
      Gosub _RUNFXWIN
   End If 
   If Not REQ
      Return 
   End If 
End If 
_HIGHPASSP:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Gosub HISET
Repeat 
   CN=Peek(FX)
   CN2=HBUF(_CHANNEL)
   Gosub SIGN2
   CN2=-CN2
   CN#=CN2+(ALPHA#*(CN-CN2))*(ALPHA#*2)
   CN=Int(CN#)
   Gosub UNSIGN
   Poke FX,CN
   HBUF(_CHANNEL)=CN
   Inc FX
Until FX>FXE
Return 
'
HISET:
HIGHF=HIGHN*50
If HIGHF<100 Then HIGHF=100
RC#=1.0/(HIGHF*_2PI#)
DT#=1.0/FREQ
ALPHA#=DT#/(RC#+DT#)
Return 
'
HIDRW:
HIGHN=FXN
Gosub HISET
FXSN=HIGHF
Return 
'  
_LOWPASS:
Gosub SETREALTIME
If FXSHOW
   Inc PASS
   If PASS<2
      Gosub SETPBANK
      If FX_LENGTH<2
         Goto FLSHSCR
      End If 
      FXBC=1
      Repeat 
         LBUF(FXBC)=0
         Inc FXBC
      Until FXBC>_CH
      CHAIN$(1)="_LOWPASSP"
      Gosub _OPENFXWIN
      FXN=LOWN : FXSN=LOWF : FXS$=_HZ$ : Gosub FXDRW
      Gosub LOSET
      FXRUN$="LODRW"
      Gosub _RUNFXWIN
   End If 
   If Not REQ
      Return 
   End If 
End If 
_LOWPASSP:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Gosub LOSET
Repeat 
   CN=Peek(FX)
   CN2=LBUF(_CHANNEL)
   Gosub SIGN2
   CN#=CN2+(ALPHA#*(CN-CN2))
   CN=Int(CN#)
   Gosub UNSIGN
   Poke FX,CN
   LBUF(_CHANNEL)=Peek(FX)
   Inc FX
Until FX>FXE
If Not MACRO
   MACRO=True : Gosub _DCSCAN : MACRO=False
Else 
   Gosub _DCSCAN
End If 
_REPEAT$="_LOWPASSP"
Return 
'
LOSET:
LOWF=(FREQ/(201-LOWN))/2
RC#=1.0/(LOWF*_2PI#)
DT#=1.0/FREQ
ALPHA#=DT#/(RC#+DT#)
Return 
'  
LODRW:
LOWN=FXN
Gosub LOSET
FXSN=LOWF
Return 
'  
_BANDPASS:
Gosub SETREALTIME
If FXSHOW
   Inc PASS
   If PASS<2
      Gosub SETPBANK
      If FX_LENGTH<2
         Goto FLSHSCR
      End If 
      FXBC=1
      Repeat 
         BBUF0(FXBC)=0
         BBUF1(FXBC)=0
         Inc FXBC
      Until FXBC>_CH
      CHAIN$(1)="_BANDPASSP"
      Gosub _OPENFXWIN
      FXN=BANDN : FXSN=BANDF : FXS$=_HZ$ : Gosub FXDRW
      Gosub BPSET
      FXRUN$="BPDRW"
      Gosub _RUNFXWIN
   End If 
   If Not REQ
      Return 
   End If 
End If 
_BANDPASSP:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Gosub BPSET
Repeat 
   CN=Peek(FX)
   CN2=BBUF0(_CHANNEL)
   CN3=BBUF1(_CHANNEL)
   Gosub SIGN3
   CN2=-CN2
   CN3=-CN3
   CN#=CN2+(ALPHA#*(CN-CN2))*ALPHA#
   CN2#=CN3+(ALPHA#*(CN2-CN3))*ALPHA#
   CN=Int(CN#-CN2#)
   Gosub UNSIGN
   Poke FX,CN
   BBUF0(_CHANNEL)=CN
   BBUF1(_CHANNEL)=CN2
   Inc FX
Until FX>FXE
Return 
'
BPSET:
BANDF=BANDN*50
If BANDF<100 Then BANDF=100
RC#=1.0/(BANDF*_2PI#)
DT#=1.0/FREQ
ALPHA#=DT#/(RC#+DT#)
Return 
'
BPDRW:
BANDN=FXN
Gosub BPSET
FXSN=BANDF
Return 
'
_RESONANT:
Gosub SETREALTIME
If FXSHOW
   Inc PASS
   If PASS<2
      Gosub SETPBANK
      If FX_LENGTH<2
         Goto FLSHSCR
      End If 
      FXBC=1
      Repeat 
         RBUF0#(FXBC)=0.0
         Inc FXBC
      Until FXBC>_CH
      CHAIN$(1)="_RESONANTP"
      Gosub _OPENFXWIN
      Gosub DIALRES
      FXRUN$=_$
      Gosub _RUNFXWIN
   End If 
   If Not REQ
      Return 
   End If 
End If 
_RESONANTP:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Gosub RESET
Repeat 
   CN=Peek(FX)
   Gosub FLOAT
   CN2#=RBUF0#(_CHANNEL) : CN3#=RBUF1#(_CHANNEL)
   '**************************************************************************
   '  Paul Kellett      
   '
   CN2#=CN2#+_RF#*(CN#-CN2#+_RFB#*(CN2#-CN3#))
   CN3#=CN3#+_RF#*(CN2#-CN3#)
   '
   '**************************************************************************
   RBUF0#(_CHANNEL)=CN2# : RBUF1#(_CHANNEL)=CN3#
   CN#=CN3#
   Gosub UNFLOAT
   Poke FX,CN
   Inc FX
Until FX>FXE
Return 
'  
RESET:
_RF#=RESN/200.0
_RFB#=_RQ#+_RQ#/(1.01-_RF#)
Return 
'  
_SMOOTH:
Gosub SETREALTIME
If FXSHOW
   Inc PASS
   If PASS<2
      Gosub SETPBANK
      If FX_LENGTH<2
         Goto FLSHSCR
      End If 
      FXBC=1
      Repeat 
         SMOBUF(FXBC)=0
         Inc FXBC
      Until FXBC>_CH
      CHAIN$(1)="_SMOOTHP"
      Gosub _OPENFXSMALLWIN
      Gosub _RUNFXWIN
   End If 
   If Not REQ
      Return 
   End If 
End If 
_SMOOTHP:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Repeat 
   CN=Peek(FX)
   CN2=SMOBUF(_CHANNEL)
   Gosub SIGN2
   CN=(CN+CN2)/2
   Gosub UNSIGN
   Poke FX,CN
   SMOBUF(_CHANNEL)=CN
   Inc FX
Until FX>FXE
Return 
'
_WAVESHAPER:
Gosub SETREALTIME
If FXSHOW
   Inc PASS
   If PASS<2
      Gosub SETPBANK
      If FX_LENGTH<2
         Goto FLSHSCR
      End If 
      CHAIN$(1)="_WAVESHAPERP"
      Gosub _OPENFXSMALLWIN
      Gosub _RUNFXWIN
   End If 
   If Not REQ
      Return 
   End If 
End If 
_WAVESHAPERP:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Repeat 
   CN=Peek(FX)
   Gosub FLOAT
   CN#=Sin(CN#)
   CN#=CN#*CN#*CN#
   Gosub UNFLOAT
   Poke FX,CN
   Inc FX
Until FX>FXE
Return 
'
_PARABOLIC:
Gosub SETREALTIME
If FXSHOW
   Inc PASS
   If PASS<2
      Gosub SETPBANK
      If FX_LENGTH<2
         Goto FLSHSCR
      End If 
      CHAIN$(1)="_PARABOLICP"
      Gosub _OPENFXSMALLWIN
      Gosub _RUNFXWIN
   End If 
   If Not REQ
      Return 
   End If 
End If 
_PARABOLICP:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Repeat 
   CN=Peek(FX)
   CN#=CN/256.0
   SQ#=CN#*CN#
   CN#=SQ#/(2.0*(SQ#-CN#)+1.0)
   CN=Int(CN#*256)
   Poke FX,CN
   Inc FX
Until FX>FXE
Return 
'
_BEZIER:
Gosub SETREALTIME
If FXSHOW
   Inc PASS
   If PASS<2
      Gosub SETPBANK
      If FX_LENGTH<2
         Goto FLSHSCR
      End If 
      CHAIN$(1)="_BEZIERP"
      Gosub _OPENFXSMALLWIN
      Gosub _RUNFXWIN
   End If 
   If Not REQ
      Return 
   End If 
End If 
_BEZIERP:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Repeat 
   CN=Peek(FX)
   CN#=CN/256.0
   CN#=(CN#*CN#)*(3.0-(2.0*CN#))
   CN=Int(CN#*256)
   Poke FX,CN
   Inc FX
Until FX>FXE
Return 
'  
_EXPAND:
Gosub SETREALTIME
If FXSHOW
   Inc PASS
   If PASS<2
      Gosub SETPBANK
      If FX_LENGTH<2
         Goto FLSHSCR
      End If 
      CHAIN$(1)="_EXPANDP"
      Gosub _OPENFXSMALLWIN
      Gosub _RUNFXWIN
   End If 
   If Not REQ
      Return 
   End If 
End If 
_EXPANDP:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Repeat 
   CN=Peek(FX)
   Gosub SIGN
   If CN>0
      Inc CN
   Else If CN<0
      Dec CN
   Else 
      CN=0
   End If 
   Gosub UNSIGN
   Gosub UNCHECKCLIP
   Poke FX,CN
   Inc FX
Until FX>FXE
Return 
'  
_CONTRACT:
Gosub SETREALTIME
If FXSHOW
   Inc PASS
   If PASS<2
      Gosub SETPBANK
      If FX_LENGTH<2
         Goto FLSHSCR
      End If 
      CHAIN$(1)="_CONTRACTP"
      Gosub _OPENFXSMALLWIN
      Gosub _RUNFXWIN
   End If 
   If Not REQ
      Return 
   End If 
End If 
_CONTRACTP:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Repeat 
   CN=Peek(FX)
   Gosub SIGN
   If CN>0
      Dec CN
   Else If CN<0
      Inc CN
   Else 
      CN=0
   End If 
   Gosub UNSIGN
   Poke FX,CN
   Inc FX
Until FX>FXE
Return 
'  
'
'
'***************************************************************************** 
'*distort********************************************************************* 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'   ::::::-.   :::   .::::::. ::::::::::    ...     :::::::..  ::::::::::  
'   ;;,   `';, ;;;  ;;;`    `;;;;;;;;''' .;;;;;;;.  ;;;;``;;;; ;;;;;;;'''  
'   `[[     [[ [[[  '[==/[[[[,    [[    ,[[     \[[,\[[[,/[[['     [[  
'    $$,    $$ $$$,   '''    $    $$    $$$,     $$$"$$$$$$c       $$  
'    888_,o8P'o88888,88b    dP    88,   "888,_ _,88P,888b "88bo    88, 
'   "MMMMP"`  MMM"MM  "YMmMY"     MMM     "YMMMMMP" "MMMM   "W"    MMM 
'
'
'
_TUBE:
Gosub SETREALTIME
If FXSHOW
   Inc PASS
   If PASS<2
      Gosub SETPBANK
      If FX_LENGTH<2
         Goto FLSHSCR
      End If 
      CHAIN$(1)="_TUBEP"
      Gosub _OPENFXWIN
      FXN=TUBEN : FXSN=TUBEN : FXS$=_PERCENT$ : Gosub FXDRW
      FXRUN$="TUDRW"
      Gosub _RUNFXWIN
   End If 
   If Not REQ
      Return 
   End If 
End If 
_TUBEP:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Repeat 
   CN=Peek(FX)
   Gosub FLOAT
   If CN#>0 and TUBEV#-CN#<>0
      CN#=CN#/(TUBEV#-CN#)
   Else If CN#<0 and TUBEV#+CN#<>0
      CN#=CN#/(TUBEV#+CN#)
   End If 
   Gosub UNFLOAT
   Poke FX,CN
   Inc FX
Until FX>FXE
Return 
'
TUDRW:
TUBEN=FXN
TUBEV#=(200-TUBEN)/100.0
FXSN=TUBEN
Return 
'
_SATURATION:
Gosub SETREALTIME
If FXSHOW
   Inc PASS
   If PASS<2
      Gosub SETPBANK
      If FX_LENGTH<2
         Goto FLSHSCR
      End If 
      CHAIN$(1)="_SATURATIONP"
      Gosub _OPENFXWIN
      FXN=SATN : FXSN=SATN : FXS$=_PERCENT$ : Gosub FXDRW
      FXRUN$="SADRW"
      Gosub _RUNFXWIN
   End If 
   If Not REQ
      Return 
   End If 
End If 
_SATURATIONP:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Repeat 
   CN=Peek(FX)
   Gosub FLOAT
   CN#=((1.1+SATV#)*CN#)-(0.2+SATV#*CN#)*CN#*CN#*CN#
   Gosub UNFLOAT
   Poke FX,CN
   Inc FX
Until FX>FXE
Return 
'
SADRW:
SATN=FXN
SATV#=SATN/100.0
FXSN=SATN
Return 
'
_OVERDRIVE:
Gosub SETREALTIME
If FXSHOW
   Inc PASS
   If PASS<2
      Gosub SETPBANK
      If FX_LENGTH<2
         Goto FLSHSCR
      End If 
      CHAIN$(1)="_OVERDRIVEP"
      Gosub _OPENFXSMALLWIN
      Gosub _RUNFXWIN
   End If 
   If Not REQ
      Return 
   End If 
End If 
_OVERDRIVEP:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Repeat 
   CN=Peek(FX)
   Gosub FLOAT
   If CN#<0 Then CN#=1-Exp(-CN#) Else CN#=-1+Exp(CN#)
   Gosub UNFLOAT
   Poke FX,CN
   Inc FX
Until FX>FXE
Return 
'
_ALIAS:
Gosub SETREALTIME
If FXSHOW
   Inc PASS
   If PASS<2
      Gosub SETPBANK
      If FX_LENGTH<2
         Goto FLSHSCR
      End If 
      CHAIN$(1)="_ALIASP"
      Gosub _OPENFXWIN
      FXN=ALIN*2 : FXSN=ALIN : FXS$="b" : Gosub FXDRW
      FXRUN$="ALDRW"
      Gosub _RUNFXWIN
   End If 
   If Not REQ
      Return 
   End If 
End If 
_ALIASP:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
ALIN2=ALIN
Repeat 
   If ALIN2<0
      ALIN2=ALIN
      CN=Peek(FX)
   End If 
   Dec ALIN2
   CN2=CN
   Poke FX,CN2
   Inc FX
Until FX>FXE
Return 
'
ALDRW:
ALIN=FXN/2
FXSN=ALIN
Return 
'
_FOLD:
Gosub SETREALTIME
If FXSHOW
   Inc PASS
   If PASS<2
      Gosub SETPBANK
      If FX_LENGTH<2
         Goto FLSHSCR
      End If 
      CHAIN$(1)="_FOLDP"
      Gosub _OPENFXWIN
      FXN=FOLDN : FXSN=FOLDN : FXS$=_PERCENT$ : Gosub FXDRW
      FXRUN$="FODRW"
      Gosub _RUNFXWIN
   End If 
   If Not REQ
      Return 
   End If 
End If 
If FOLDN<1
   Return 
End If 
_FOLDP:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Repeat 
   CN=Peek(FX)
   Gosub FLOAT
   If CN#>FOLDV# Then CN#=FOLDV#-(CN#-FOLDV#)
   If CN#<-FOLDV# Then CN#=-FOLDV#+(-FOLDV#-CN#)
   Gosub UNFLOAT
   Poke FX,CN
   Inc FX
Until FX>FXE
Return 
'
FODRW:
FOLDN=FXN
FOLDV#=FOLDN/256.0
FXSN=FOLDN
Return 
'
_2BIT:
Gosub SETREALTIME
If FXSHOW
   Inc PASS
   If PASS<2
      Gosub SETPBANK
      If FX_LENGTH<2
         Goto FLSHSCR
      End If 
      CHAIN$(1)="_2BITP"
      Gosub _OPENFXSMALLWIN
      Gosub _RUNFXWIN
   End If 
   If Not REQ
      Return 
   End If 
End If 
_2BITP:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Repeat 
   CN=Peek(FX)
   Rol.b 8,CN
   Bclr 7,CN
   Bclr 5,CN
   Bclr 4,CN
   Bclr 3,CN
   Bclr 2,CN
   Bclr 1,CN
   Bclr 0,CN
   Poke FX,CN-32
   Inc FX
Until FX>FXE
Return 
'  
'
'
'***************************************************************************** 
'*effects********************************************************************* 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'   .,:::::: .-:::::' .-:::::'.,::::::  .,-:::::::::::::::: .::::::.     
'   ;;;;'''' ;;;'''', ;;;'''',;;;;'''',;;;'````' ;;;;;;;''';;;`    `;    
'    [[cccc  [[[,,==[ [[[,,==[ [[cccc [[[            [[    '[==/[[[[,  
'    $$""""  `$$$"``" `$$$"``" $$"""" $$$            $$      '''    $  
'    888oo,__ 888    _ 888     888oo,_`88bo,__,o,    88,   ,88b    dP  
'    """"YUMMM"MM,   MM"MM,    """"YUM  "YUMMMMMP    MMM     "YMmMY"   
'
'                                                              
'
_PHASE:
Gosub SETREALTIME
If FXSHOW
   Inc PASS
   If PASS<2
      Gosub SETPBANK
      If FX_LENGTH<2
         Goto FLSHSCR
      End If 
      Gosub FXBUFFER
      FXBC=1
      Repeat 
         PHBUF#(FXBC)=0.0
         PHBUF0(FXBC)=Start(_FXCUR-FXBC)
         PHBUF1(FXBC)=PHBUF0(FXBC)
         Inc FXBC
      Until FXBC>_CH
      CHAIN$(1)="_PHASEP"
      Gosub _OPENFXWIN
      FXN=PHAN*2 : FXSN=PHAN : FXS$=_PERCENT$ : Gosub FXDRW
      FXRUN$="PHDRW"
      Gosub _RUNFXWIN
   End If 
   If Not REQ
      Return 
   End If 
   Gosub FXBUFFERC
End If 
_PHASEP:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Repeat 
   CN=Peek(FX)
   CN2=Peek(PHBUF0(_CHANNEL))
   Poke PHBUF0(_CHANNEL),CN
   Gosub SIGN2
   CN#=(CN2*0.6)+PHBUF#(_CHANNEL)
   CN2=Int(CN#)
   PHBUF#(_CHANNEL)=CN#-CN2
   If CN<0 and CN2<0
      CN=(CN+CN2)-((CN*CN2)/-127)
   Else If CN>0 and CN2>0
      CN=(CN+CN2)-((CN*CN2)/127)
   Else 
      CN=CN+CN2
   End If 
   Gosub UNSIGN
   Poke PHBUF0(_CHANNEL),CN
   Poke FX,CN
   Inc FX
   Inc PHBUF0(_CHANNEL)
   If PHBUF0(_CHANNEL)>PHBUF1(_CHANNEL)+PHAN
      PHBUF0(_CHANNEL)=PHBUF1(_CHANNEL)
   End If 
Until FX>FXE
Return 
'
PHDRW:
PHAN=FXN/2
FXSN=PHAN
Return 
'
_ECHO:
If Not REALTIME
   Inc PASS
   If PASS<2
      WTXT$="Echo"
      Gosub ECHO_WIN
      _ECHON=_EFFN
      If _ECHON<1 or Fn KEY_ESC
         Gosub FLSHSCR
         K$=_$
         C=0
         Return 
      End If 
   End If 
Else 
   _ECHON=200
End If 
_ECHOP:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
FX=FX+_ECHON
Repeat 
   CN=Peek(FX)
   CN2=Peek(FX-_ECHON)
   Gosub SIGN2
   CN=(CN+CN2)/1.8
   Gosub UNSIGN
   Poke FX,CN
   Inc FX
Until FX>FXE
Return 
'
_REVERB:
Gosub SETREALTIME
If FXSHOW
   Inc PASS
   If PASS<2
      Gosub SETPBANK
      If FX_LENGTH<2
         Goto FLSHSCR
      End If 
      Gosub FXBUFFER
      FXBC=1
      Repeat 
         RVBUF0(FXBC)=Start(_FXCUR-FXBC)
         RVBUF1(FXBC)=RVBUF0(FXBC)
         Inc FXBC
      Until FXBC>_CH
      CHAIN$(1)="_REVERBP"
      Gosub _OPENFXWIN
      FXN=RVN*2 : FXSN=RVN : FXS$=_PERCENT$ : Gosub FXDRW
      FXRUN$="REDRW"
      Gosub _RUNFXWIN
   End If 
   If Not REQ
      Return 
   End If 
   Gosub FXBUFFERC
End If 
_REVERBP:
Gosub SETPBANK
If FX_LENGTH<2 or _FXCUR=-1 Then Goto FLSHSCR
Repeat 
   CN=Peek(FX)
   CN2=Peek(RVBUF0(_CHANNEL))
   Poke RVBUF0(_CHANNEL),CN
   Gosub SIGN2
   CN#=CN2*RVV#
   CN2=Int(CN#)
   If CN<0 and CN2<0
      CN=(CN+CN2)-((CN*CN2)/-127)
   Else If CN>0 and CN2>0
      CN=(CN+CN2)-((CN*CN2)/127)
   Else 
      CN=CN+CN2
   End If 
   Gosub UNSIGN
   Poke RVBUF0(_CHANNEL),CN
   Poke FX,CN
   Inc FX
   Inc RVBUF0(_CHANNEL)
   If RVBUF0(_CHANNEL)>RVBUF1(_CHANNEL)+(RVN*100) Then RVBUF0(_CHANNEL)=RVBUF1(_CHANNEL)
Until FX>FXE
Return 
'
REDRW:
RVN=FXN/2
FXSN=RVN
Return 
'
_TREMOLO:
Gosub SETREALTIME
If FXSHOW
   Inc PASS
   If PASS<2
      Gosub SETPBANK
      If FX_LENGTH<2
         Goto FLSHSCR
      End If 
      FXBC=1
      Repeat 
         TBUF(FXBC)=0
         Inc FXBC
      Until FXBC>_CH
      FX_LENGTH2=Length(PBANK)
      CHAIN$(1)="_TREMOLOP"
      Gosub _OPENFXWIN
      FXN=TREMN : FXSN=TREMN : FXS$=_PERCENT$ : Gosub FXDRW
      FXRUN$="TRDRW"
      Gosub _RUNFXWIN
   End If 
   If Not REQ
      Return 
   End If 
End If 
_TREMOLOP:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Repeat 
   CN2#=(_2PI#*TREMN#)/FREQ
   CN3#=Sin(CN2#*(TBUF(_CHANNEL)))
   CN=Peek(FX)
   Gosub SIGN
   CN#=CN*CN3#
   CN=Int(CN#)
   Gosub UNSIGN
   Poke FX,CN
   Inc FX
   Inc TBUF(_CHANNEL)
   If TBUF(_CHANNEL)>FX_LENGTH2 Then TBUF(_CHANNEL)=0
Until FX>FXE
Return 
'
TRDRW:
TREMN=FXN
TREMN#=TREMN/16.0
FXSN=TREMN
Return 
'
_COMPRESSOR:
Gosub SETREALTIME
If FXSHOW
   Inc PASS
   If PASS<2
      Gosub SETPBANK
      If FX_LENGTH<2
         Goto FLSHSCR
      End If 
      FXBC=1
      Repeat 
         CBUFENV#(FXBC)=0
         CBUFSUMM#(FXBC)=0
         Inc FXBC
      Until FXBC>_CH
      Gosub CPSET
      FX2=P_START
      CHAIN$(1)="_COMPRESSORP"
      Gosub _OPENFXBIGWIN
      Gosub DIALCOMP
      FXRUN$=_$
      Gosub _RUNFXWIN
   End If 
   If Not REQ
      Return 
   End If 
End If 
_COMPRESSORP:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Gosub CPSET
If _RMS<22 Then _RMS=22
Repeat 
   If FX2+_OFFSET<P_START+P_LENGTH
      CN=Peek(FX2+_OFFSET)
      CN2=Peek(FX2+_OFFSET-_RMS)
      Gosub FLOAT2
   Else 
      FX2=P_START
      CN#=0 : CN2#=0
   End If 
   CBUFSUMM#(_CHANNEL)=CBUFSUMM#(_CHANNEL)+(CN#*CN#)
   If FX2>_RMS Then CBUFSUMM#(_CHANNEL)=CBUFSUMM#(_CHANNEL)-(CN2#*CN2#)
   If CBUFSUMM#(_CHANNEL)<0 Then CBUFSUMM#(_CHANNEL)=-CBUFSUMM#(_CHANNEL)
   _RMS#=Sqr(CBUFSUMM#(_CHANNEL)/_RMS)
   If _RMS#>CBUFENV#(_CHANNEL) Then _THETA#=_ATT# Else _THETA#=_REL#
   CBUFENV#(_CHANNEL)=(1.0-_THETA#)*_RMS#+_THETA#*CBUFENV#(_CHANNEL)
   _GAIN#=1.0
   If(CBUFENV#(_CHANNEL)>_THRESHOLD#) Then _GAIN#=_GAIN#-(CBUFENV#(_CHANNEL)-_THRESHOLD#)*_SLOPE#
   CN=Peek(FX)
   Gosub FLOAT
   CN=Int((CN#*_GAIN#)*127.0)
   Gosub UNSIGN
   Poke FX,CN
   Inc FX : Inc FX2
Until FX>FXE
Return 
'  
CPSET:
_THRESHOLD#=_THRESHOLDN#*0.01
_SLOPE#=_SLOPEN#*0.1
_LOOKAHEAD#=_LOOKAHEADN#*0.001
_WINDOW#=_WINDOWN#*0.1
If _WINDOW#<=0 Then _WINDOW#=0.1
_ATTACK#=_ATTACKN#*0.001
If _ATTACK#<=0 Then _ATTACK#=0.001
_RELEASE#=_RELEASEN#*0.001
If _RELEASE#<=0 Then _RELEASE#=0.001
_ATT#=Exp(-1.0/(FREQ*_ATTACK#))
_REL#=Exp(-1.0/(FREQ*_RELEASE#))
_OFFSET=Int(FREQ*_LOOKAHEAD#)
_RMS=Int(FREQ*_WINDOW#)
Return 
'  
'
'
'***************************************************************************** 
'*bitwise********************************************************************* 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'   :::::::.   :::  ::::::::::.::    .   .:: :::   .::::::. .,:::::: 
'    ;;;'';;'  ;;;  ;;;;;;;'''';;,  ;;  ;;;' ;;;  ;;;`    `;;;;;'''' 
'    [[[__[[\. [[[      [[     '[[, [[, [['  [[[  '[==/[[[[, [[cccc
'    $$""""Y$$ $$$,     $$       Y$c$$$c$P   $$$,   '''    $ $$""""
'   _88o,,od8Po88888    88,       "88"888   o88888,88b    dP 888oo,_ 
'   ""YUMMMP" MMM"MM    MMM        "M "M"   MMM"MM  "YMmMY"  """"YUM 
'
'
'
_ROTATELEFT:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Gosub _UNDOSET
Repeat 
   CN=Peek(FX)
   Rol.b 1,CN
   Poke FX,CN
   Inc FX
Until FX>FXE
Return 
'
_ROTATERIGHT:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Gosub _UNDOSET
Repeat 
   CN=Peek(FX)
   Ror.b 1,CN
   Poke FX,CN
   Inc FX
Until FX>FXE
Return 
'
_SHIFTLEFT:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Gosub _UNDOSET
_MSB=False
Repeat 
   CN=Peek(FX)
   _MSB=Btst(7,CN)
   Bclr 0,CN
   Rol.b 1,CN
   If _MSB Then Bset 7,CN Else Bclr 7,CN
   Poke FX,CN
   Inc FX
Until FX>FXE
Return 
'
_SHIFTRIGHT:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Gosub _UNDOSET
_MSB=False
Repeat 
   CN=Peek(FX)
   _MSB=Btst(7,CN)
   Ror.b 1,CN
   If _MSB Then Bset 7,CN Else Bclr 7,CN
   Poke FX,CN
   Inc FX
Until FX>FXE
Return 
'  
_LOGICALSHIFTLEFT:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Gosub _UNDOSET
Repeat 
   CN=Peek(FX)
   Rol.b 1,CN
   Bclr 7,CN
   Poke FX,CN
   Inc FX
Until FX>FXE
Return 
'  
_LOGICALSHIFTRIGHT:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Gosub _UNDOSET
Repeat 
   CN=Peek(FX)
   Ror.b 1,CN
   Bclr 0,CN
   Bset 7,CN
   Poke FX,CN
   Inc FX
Until FX>FXE
Return 
'  
'
'
'***************************************************************************** 
'*convert********************************************************************* 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'     .,-::::::    ...     ::.    :::.:::      .::..,:::::::::::::..  :::::::::: 
'   ,;;;'````'  .;;;;;;;.  ;;;;,  `;;;';;,   ,;;;' ;;;;'''';;;;``;;;; ;;;;;;;''' 
'   [[[        ,[[     \[[, [[[[[. '[[ \[[  .[[/    [[cccc \[[[,/[[['     [['    
'   $$$        $$$,     $$$ $$$ "Y$c$$  Y$c.$$"     $$"""" "$$$$$$c       $$ 
'   `88bo,__,o,"888,_ _,88P888    Y88    Y88P       888oo,_,888b "88bo    88,
'     "YUMMMMMP  "YMMMMMP" MM     YM      MP        """"YUM"MMMM   "W"    MMM
'
'
'
_8BITUNSIGNEDTO8BIT:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Gosub _UNDOSET
Repeat 
   Poke FX,Peek(FX)-128
   Inc FX
Until FX>FXE
Return 
'  
_16BITTO8BIT:
_FLIP=False
Goto P16BIT
'
_16BITTO8BITFLIP:
_FLIP=True
'
P16BIT:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
FX_LENGTH2=FX_LENGTH/2
Trap Reserve As Work WBANK,FX_LENGTH2
If Errtrap Then Goto NOMEMORY
Gosub _UNDOSET
CN=0
FX2=FX
If _FLIP
   Repeat 
      CN3=Varptr(CN)
      Poke CN3+1,Peek(FX) : Inc FX
      Poke CN3,Peek(FX) : Inc FX
      Poke FX2,Deek(CN3)/256
      Inc FX2
   Until FX>FXE
Else 
   Repeat 
      Poke FX2,Deek(FX)/256 : Add FX,2
      Inc FX2
   Until FX>FXE
End If 
Copy FX_START,FX_START+FX_LENGTH2 To Start(WBANK)
Gosub _WORKSWAP
Gosub _CLEARWORK
Gosub _RESIZED
Return 
'
_24BITTO8BIT:
_FLIP=False
Goto P24BIT8
'
_24BITTO8BITFLIP:
_FLIP=True
'  
P24BIT8:
Gosub SETPBANK
FX_LENGTH2=FX_LENGTH/3
If FX_LENGTH<2 Then Goto FLSHSCR
Trap Reserve As Work WBANK,FX_LENGTH2
If Errtrap Then Goto NOMEMORY
Gosub _UNDOSET
CN=0
FX2=FX
Randomize Timer
If _FLIP
   Repeat 
      CN3=Varptr(CN)
      Poke CN3+3,Peek(FX) : Inc FX
      Poke CN3+2,Peek(FX) : Inc FX
      Poke CN3+1,Peek(FX) : Inc FX
      Poke CN3,0
      Gosub DITHER
      CN=Leek(CN3)+CN2
      Poke FX2,CN/65536
      Inc FX2
   Until FX>FXE
Else 
   Repeat 
      CN3=Varptr(CN)
      Poke CN3,0
      Poke CN3+1,Peek(FX) : Inc FX
      Poke CN3+2,Peek(FX) : Inc FX
      Poke CN3+3,Peek(FX) : Inc FX
      Gosub DITHER
      CN=Leek(CN3)+CN2
      Poke FX2,CN/65536
      Inc FX2
   Until FX>FXE
End If 
Copy FX_START,FX_START+FX_LENGTH2 To Start(WBANK)
Gosub _WORKSWAP
Gosub _CLEARWORK
Gosub _RESIZED
Return 
'
_24BITTO16BIT:
_FLIP=False
Goto P24BIT16
'
_24BITTO16BITFLIP:
_FLIP=True
'
P24BIT16:
Gosub SETPBANK
FX_LENGTH2=FX_LENGTH-(FX_LENGTH/3)
If FX_LENGTH<2 Then Goto FLSHSCR
Trap Reserve As Work WBANK,FX_LENGTH2
If Errtrap Then Goto NOMEMORY
Gosub _UNDOSET
CN=0
FX2=FX
Randomize Timer
If _FLIP
   Repeat 
      CN3=Varptr(CN)
      Poke CN3+3,Peek(FX) : Inc FX
      Poke CN3+2,Peek(FX) : Inc FX
      Poke CN3+1,Peek(FX) : Inc FX
      Poke CN3,0
      Gosub DITHER
      CN=Leek(CN3)+CN2
      Doke FX2,CN/256
      Add FX2,2
   Until FX>FXE
Else 
   Repeat 
      CN3=Varptr(CN)
      Poke CN3,0
      Poke CN3+1,Peek(FX) : Inc FX
      Poke CN3+2,Peek(FX) : Inc FX
      Poke CN3+3,Peek(FX) : Inc FX
      Gosub DITHER
      CN=Leek(CN3)+CN2
      Doke FX2,CN/256
      Add FX2,2
   Until FX>FXE
End If 
Copy FX_START,FX_START+FX_LENGTH2 To Start(WBANK)
Gosub _WORKSWAP
Gosub _CLEARWORK
Gosub _RESIZED
Return 
'
_32BITTO8BIT:
_FLIP=False
Goto P32BIT8
'
_32BITTO8BITFLIP:
_FLIP=True
'
P32BIT8:
If Not MATH Then Goto NOMATH
Gosub SETPBANK
FX_LENGTH2=FX_LENGTH/4
If FX_LENGTH<2 Then Goto FLSHSCR
Trap Reserve As Work WBANK,FX_LENGTH2
If Errtrap Then Goto NOMEMORY
Gosub _UNDOSET
CN=0 : CN#=0
FX2=FX
If _FLIP
   Repeat 
      CN3=Varptr(CN)
      CN2=Varptr(CN#)
      Poke CN3+3,Peek(FX) : Inc FX
      Poke CN3+2,Peek(FX) : Inc FX
      Poke CN3+1,Peek(FX) : Inc FX
      Poke CN3,Peek(FX) : Inc FX
      Dreg(0)=CN
      CN=Lib Call(1,-108) : Rem _LVOIEEESPFiee 
      Loke CN2,Leek(CN3)
      CN=Int(CN#*127)
      Poke FX2,CN
      Inc FX2
   Until FX>FXE
Else 
   Repeat 
      CN3=Varptr(CN)
      CN2=Varptr(CN#)
      Loke CN3,Leek(FX) : Add FX,4
      Dreg(0)=CN
      CN=Lib Call(1,-108) : Rem _LVOIEEESPFiee 
      Loke CN2,Leek(CN3)
      CN=Int(CN#*127)
      Poke FX2,CN
      Inc FX2
   Until FX>FXE
End If 
Copy FX_START,FX_START+FX_LENGTH2 To Start(WBANK)
Gosub _WORKSWAP
Gosub _CLEARWORK
Gosub _RESIZED
Return 
'
_32BITTO16BIT:
_FLIP=False
Goto P32BIT16
'
_32BITTO16BITFLIP:
_FLIP=True
'
P32BIT16:
If Not MATH Then Goto NOMATH
Gosub SETPBANK
FX_LENGTH2=FX_LENGTH/2
If FX_LENGTH<2 Then Goto FLSHSCR
Trap Reserve As Work WBANK,FX_LENGTH2
If Errtrap Then Goto NOMEMORY
Gosub _UNDOSET
CN=0 : CN#=0
FX2=FX
If _FLIP
   Repeat 
      CN3=Varptr(CN)
      CN2=Varptr(CN#)
      Poke CN3+3,Peek(FX) : Inc FX
      Poke CN3+2,Peek(FX) : Inc FX
      Poke CN3+1,Peek(FX) : Inc FX
      Poke CN3,Peek(FX) : Inc FX
      Dreg(0)=CN
      CN=Lib Call(1,-108) : Rem _LVOIEEESPFiee 
      Loke CN2,Leek(CN3)
      CN=Int(CN#*32767)
      Doke FX2,CN
      Add FX2,2
   Until FX>FXE
Else 
   Repeat 
      CN3=Varptr(CN)
      CN2=Varptr(CN#)
      Loke CN3,Leek(FX) : Add FX,4
      Dreg(0)=CN
      CN=Lib Call(1,-108) : Rem _LVOIEEESPFiee 
      Loke CN2,Leek(CN3)
      CN=Int(CN#*32767)
      Doke FX2,CN
      Add FX2,2
   Until FX>FXE
End If 
Copy FX_START,FX_START+FX_LENGTH2 To Start(WBANK)
Gosub _WORKSWAP
Gosub _CLEARWORK
Gosub _RESIZED
Return 
'
_32BITTO24BIT:
_FLIP=False
Goto P32BIT24
'
_32BITTO24BITFLIP:
_FLIP=True
'
P32BIT24:
If Not MATH Then Goto NOMATH
Gosub SETPBANK
FX_LENGTH2=FX_LENGTH-(FX_LENGTH/4)
If FX_LENGTH<2 Then Goto FLSHSCR
Trap Reserve As Work WBANK,FX_LENGTH2
If Errtrap Then Goto NOMEMORY
Gosub _UNDOSET
CN=0 : CN#=0
FX2=FX
If _FLIP
   Repeat 
      CN3=Varptr(CN)
      CN2=Varptr(CN#)
      Poke CN3+3,Peek(FX) : Inc FX
      Poke CN3+2,Peek(FX) : Inc FX
      Poke CN3+1,Peek(FX) : Inc FX
      Poke CN3,Peek(FX) : Inc FX
      Dreg(0)=CN
      CN=Lib Call(1,-108) : Rem _LVOIEEESPFiee 
      Loke CN2,Leek(CN3)
      CN=Int(CN#*8388607)
      Poke FX2,Peek(Varptr(CN)+1) : Inc FX2
      Poke FX2,Peek(Varptr(CN)+2) : Inc FX2
      Poke FX2,Peek(Varptr(CN)+3) : Inc FX2
   Until FX>FXE
Else 
   Repeat 
      CN3=Varptr(CN)
      CN2=Varptr(CN#)
      Loke CN3,Leek(FX) : Add FX,4
      Dreg(0)=CN
      CN=Lib Call(1,-108) : Rem _LVOIEEESPFiee 
      Loke CN2,Leek(CN3)
      CN=Int(CN#*8388607)
      Poke FX2,Peek(Varptr(CN)+1) : Inc FX2
      Poke FX2,Peek(Varptr(CN)+2) : Inc FX2
      Poke FX2,Peek(Varptr(CN)+3) : Inc FX2
   Until FX>FXE
End If 
Copy FX_START,FX_START+FX_LENGTH2 To Start(WBANK)
Gosub _WORKSWAP
Gosub _CLEARWORK
Gosub _RESIZED
Return 
'
'
'
'***************************************************************************** 
'***encoding****************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'   .,::::::::.    :::.  .,-::::::    ...     ::::::-.   :::  ::.    :::. .,-::::: 
'   ;;;;'''';;;;,  `;;;,;;;'````'  .;;;;;;;.  ;;,   `';, ;;;  ;;;;,  `;;;;;-'````' 
'    [[cccc  [[[[[. '[[[[[        ,[[     \[[,`[[     [[ [[[   [[[[[. '[[[[   [[[[ 
'    $$""""  $$$ "Y$c$$$$$        $$$,     $$$ $$,    $$ $$$,  $$$ "Y$c$$$$c.    " 
'    888oo,_888    Y88 `88bo,__,o,"888,_ _,88P 888_,o8P'o88888888    Y88 `Y8bo,,,o 
'    """"YUMMM     YM    "YUMMMMMP  "YMMMMMP" "MMMMP"`  MMM"MMMM     YM    `'YMUP" 
'  
'
'
_INTERLEAVE:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
FX_LENGTH2=FX_LENGTH
Trap Reserve As Work WBANK,FX_LENGTH2
If Errtrap Then Goto NOMEMORY
Gosub _UNDOSET
FX_START2=Start(WBANK)
Copy FX_START,FX_START+FX_LENGTH To FX_START2
FX_LENGTH3=FX_LENGTH2/2
FX2=FX_START2
Repeat 
   Poke FX,Peek(FX2) : Inc FX
   Poke FX,Peek(FX2+FX_LENGTH3) : Inc FX
   Inc FX2
Until FX>FXE
Goto _CLEARWORK
'
_DEINTERLEAVE:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
FX_LENGTH2=FX_LENGTH/2
Trap Reserve As Work WBANK,FX_LENGTH2
If Errtrap Then Goto NOMEMORY
Gosub _UNDOSET
FX_START2=Start(WBANK)
FX2=0
Add FXE,-2
Repeat 
   Poke FX_START+FX2,Peek(FX)
   Inc FX
   Poke FX_START2+FX2,Peek(FX)
   Inc FX
   Inc FX2
Until FX>FXE
Copy FX_START2,FX_START2+FX_LENGTH2 To FX_START+FX_LENGTH2
Goto _CLEARWORK
'
_DEINTERLEAVE16BIT:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
FX_LENGTH2=FX_LENGTH/2
Trap Reserve As Work WBANK,FX_LENGTH2
If Errtrap Then Goto NOMEMORY
Gosub _UNDOSET
FX_START2=Start(WBANK)
FX2=0
Add FXE,-4
Repeat 
   Loke FX_START+FX2,Leek(FX)
   Add FX,2
   Loke FX_START2+FX2,Leek(FX)
   Add FX,2
   Add FX2,2
Until FX>FXE
Copy FX_START2,FX_START2+FX_LENGTH2 To FX_START+FX_LENGTH2
Goto _CLEARWORK
'  
'**************************************************************************
'  Steve Hayes Fibonacci Delta sound compression technique         
'
_DECODEFIBONACCIDELTA:
Gosub SETPBANK
RST=0 : RL=P_LENGTH
G$="FIBDELDEC"
Gosub G$
Gosub STEREOQUADX2
RST=0 : RL=0
Gosub _RESIZED
COMP(B)=0
Return 
'
FIBDELDEC:
Gosub SETPBANK
FX_LENGTH2=((FX_LENGTH-2)*2)
If FX_LENGTH<2 Then Goto FLSHSCR
Trap Reserve As Work WBANK,FX_LENGTH2
If Errtrap Then Goto NOMEMORY
Gosub _UNDOSET
FX2=Start(WBANK)
Inc FX : CN3=Peek(FX) : Inc FX : Rem a pad byte, an 8bit initial value  
_NYBBLE=False
Repeat 
   CN=Peek(FX)
   If _NYBBLE
      CN=CN and $F
      Inc FX
      _NYBBLE=False
   Else 
      Ror.b 4,CN
      Bclr 4,CN
      Bclr 5,CN
      Bclr 6,CN
      Bclr 7,CN
      _NYBBLE=True
   End If 
   Add CN3,FIBO(CN)
   Poke FX2,CN3
   Inc FX2
Until FX>FXE
If _CHANNELS<2
   Gosub _WORKSWAP
   Gosub _CLEARWORK
End If 
Return 
'
'**************************************************************************
'  
_ENCODEFIBONACCIDELTA:
Gosub SETPBANK
RST=0 : RL=P_LENGTH
G$="FIBDELENC"
Gosub G$
Gosub STEREOQUADX2
RST=0 : RL=0
Gosub _RESIZED
COMP(B)=1
Return 
'
FIBDELENC:
Gosub SETPBANK
FX_LENGTH2=(FX_LENGTH/2)+2
If FX_LENGTH<2 Then Goto FLSHSCR
Trap Reserve As Work WBANK,FX_LENGTH2
If Errtrap Then Goto NOMEMORY
Gosub _UNDOSET
FX2=Start(WBANK)
_NYBBLE=False
CN2=Peek(FX)
Gosub SIGN2
Inc FX2 : Poke FX2,CN2 : Inc FX2 : Rem a pad byte, an 8bit initial value
Repeat 
   CN=Peek(FX) : Inc FX
   Gosub SIGN
   FX3=0 : FIBMATCH=8 : DELTAHISTORY=256
   Repeat 
      CN3=CN2+FIBO(FX3)
      If CN>CN3
         DELTA=CN-CN3
      Else 
         DELTA=CN3-CN
      End If 
      If DELTA<DELTAHISTORY
         FIBMATCH=FX3
         DELTAHISTORY=DELTA
      End If 
      Inc FX3
   Until FX3>15
   CN2=CN2+FIBO(FIBMATCH)
   If _NYBBLE
      _BYTE=FIBMATCH+_BYTE
      Poke FX2,_BYTE
      Inc FX2
      _NYBBLE=False
   Else 
      Rol.b 4,FIBMATCH
      _BYTE=FIBMATCH
      _NYBBLE=True
   End If 
Until FX>FXE
If _CHANNELS<2
   Gosub _WORKSWAP
   Gosub _CLEARWORK
End If 
Return 
'
'
'  
'***************************************************************************** 
'*process********************************************************************* 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'   ::::::::. :::::::..      ...       .,-::::::.,:::::: .::::::.  .::::::.
'   `;;```.;;;;;;;``;;;;  .;;;;;;;.  ,;;;'````' ;;;;'''';;;`    `;;;;`    `; 
'   `]]nnn]]' \[[[,/[[[' ,[[     \[[,[[[         [[cccc '[==/[[[[,'[==/[[[[, 
'    $$$""    "$$$$$$c   $$$,     $$$$$$         $$""""   '''    $  '''    $ 
'    888o     ,888b "88bo"888,_ _,88P`88bo,__,o, 888oo,_,88b    dP,88b    dP 
'    YMMMb    "MMMM   "W"  "YMMMMMP"   "YUMMMMMP """"YUM  "YMmMY"   "YMmMY"
'
'
'
_REVERSE:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Trap Reserve As Work WBANK,FX_LENGTH
If Errtrap Then Goto NOMEMORY
Gosub _UNDOSET
FX2=FX_START+FX_LENGTH
Repeat 
   Dec FX2
   CN=Peek(FX2)
   Poke Start(WBANK)-FX_START+FX,CN
   Inc FX
Until FX>FXE
Copy Start(WBANK),Start(WBANK)+Length(WBANK) To FX_START
Gosub _CLEARWORK
Return 
'
_INVERT:
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Gosub _UNDOSET
Repeat 
   CN=Peek(FX)
   Gosub SIGN
   CN=-CN
   Gosub UNSIGN
   Poke FX,CN
   Inc FX
Until FX>FXE
Return 
'
'
'
'***************************************************************************** 
'*utils*********************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'    ...    ::::::::::::: :::    :::     .::::::.  
'   ';;     ;;;;;;;;;;''' ;;;    ;;;    ;;;`    `; 
'   [['     [[[    [[     [[[    [[[    '[==/[[[[, 
'   $$      $$$    $$     $$$,   $$'      '''    $ 
'   88    .d888    88,   o88888o88oo,.__,88b    dP 
'    "YmmMMMM""    MMM   MMM"MM"""YUMMMM  "YMmMY"  
'
'  
'
T0NEG:
T0NEGENERATOR=True
RX=215 : RY=59
WTXT$=S$ : WX=RX-5 : WY=RY-15 : WX2=RX+215 : WY2=RY+107 : _COL=_WHITE : Gosub WIN
Restore T0B
I=1
Repeat 
   Read BTXT1$,BX,BY,BZN,E$
   Add BX,RX : Add BY,RY : Gosub BTN
   Inc I
Until I>_TB
Ink _BLACK
Bar RX+63,RY+28 To BX+94,RY+37
Paste Bob RX+63,RY+28,3
Gosub DIALT0NE
Gosub T0SET
Repeat 
   G$=_$
   Gosub GVAL
   If Z=48 and M
      Repeat 
         Gosub GVAL
         If Z=48 and KDOWN=False
            PZN=Z : BX=269 : BY=85 : Gosub HIGHBTN
            N=0
            T$=_$
            KDOWN=True
         End If 
         If C>0 and Z=48
            Gosub PIANO
            If KEYC<>-1
               T0NEV#=KEYF/100.0
               Gosub T0SET
               ROT#(10)=(((T0NEV#-5)*4.8)/(FREQ/8))+8.5
               Gosub DIALT0NE
            End If 
         End If 
         If KDOWN and(M=0 or PZN<>Z)
            PZN=Z : BX=269 : BY=85 : Gosub HIGHBTN
            KDOWN=False
         End If 
      Until M=0
   Else If M=1
      Restore T0B
      I=1
      Repeat 
         Read BTXT1$,BX,BY,BZN,G$
         If Z=BZN
            PZN=Z : Add BX,RX : Add BY,RY : Gosub PRESS
            If _ON
               Gosub _UNDOSET
               _REPEAT$=G$
               Gosub G$
               Gosub STEREOQUAD
               G$=_$
               Exit 
            End If 
         End If 
         Inc I
      Until I>_TB+2
      If G$=_$
         Exit 
      Else 
         Gosub DRWDIALS
      End If 
   Else If Fn KEY_UPCUR
      T0NEV#=T0NEV#+1
      Gosub T0SET
   Else If Fn KEY_DOWNCUR
      T0NEV#=T0NEV#-1
      Gosub T0SET
   End If 
Until Fn KEY_ESC
Goto T0NEC
'
T0NEEXIT:
Pop 
T0NEC:
Reset Zone 48
I=58
Repeat 
   Reset Zone I
   Inc I
Until I>64
Gosub WINC
T0NEGENERATOR=False
WTXT$=_$
If F$=_$
   F$=_UNNAMED$ : Gosub SETFILENAME
End If 
If Not Fn KEY_ESC
   If G$=_$ and Length(PBANK)>0
      Goto _SHOWALL
   End If 
End If 
Return 
'
T0SET:
If T0NEV#<1
   T0NEV#=1
Else If T0NEV#>FREQ
   T0NEV#=FREQ
End If 
T0NEN=(T0NEV#/3)-2
Ink _WHITE,_SHADOW
N=Text Length(Str$(T0NEV#)+_SPACE$)
Text RX+188-N,RY-5,Space$(Len(Str$(FXSN))+1)
Text RX+212-N,RY-5,Str$(T0NEV#)-_SPACE$+_HZ$
Return 
'
T0NESET:
If Length(PBANK)=0
   S_LENGTH=FREQ*2
   Trap Reserve As Work PBANK,S_LENGTH
   Gosub SETCORDS
End If 
Return 
'
_SINTONE:
Gosub T0NESET
If Errtrap Then Goto NOMEMORY
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
N#=(_2PI#*T0NEV#)/FREQ
Repeat 
   CN=T0NEA*Sin(N#*(FX-FX_START))
   Poke FX,CN
   Inc FX
Until FX>FXE
Return 
'
_SQRTONE:
Gosub T0NESET
If Errtrap Then Goto NOMEMORY
SAWN=0
SAWMUL#=1.0
SYNTH:
Repeat 
   Gosub SETPBANK
   If FX_LENGTH<2 Then Goto FLSHSCR
   N#=(_2PI#*(T0NEV#*SAWMUL#))/FREQ
   Repeat 
      If SAWN>0
         CN=Peek(FX)
      Else 
         CN=0
      End If 
      CN2=T0NEA*(Sin(N#*(FX-FX_START)))
      If SAWN>0
         Poke FX,CN+Int(CN2/SAWMUL#)
      Else 
         Poke FX,CN2
      End If 
      Inc FX
   Until FX>FXE
   SAWMUL#=SAWMUL#+2
   Inc SAWN
Until SAWN>8
Return 
'  
_SAWTONE:
Gosub T0NESET
If Errtrap Then Goto NOMEMORY
SAWN=0
SAWMUL#=0.0
Goto SYNTH
'  
_COSTONE:
Gosub T0NESET
If Errtrap Then Goto NOMEMORY
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
N#=(_2PI#*T0NEV#)/FREQ
Repeat 
   CN=T0NEA*Cos(N#*(FX-FX_START))
   Poke FX,CN
   Inc FX
Until FX>FXE
Return 
'
_TRITONE:
Gosub T0NESET
If Errtrap Then Goto NOMEMORY
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
N#=(_2PI#*T0NEV#)/FREQ
CN#=127
CN2#=(N#*T0NEA)/2
Repeat 
   CN3#=T0NEA*Cos(N#*(FX-FX_START))
   If CN3#>0
      CN#=CN#+CN2#
   Else If CN3#<0
      CN#=CN#-CN2#
   Else 
      CN#=127
   End If 
   Poke FX,Int(CN#-127)
   Inc FX
Until FX>FXE
Return 
'
_PLSTONE:
Gosub T0NESET
If Errtrap Then Goto NOMEMORY
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
N#=(_2PI#*T0NEV#)/FREQ
Repeat 
   CN=T0NEA*Sin(N#*(FX-FX_START))
   Gosub SIGN
   If CN>0 Then CN=T0NEA Else CN=-T0NEA
   Poke FX,CN
   Inc FX
Until FX>FXE
Return 
'
_WHITENOISE:
Gosub T0NESET
If Errtrap Then Goto NOMEMORY
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Gosub _UNDOSET
Randomize Timer
Repeat 
   CN=Rnd(255)-127
   Poke FX,CN
   Inc FX
Until FX>FXE
If F$=_$
   F$=_UNNAMED$ : Gosub SETFILENAME
End If 
Return 
'
_DITHER:
Gosub T0NESET
If Errtrap Then Goto NOMEMORY
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Gosub _UNDOSET
Randomize Timer
Repeat 
   Gosub DITHER
   Poke FX,CN2
   Inc FX
Until FX>FXE
If F$=_$
   F$=_UNNAMED$ : Gosub SETFILENAME
End If 
Return 
'
DITHER:
On _DITHER Goto DI,DT,DL
CN2=0 : Return 
DI: CN2=Rnd(2)-1 : Return 
DT: R2=R1 : R1=Rnd(2)-1 : CN2=(R1-R2) : Return 
DL: R2=R1 : R1=Rnd(2)-1 : CN2=(R2+R1)/2 : Return 
'
_CRYPTO:
AA=0 : BB=0 : CC=0
ISA=$9E3779B9 : ISB=ISA : ISC=ISA : ISD=ISA : ISE=ISA : ISF=ISA : ISG=ISA : ISH=ISA
N=0
T$=_$
_EXIT=False
Gosub _OPENFXWIN
REALTIME=False
TXT1$="Passphrase:"
_TEXT=True : _EXIT=False
T$=_$
CRY_X=340 : CRY_Y=Y+72 : TXT_X=0 : N=0 : _MAX=256
Repeat 
   Gosub _TEXTINPUT
   FOFF=Len(T$)-9
   Gosub DRWCRY
   Repeat 
      Gosub GVAL
      If Z=60 and M=1 or Fn KEY_RETURN
         PZN=Z : BX=RX+2 : Gosub PRESS
         If _ON
            REQ=True
            _EXIT=True
         End If 
      Else If Z=61 and M=1 or Fn KEY_ESC
         PZN=Z : BX=RX+108 : Gosub PRESS
         If _ON
            REQ=False
            _EXIT=True
         End If 
      End If 
   Until _EXIT or C
Until _EXIT
Gosub _CLOSEFXWIN
Gosub WINC
If T$=_$ Then REQ=False
If REQ
   Gosub T0NESET
   If Errtrap
      Goto NOMEMORY
   End If 
   Gosub SETPBANK
   If FX_LENGTH<2
      Goto FLSHSCR
   End If 
   Gosub _UNDOSET
   Gosub ISET
   Gosub ISAAC
   ISI=0
   Repeat 
      ISN=RANDRSL(ISI)
      Inc ISI
      If ISI>255
         Gosub ISAAC
         ISI=0
      End If 
      ISN=(ISN mod 255)
      Poke FX,ISN
      Inc FX
   Until FX>FXE
   If F$=_$
      F$=_UNNAMED$ : Gosub SETFILENAME
   End If 
End If 
_TEXT=False
Return 
'
DRWCRY:
Ink _BLACK,_WHITE
Text CRY_X,CRY_Y,Space$(9)
If FOFF<0 Then FOFF=0
Text CRY_X-Text Length(TXT1$),CRY_Y,TXT1$+Mid$(T$,FOFF+1,Min(Len(T$)-FOFF,9))
Gr Writing %10
If N-FOFF>0 Then Text CRY_X,CRY_Y,Space$(N-FOFF)+_SCORE$+_SPACE$ Else Text CRY_X,CRY_Y,_SCORE$+_SPACE$
Gr Writing %1
Return 
'
ISET:
ISI=0 : ISN=0
Repeat 
   MM(ISI)=0
   If ISN>Len(T$)
      ISN=0
   End If 
   RANDRSL(ISI)=Asc(Mid$(T$,ISN,1))
   Inc ISI : Inc ISN
Until ISI>255
ISI=0
Repeat 
   Gosub IMIX
   Inc ISI
Until ISI>3
ISI=0
Repeat 
   ISA=ISA+RANDRSL(ISI) : ISB=ISB+RANDRSL(ISI+1) : ISC=ISC+RANDRSL(ISI+2) : ISD=ISD+RANDRSL(ISI+3)
   ISE=ISE+RANDRSL(ISI+4) : ISF=ISF+RANDRSL(ISI+5) : ISG=ISG+RANDRSL(ISI+6) : ISH=ISH+RANDRSL(ISI+7)
   Gosub IMIX
   MM(ISI)=ISA : MM(ISI+1)=ISB : MM(ISI+2)=ISC : MM(ISI+3)=ISD
   MM(ISI+4)=ISE : MM(ISI+5)=ISF : MM(ISI+6)=ISG : MM(ISI+7)=ISH
   Add ISI,8
Until ISI>255
Return 
'
ISAAC:
CC=CC+1
BB=BB+CC
IV=0
Repeat 
   XV=MM(IV)
   BIT1=IV : BIT2=3 : Gosub BITWISEAND
   TAA13=AA : TAA6=AA : TAA2=AA : TAA16=AA
   Rol.l 13,TAA13 : Ror.l 6,TAA6 : Rol.l 2,TAA2 : Ror.l 16,TAA16
   If BIT3=0
      AA=AA xor TAA13
   Else If BIT3=1
      AA=AA xor TAA6
   Else If BIT3=2
      AA=AA xor TAA2
   Else If BIT3=3
      AA=AA xor TAA16
   End If 
   BIT3=IV+128 : BIT2=255 : Gosub BITWISEAND
   Trap AA=MM(BIT3)+AA
   If Errtrap
      AA=MM(BIT3)+-AA
   End If 
   Ror.l 2,XV
   BIT3=XV : BIT2=255 : Gosub BITWISEAND
   Trap YV=MM(BIT3)+AA+BB
   If Errtrap
      Trap YV=MM(BIT3)+-AA+BB
      If Errtrap
         YV=MM(BIT3)+-AA+-BB
      End If 
   End If 
   Rol.l 10,YV
   MM(IV)=YV
   BIT3=YV : BIT2=255 : Gosub BITWISEAND
   Trap BB=MM(BIT3)+XV
   If Errtrap
      BB=MM(BIT3)+-XV
   End If 
   RANDRSL(IV)=BB
   Inc IV
Until IV>255
Return 
'
IMIX:
Rol.l 11,ISB
Ror.l 2,ISC
Rol.l 8,ISD
Ror.l 16,ISE
Rol.l 10,ISF
Ror.l 4,ISG
Rol.l 8,ISH
Ror.l 9,ISA
ISA=ISA xor ISB : Add ISD,ISA : Add ISB,ISC
ISB=ISB xor ISC : Add ISE,ISB : Add ISC,ISD
ISC=ISC xor ISD : Add ISF,ISC : Add ISF,ISC
ISD=ISD xor ISE : Add ISG,ISD : Add ISE,ISF
ISE=ISE xor ISE : Add ISH,ISE : Add ISF,ISG
ISF=ISF xor ISG : Add ISA,ISF : Add ISG,ISH
ISG=ISG xor ISH : Add ISB,ISG : Add ISH,ISA
ISH=ISH xor ISA : Add ISC,ISH : Add ISA,ISB
Return 
'
BITWISEAND:
BIT3=0
For I=0 To 31
   B1=Btst(I,BIT1)
   B2=Btst(I,BIT2)
   If B1=True and B2=True Then Bset I,BIT3 Else Bclr I,BIT3
Next I
Return 
'  
_SINNOISE:
Gosub T0NESET
If Errtrap Then Goto NOMEMORY
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Gosub _UNDOSET
CN=Rnd(1073741823)+1073741823
CN#=0
Repeat 
   CN#=Sin(CN*CN#+1)
   CN2=Int(CN#*127)
   Inc CN
   Poke FX,CN2
   Inc FX
Until FX>FXE
If F$=_$
   F$=_UNNAMED$ : Gosub SETFILENAME
End If 
Return 
'
_FILLNOISE:
Gosub T0NESET
If Errtrap Then Goto NOMEMORY
Gosub SETPBANK
If FX_LENGTH<2 Then Goto FLSHSCR
Gosub _UNDOSET
Randomize Timer
Repeat 
   CN=Rnd(255)-127
   CN2=Rnd(BSIZE-1)+1
   If FX+CN2>FXE Then CN2=FXE-FX
   FX2=FX
   S_LENGTH2=FX2+CN2
   Repeat 
      Poke FX2,CN
      Inc FX2
   Until FX2>S_LENGTH2
   FX=FX+CN2
   If FX=>FXE Then Inc FX
Until FX>FXE
If F$=_$
   F$=_UNNAMED$ : Gosub SETFILENAME
End If 
Return 
'
_TGLRECORD:
If _REC
   _REC=False
   Close 3
   If _MODE>1
      Close 4
      If _MODE>2
         Close 5 : Close 6
      End If 
   End If 
   _RECORD$="Record Output "
   Colour _HIGHLIGHT,_COLP
Else 
   If Length(PBANK)=0
      Goto FLSHSCR
   End If 
   PF$=F$
   If Fn STEREO
      WTXT$="as Stereo"
   Else If Fn QUAD
      WTXT$="as Quad"
   Else If Fn MULTI
      WTXT$="4 Track"
   Else 
      WTXT$=_$
   End If 
   WTXT$="Record "+WTXT$+": raw"
   Gosub FREQUEST
   S$=F$
   F$=PF$
   If Not FRQ
      Return 
   End If 
   If Exist(S$)
      Gosub OVERWRITE
      If Not REQ
         Return 
      End If 
   End If 
   If Fn MONO
      Open Out 3,S$
   Else If Fn STEREO
      Open Out 3,S$+"-L"
      Open Out 4,S$+"-R"
   Else 
      Open Out 3,S$+"-1"
      Open Out 4,S$+"-2"
      Open Out 5,S$+"-3"
      Open Out 6,S$+"-4"
   End If 
   _RECORD$="Stop Recording"
   _REC=True
   _COLP=Colour(_HIGHLIGHT)
   Colour _HIGHLIGHT,$F00
End If 
RECTOTAL=-1
Return 
'
REC:
If RECTOTAL=-1
   Inc RECTOTAL
   Return 
End If 
BBUFF=0 : SBCOUNT=1
If Fn MONO
   Ssave 3,P_BUF To P_BUF+BSIZE
Else If Fn STEREO
   Repeat 
      Ssave SBCOUNT+2,P_BUF+BBUFF To P_BUF+BBUFF+BSIZE
      BBUFF=BBUFF+BSIZE
      Inc SBCOUNT
   Until SBCOUNT>2
Else 
   Repeat 
      Ssave SBCOUNT+2,P_BUF+BBUFF To P_BUF+BBUFF+BSIZE
      BBUFF=BBUFF+BSIZE
      Inc SBCOUNT
   Until SBCOUNT>4
End If 
DRWREC:
RECTOTAL=RECTOTAL+BSIZE
If TGL
   If Not RENDERING
      Gosub GFXSET
      Ink _HIGHLIGHT,_PANE
      T_LENGTH=RECTOTAL
      Gosub TIME_ALL
      T$=Str$(DIV#)-_SPACE$+T$
      If Len(T$)<16
         T$=Space$(5)+T$
      Else If Len(T$)<22
         T$=_SPACE$+T$
      End If 
      Text 600-Text Length(T$),WVE_H+24+16,T$
      Gosub GFXRESTORE
   End If 
End If 
Return 
'
_OPENASSEQUENCE:
PF$=F$ : F$=_$
WTXT$="Open As: sequence"
PPROGDIR$=Dir$
If Dir$<>SEQDIR$ and Exist(SEQDIR$)
   Dir$=SEQDIR$
   Gosub CLRFILES
End If 
Gosub FREQUEST
If Not FRQ
   Dir$=PPROGDIR$
   Gosub CLRFILES
   Return 
End If 
SEQDIR$=Dir$
If Dir$<>PROGDIR$
   Dir$=PPROGDIR$
   Gosub CLRFILES
End If 
If Not Exist(SEQDIR$+F$)
   Gosub CANTFIND
   F$=PF$
   Return 
End If 
_TEXT=True
T$="SEQ "+F$
MACRO=True : Gosub CMD : MACRO=False
F$=PF$
_TEXT=False
Return 
'
'
'
'***************************************************************************** 
'*direct********************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'   ::::::-.   :::  :::::::..  .,::::::  .,-:::::::::::::::: 
'   ;;,   `';, ;;;  ;;;;``;;;; ;;;;'''',;;;'````' ;;;;;;;''' 
'   `[[     [[ [[[  \[[[,/[[['  [[cccc [[[            [[ 
'    $$,    $$ $$$, "$$$$$$c    $$"""" $$$            $$ 
'    888_,o8P'o88888,888b "88bo 888oo,_`88bo,__,o,    88,
'   "MMMMP"`  MMM"MM"MMMM   "W" """"YUM  "YUMMMMMP    MMM
'
'
'
NEWCLI:
Inc _DIRECT : _TEXT=True
Gosub NUDGEPOS
RENDERING=True
Gosub CLISHW
¯LOOP:
T$=Str$(_DIRECT)-_SPACE$ : TXT_X=0 : N=0
Print T$+"."+Dir$;"> ";_SCORE$;
_LOC=Len(Dir$)+Len(T$)+1
If _LOC>80
   Add _LOC,-80
End If 
_MAX=156-_LOC
T$=_$
Repeat 
   Gosub GVAL
   If C>0
      If Fn KEY_UPCUR and S=0
         T$=_REPEAT$
         N=Len(_REPEAT$)
      Else 
         Gosub _TEXTINPUT
      End If 
      If( Fn KEY_LEFTAMIGA and Fn KEY_D) or Fn KEY_ESC
         Goto CLIEND
      End If 
      S$="> "+T$
      Locate _LOC,
      If _NEXTLINE
         Cup 
      Else If Fn KEY_BACKSPACE
         If Len(S$)+_LOC=79
            Cup 
         End If 
      Else 
         If Len(S$)>80-_LOC
            If Fn KEY_UPCUR
               Print Left$(S$,80);
            End If 
            Locate 0,
            S$=Mid$(S$,(80-_LOC)+1)
         End If 
         Print S$;
      End If 
      _NEXTLINE=False
      If _LOC=Len(S$)>80
         Add _LOC,-80
      End If 
      If Fn KEY_BACKSPACE or Fn KEY_DEL
         If _LOC+Len(S$)<80
            Locate _LOC+Len(S$),
            If _LOC+Len(S$)<78
               Print _SPACER$;
            Else If _LOC+Len(S$)=78
               Print _SPACER$;
               Cup 
            Else If _LOC+Len(S$)=79
               Print _SPACE$;
               Locate 0, : Print _SPACE$ : Cup 
               Cup 
            End If 
         Else If _LOC+Len(S$)=80
            Locate 0,
            Print "_ ";
         Else If _LOC+Len(S$)>80
            Locate(Len(S$)-80)+_LOC,
            Print "_ ";
         End If 
      End If 
      If N=Len(T$)-1
         Print _SPACE$;
      End If 
      If Not _NEXTLINE
         If Not Fn KEY_ESC
            Writing 2,2
            If _LOC+N+2<80
               Locate _LOC+N+2, : Print _SCORE$;
               If _LOC+N+2=79
                  Cup 
               End If 
            Else If _LOC+N=80 and C-65 : Rem backspace
               Print _SCORE$;
            Else If _LOC+N+2>80
               Locate _LOC+N+2-80,
               If C-65 and C-70 : Rem backspace del
                  Print _SCORE$;
               End If 
            End If 
            Writing 0,0
         Else 
            Print _SPACE$
         End If 
      End If 
   End If 
Until Fn KEY_RETURN
Print 
S$=T$
T$=Upper$(T$)
_REPEAT$=T$
CMD:
If Upper$(S$)<>T$ Then S$=T$
PASS=0
DCN=0
Repeat 
   CMDL=Len(CMD$(DCN))
   If Left$(T$,CMDL)=CMD$(DCN)
      Inc CMDL
      If Mid$(T$,CMDL-1,1)<>"="
         Inc CMDL
      End If 
      T$=Mid$(S$,CMDL)
      Gosub CMDG$(DCN)
      CMDL=True
      Exit 
   End If 
   Inc DCN
Until DCN>_CM
If Not CMDL
   If MACRO
      G$=T$
      Trap Gosub G$
      T$=G$
      If Errtrap
         Gosub MACROERROR
      Else 
         Gosub CHECKFX
         If Not MENFX
            T$=Left$(G$,Len(G$)-1)
            Gosub CHECKFX
         End If 
         If MENFX
            MENFX=False
            If RL>1
               Inc PASS
               Gosub STEREOQUAD
            End If 
         End If 
      End If 
      Return 
   Else 
      Gosub MACROSET
      Trap Gosub T$
      If Errtrap
         Gosub MACRORESTORE
         If T$<>_$
            Print "Unknown command/subroutine: ";T$
         End If 
      Else 
         If _REPEAT$="_INIT"
            Goto ¯EXIT
         End If 
         Gosub CHECKFX
         If Not MENFX
            T$=Left$(T$,Len(T$)-1)
            Gosub CHECKFX
         End If 
         If MENFX
            MENFX=False
            G$=T$
            If RL>1
               Gosub STEREOQUAD
            End If 
            'from MCHOOSE
            RENDERING=True
            Gosub _DRAW
            Gosub FRESHSEL
            Gosub CLRPOS
         End If 
         Gosub MACRORESTORE
         If _TOAST$<>_$
            Print _TOAST$
            _TOAST$=_$
         End If 
      End If 
   End If 
End If 
If Not MACRO
   If Not _REPEATING
      Goto ¯LOOP
   End If 
End If 
Return 
'
CLISHW:
Gosub R4INBOW
Trap Screen Show 2
If Errtrap
   Screen Open 2,SCR_W,CLI_H,2,Hires
   Palette $0,$DDD,,,,,,,,,,,,,,,,$444,$500,$111
   Flash Off : Curs Off 
Else 
   Screen 2 : Screen To Front 2
   Cls 
End If 
If Not BGPLYING
   I#=-90 : DD#=1.5
   Repeat 
      Screen Display 2,128,Int(I#),320,CLI_H
      Gosub TASK
      I#=I#+DD#
      DD#=DD#+3.25
   Until I#>CLI_Y
End If 
CLISCR:
Screen Display 2,128,CLI_Y,320,CLI_H
Return 
'
CLIMVE:
If M=1 and((CY>0 and CY<HDR_H) or _DIRECTMOVE)
   CLI_Y=Y Mouse-5
   _DIRECTMOVE=True
   Goto CLISCR
Else 
   _DIRECTMOVE=False
End If 
Return 
'
¯EXIT:
Pop 
CLIEND:
If Not BGPLYING
   I#=CLI_Y
   Repeat 
      Screen Display 2,128,Int(I#),320,CLI_H
      Gosub TASK
      I#=I#-DD#
   Until I#<-90
End If 
Screen Hide 2 : Screen 0
If Fn MULTI Then Gosub R4INSET
Dec _DIRECT
If _DIRECT>0 Then Goto CLISHW
RENDERING=False : _TEXT=False
Return 

'
¯BANK:
B=Val(T$)
If B>_FL Then B=_FL
Gosub MACROSET
Gosub SETBANK
Goto MACRORESTORE
'
¯SWAP:
N=Val(T$)
If B=N Then Return 
If N>_FL Then N=_FL
Gosub MACROSET
Bank Swap PBANK,N+7
Swap FB$(B),FB$(N)
Swap FPATH$(B),FPATH$(N)
If Fn MULTI
   If B<5
      If N<5
         Swap D_LENGTH(B),D_LENGTH(N)
      Else 
         D_LENGTH(B)=Length(PBANK)
      End If 
   Else If N<5
      D_LENGTH(N)=Length(N+7)
   End If 
End If 
If B<11
   F$=Right$(MENB$(B),8)
   If N<11
      Right$(MENB$(B),8)=Right$(MENB$(N),8)
   Else 
      Right$(MENB$(B),8)=Left$(FB$(B),8)
   End If 
Else 
   F$=Space$(8)
   Right$(F$,8)=Left$(FB$(N),8)
End If 
If N<11
   Right$(MENB$(N),8)=F$
End If 
F$=FB$(B)
If RENDERING=False and SETTING$(27)<>_FALSE$
   Gosub POSCAL
   RENDERING=True
   Gosub _DRAW
   Gosub HEADER
   Gosub CLRPOS
   RENDERING=False
End If 
Goto MACRORESTORE
'
¯MODE:
_MODEP=Val(T$)
Gosub MACROSET
_M3SEL=_MODEP
Gosub M0DEMENU
Goto MACRORESTORE
'
¯DIV:
Gosub MACROSET
Gosub TIMEDIV
Goto MACRORESTORE
'
¯RNG:
R=Val(T$)
If R<1 or R>Int(_TIMEDIV#) Then Goto FLSHSCR
Gosub MACROSET
RL=Int(P_LENGTH/_TIMEDIV#) : RST=(R*(P_LENGTH/_TIMEDIV#))-RL : RFIN=P_LENGTH-RST-RL
Gosub CLRPOS
Gosub SETCORDS
RENDERING=True
Gosub SH0WWAVE
Gosub FRESHSEL
RENDERING=False
Gosub SETZ0NES
Gosub HEADERCORDS
Goto MACRORESTORE
'
¯FREQ:
TVAL=Val(T$)
If TVAL>0 and TVAL<MFREQ
   Gosub MACROSET
   FREQ=TVAL
   _AMIHZ=False
   RENDERING=False
   Gosub SETHZ : Rem round to CLOCK 
   Gosub DIALMAIN
   FREQ=TVAL : Gosub DRWTIME : Rem redisplay actual chosen frequency  
   RENDERING=True
   Gosub MACRORESTORE
End If 
Return 
'
¯VOL:
TVAL=Val(T$)
If TVAL<0 or TVAL>64 Then Goto ¯BADARGS
VOL=TVAL : ROT#(1)=(VOL/13.3333)+8.5
Gosub MACROSET
Gosub SETVOL
Goto MACRORESTORE
'
¯SIZE:
S_LENGTH=Val(T$)
Gosub MACROSET
If S_LENGTH=0
   Gosub _NEW
   Gosub MACRORESTORE
   Return 
End If 
Trap Reserve As Work WBANK,S_LENGTH
If Errtrap
   Gosub NOMEMORY
   Gosub MACRORESTORE
   Return 
End If 
If Length(PBANK)>0
   If S_LENGTH>Length(PBANK)
      Copy Start(PBANK),Start(PBANK)+Length(PBANK) To Start(WBANK)
   Else 
      Copy Start(PBANK),Start(PBANK)+S_LENGTH To Start(WBANK)
   End If 
End If 
S_LENGTH=Length(PBANK)
Gosub _WORKSWAP
If S_LENGTH>Length(PBANK) Then Gosub _RANGENONE
Gosub _RESIZED
Goto MACRORESTORE
'
¯NAME: NAME$(B)=T$ : Return 
'
¯CPYR: CPYR$(B)=T$ : Return 
'
¯AUTH: AUTH$(B)=T$ : Return 
'
¯ANNO: ANNO$(B)=T$ : Return 
'
¯WAND:
XD2=Val(T$)
XD=XD2-(640/_TIMEDIV#)
If XD<0 Then XD=0
Goto WAND
¯ZOOML:
XD=Val(T$)
Goto WAND
¯ZOOMR:
XD2=Val(T$)
WAND:
Gosub MACROSET
Gosub MAGICWAND
If SETTING$(27)<>_FALSE$
   If RENDERING=False and _MENUDO=False and OSCILLO=False
      Gosub WREFRSH
   End If 
End If 
Goto MACRORESTORE
'
¯CDDD:
T$="/"
¯CD:
If T$=_$
   Print Dir$
Else If Exist(T$)
   Dir$=T$
   PROGDIR$=Dir$
   Gosub CLRFILES
Else 
   Goto ¯CANTFIND
End If 
Return 
'
¯MD:
If T$=_$
   Gosub ¯BADARGS
Else If Exist(T$)
   Print T$;" exists"
Else 
   Mkdir T$
End If 
Return 
'
¯DIR:
If T$=_$
   Goto ¯D
Else If Exist(T$)
   PPROGDIR$=Dir$ : Dir$=T$
   Gosub ¯D
   Dir$=PPROGDIR$ : PROGDIR$=PPROGDIR$
Else 
   Goto ¯CANTFIND
End If 
Return 
'
¯D:
Gosub SETFILENAMELENGTH
D$=Dir First$(_$)
Repeat 
   Print Left$(D$,30)
   D$=Dir Next$
Until D$=_$
Return 
'
¯DEL:
If T$=_$
   Goto ¯BADARGS
Else If Exist(T$)
   Kill T$
Else 
   Goto ¯CANTFIND
End If 
Return 
'
¯CLS:
Cls 
Return 
'
¯ED:
If T$=_$
   Goto ¯BADARGS
Else 
   Gosub _STOP
   Amos To Back 
   Exec 'ed "'+PROGDIR$+T$-'"'+'"'
   Amos To Front 
End If 
Return 
'
¯OPEN:
If T$=_$ Then Goto ¯BADARGS
Gosub MACROSET
F$=T$
If Instr(F$,_COLON$)=0 and Instr(F$,"/")>0
   F$=PROGDIR$+F$
End If 
_APPEND=B-1
Gosub _OPENF
Goto MACRORESTORE
'
¯MACRO:
If T$=_$ Then Goto ¯BADARGS
Gosub MACROSET
S$=T$
Gosub MACROP
Goto MACRORESTORE
'
¯SEQ:
If T$=_$
   Goto ¯BADARGS
Else If Exist(SEQDIR$+T$)
   Gosub MACROSET
   Trap Open In 1,SEQDIR$+T$
   If Errtrap
      F$=T$ : Gosub FILEERROR
   Else 
      N=Lof(1)
      If N> Fn BUFFER
         Gosub NOSPACE
      Else 
         SEQ$=Space$(N)
         Sload 1 To Varptr(SEQ$),N
         Close 1
         If Instr(SEQ$,_COMMA$+_NEWLINE$)=False
            TXT1$="Not a" : TXT2$="sequence"
            If _DIRECT
               Print TXT1$;_SPACE$;TXT2$
            Else 
               Gosub _NOTICE
            End If 
            SEQ$=_$
         End If 
      End If 
   End If 
   Gosub MACRORESTORE
Else 
   Gosub ¯CANTFIND
End If 
Return 
'
¯RX:
If Not REXX
   Print "Arexx: not active"
Else If T$=_$
   Goto ¯BADARGS
Else If Exist(RXDIR$)
   Gosub MACROSET
   Exec "run "+RXDIR$+_SPACE$+T$
   Gosub SPIN
   Gosub MACRORESTORE
Else 
   T$=RXDIR$
   Gosub ¯CANTFIND
End If 
Return 
'
¯HELP:
Restore HLP
H:
Read N
I=1
Repeat 
   Read T$
   Print _SPACER$;T$
   Inc I
Until I>N
Return 
'  
¯HELPSUBS:
Print : Print "  Subroutines: ";
M1=1
Repeat 
   M2=1
   Repeat 
      Restore 0+Val(Str$(M1)+Str$(M2)-_SPACE$)
      Read S$
      If Instr(S$,_»$)
         M3=1
         Repeat 
            Restore 0+Val(Str$(M1)+Str$(M2)+Str$(M3)-_SPACE$)
            Read S$
            If S$<>_NULL$
               Read MENFX,G$
               If Left$(G$,1)=_SCORE$
                  Print G$;
               End If 
            End If 
            Inc M3
         Until S$=_NULL$
         S$=_$
      Else If Mid$(S$,2,1)<>_MINUS$ and S$<>_NULL$
         Read MENFX,G$
         If Left$(G$,1)=_SCORE$
            Print G$;
         End If 
      End If 
      Inc M2
   Until S$=_NULL$ or(M1=3 and M2=11) : Rem menu collision   
   Inc M1
Until M1>_M1
Print "_RANGESET_RANGERESTORE_TGLDITHER_TGLLOOP_TGLMIXLOOP_PLAY_PLAYALL_PLAYRANGE_PLAYDISPLAY_REWIND_STOP_NOP"
Return 
'
¯HELP14:
Restore H14
Goto H
'
¯HELPDITHER:
Restore D24
Goto H
'
¯CANTFIND:
If SEQUENCER
   SETTING$(24)=_FALSE$ : Rem scroller 
   _TOAST$="Can'"+'t Find "'+T$+'"'
Else If MACRO
   F$=T$
   Goto CANTFIND
Else 
   Print "Can'";'t Find "';T$;'"'
End If 
Return 
'
¯BADARGS:
Print "Bad Args"
Return 
'
'
'
'***************************************************************************** 
'*memory********************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'    .        :  .,:::::: .        :      ...     :::::::..  -:.     ::-.: 
'    ;;,.    ;;; ;;;;'''' ;;,.    ;;;  .;;;;;;;.  ;;;;``;;;; ';;.   ;;;;'' 
'    [[[[, ,[[[[, [[cccc  [[[[, ,[[[[,,[[     \[[,\[[[,/[[['    '[[,[[[' 
'    $$$$$$$$"$$$ $$""""  $$$$$$$$"$$$$$$,     $$$"$$$$$$c        c$$" 
'   888 Y88" 888o 888oo,_888 Y88" 888o"888,_ _,88P,888b "88bo   ,8P"`
'   MM  M'  "MMMM """"YUMMM  M'  "MMMM  "YMMMMMP" "MMMM   "W"  mM" 
'
'
'
'  
FKEY:
N=C-79
If Fn KEY_SHIFT Then T$="S=" Else T$="B="
T$=T$+Str$(N)-_SPACE$+_COMMA$+_NEWLINE$
If PLYING and CON#>0 and SETTING$(26)<>_FALSE$
   _TOAST$=_$
   Goto ACTION
Else 
   MACRO=True
   Gosub CMD
   MACRO=False
End If 
Return 
'
BANKMENU:
B=_M2SEL
If PB=B Then Return 
Goto SETBANK
'
_PREVIOUS:
Dec B
If B<1 Then B=1
Goto SETBANK
'
_NEXT:
Inc B
If B>_FL Then B=_FL
Goto SETBANK
'  
SETBANK:
Gosub CLRRNG
Gosub BANKSET
If PB=B
   If Fn MULTI
      PBANK=PB+7
   End If 
   Return 
End If 
LB=PB
XR=0 : XR2=0 : LXR=0 : LXR2=0
RST=0 : RL=0 : RFIN=0
Gosub SETZ0NES
If Fn MULTIOFF
   PBANK=5
   Bank Swap PB+7,PBANK
   Bank Swap PBANK,B+7
Else 
   PBANK=B+7
End If 
PB=B
F$=FB$(B)
P_LENGTH=Length(PBANK)
If P_LENGTH>0
   P_START=Start(PBANK)
Else If Fn MULTIOFF or B<5
   If Fn MULTIOFF
      Gosub CLRPLY
   Else 
      Gosub CLREMPTY
   End If 
End If 
Gosub PBANKLEN
Gosub SETPBANK
If SETTING$(27)<>_FALSE$ or STPPED
   If RENDERING=False and _MENUDO=False
      RENDERING=True
      If Fn MULTI
         Gosub R4INSET
      Else 
         Gosub CLRPOS
         Gosub _DRAW
      End If 
      RENDERING=False
   End If 
End If 
Gosub HEADER
Gosub DRWTIME
Gosub MAGICWAND
Return 
'
_ERASEALL:
BTXT1$="Clear" : TXT1$="This will clear all" : TXT2$="samples and buffers?" : Gosub _REQUEST
If Not REQ Then Return 
Gosub _STOP
BANKNAME$=_$ : F$=_$ : PF$=_$ : _REPEAT$=_$
B=1
Repeat 
   Mid$(MENB$(B),7)=Space$(16)
   Inc B
Until B>10
B=1
Repeat 
   FB$(B)=_$ : FPATH$(B)=_$
   Inc B
Until B>_FL
Gosub CLRFILES
SEQ$=_$
Gosub _CLEARUNDO
B=5
Repeat 
   Erase B
   Inc B
Until B>_FL+7
B=1
XD=0 : XD2=SCR_W : XR=0 : XR2=0 : LXR=0 : LXR2=0
RST=0 : RL=0 : RFIN=0
Gosub SETBANK
Gosub MAGICWAND
Gosub SETPBANK
Gosub _DRAW
Gosub HEADER
Return 
'
CLRPLY:
Fill P_STARTB1 To P_STARTB1+(BSIZE*_CH),0
Fill P_STARTB2 To P_STARTB2+(BSIZE*_CH),0
Return 
'
CLREMPTY:
If B<5
   N=0
   Repeat 
      If Length(N+8)=0
         If PBSIZE>BSIZE and N>1
            Fill P_STARTB1+(BSIZE*(N-1)) To P_STARTB1+(BSIZE*N)+PBSIZE,0
            Fill P_STARTB2+(BSIZE*(N-1)) To P_STARTB2+(BSIZE*N)+PBSIZE,0
         End If 
      End If 
      Inc N
   Until N>3
End If 
Return 
'  
SETPBANK:
If Length(PBANK)=0
   FX_LENGTH=0
   P_LENGTH=0
   Return 
End If 
If REALTIME
   FX=P_SWP
   FX_START=FX
   FX_LENGTH=BSIZE
   FXE=FX+BSIZE-1
Else 
   FX_START=Start(PBANK)+RST
   If RL>Length(PBANK)-RST or RST+RL=0
      FX_LENGTH=Length(PBANK)-RST
   Else If RL<2
      FX_START=Start(PBANK)
      FX_LENGTH=Length(PBANK)
   Else 
      FX_LENGTH=RL
   End If 
   FX=FX_START
   FXE=FX_START+FX_LENGTH-1
End If 
P_START=Start(PBANK)
P_LENGTH=Length(PBANK)
If Fn STEREO
   P_LENGTH=P_LENGTH/2
Else If Fn QUAD
   P_LENGTH=P_LENGTH/4
End If 
Return 
'
PBANKLEN:
FI=Length(PBANK)
If Fn STEREO
   FI=FI/2
Else If Fn QUAD
   FI=FI/4
End If 
Return 
'
STEREOQUAD:
If Fn STEREO
   _RST=RST : _RFIN=RFIN
   RST=RST+P_LENGTH : RFIN=RFIN+P_LENGTH
   Gosub G$
   RST=_RST : RFIN=_RFIN
Else If Fn QUAD
   _RST=RST : _RFIN=RFIN
   RI=0
   Repeat 
      RST=RST+P_LENGTH : RFIN=RFIN+P_LENGTH
      Gosub G$
      Inc RI : Inc PASS
   Until RI>2
   RST=_RST : RFIN=_RFIN
End If 
Return 
'
STEREOQUADX2:
If Fn STEREO
   Inc _FXCUR
   Reserve As Work _FXCUR,Length(WBANK)*2
   Copy Start(WBANK),Start(WBANK)+Length(WBANK) To Start(_FXCUR)
   _RST=RST : _RFIN=RFIN
   RST=RST+P_LENGTH : RFIN=RFIN+P_LENGTH
   Gosub G$
   RST=_RST : RFIN=_RFIN
   Copy Start(WBANK),Start(WBANK)+Length(WBANK) To Start(_FXCUR)+Length(WBANK)
   Bank Swap _FXCUR,PBANK
   Erase WBANK
   Erase _FXCUR
   Dec _FXCUR
Else If Fn QUAD
   Inc _FXCUR
   Reserve As Work _FXCUR,Length(WBANK)*4
   Copy Start(WBANK),Start(WBANK)+Length(WBANK) To Start(_FXCUR)
   _RST=RST : _RFIN=RFIN
   RI=1
   Repeat 
      RST=RST+P_LENGTH : RFIN=RFIN+P_LENGTH
      Gosub G$
      Copy Start(WBANK),Start(WBANK)+Length(WBANK) To Start(_FXCUR)+(Length(WBANK)*RI)
      Erase WBANK
      Inc RI : Inc PASS
   Until RI>3
   RST=_RST : RFIN=_RFIN
   Bank Swap _FXCUR,PBANK
   Erase _FXCUR
   Dec _FXCUR
End If 
Return 
'
FXBUFFER:
FXBC=1
Repeat 
   Trap Reserve As Work _FXCUR,_FXBUF
   If Errtrap
      _FXCUR=-1
      Goto NOMEMORY
   End If 
   Inc _FXCUR
   Inc FXBC
Until FXBC>_CH
Return 
'
FXBUFFERC:
FXBC=_CH
Repeat 
   Dec _FXCUR
   Erase(_FXCUR)
   Dec FXBC
Until FXBC<0
Return 
'
_TOASTFREE:
_TOAST$="Buffer:"+Str$( Fn BUFFER/1024)+_KILO$+_SPACE$
Goto T0AST
'
'
'
'***************************************************************************** 
'*help************************************************************************ 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'     ::   .: .,::::::  :::    ::::::::. 
'    ,;;   ;;,;;;;''''  ;;;    `;;```.;;;
'   ,[[[,,,[[[ [[cccc   [[[    `]]nnn]]' 
'   "$$$"""$$$ $$""""   $$'     $$$""
'    888   "88 888oo,_o88oo,.__ 888o 
'    MMM    YM """"YUM"""YUMMMM YMMMb
'
'
'
SHORTCUTS:
_TEXT=True : SHORTCUTS=True
WTXT$=S$ : WX=94 : WY=20 : WX2=546 : WY2=182 : _COL=_WHITE : Gosub WIN
Ink _BLACK,_WHITE
Restore CUT
I=0
Repeat 
   Read T$
   Text WX+29,WY+28+(I*8),T$
   Inc I
Until I>14
Gosub WINWAITC
SHORTCUTS=False
Return 
'
OPTIONS:
_TEXT=True
RX=105 : RY=20
WTXT$=S$ : WX=98 : WY=RY : WX2=542 : WY2=146 : _COL=_PANE : Gosub WIN
Gosub OPTBTN
Repeat 
   Gosub GVAL
   E$=_$
   If M=1 and Z>58 and Z<80
      Restore OPB
      I=1
      Repeat 
         Read BTXT1$,BTXT2$,BX,BY,BZN,E$
         If Z=BZN
            PZN=Z : Add BX,RX : Add BY,RY : Gosub PRESS
            If _ON
               If I<_OB : Rem ok 
                  N=I+9
                  Gosub SETSET
               End If 
               Gosub E$
               Gosub OPTBTN
               Exit 
            Else 
               Exit 
            End If 
         End If 
         Inc I
      Until I>_OB
      If E$=_$
         Exit 
      End If 
   End If 
Until Fn KEY_RETURN or Fn KEY_ESC
Goto OPTIONSC
'
OPTIONSEXIT:
Pop 
OPTIONSC:
I=59
Repeat 
   Reset Zone I
   Inc I
Until I>79
Gosub MAGICWAND
Gosub WINC
Gosub NUDGEPOS
Return 
'  
OPTBTN:
Restore OPB
N=1
Repeat 
   Read BTXT1$,BTXT2$,BX,BY,BZN,E$
   If N<_OB : Rem ok 
      If SETTING$(N+9)<>_FALSE$
         If BTXT2$<>_$
            Swap BTXT1$,BTXT2$
         End If 
         _COL=_WHITE
      Else 
         _COL=_PANE
      End If 
   Else 
      _COL=_WHITE : Rem ok 
   End If 
   Add BX,RX : Add BY,RY : Gosub BTN
   Inc N
Until N>_OB
Return 
'
SETSET:
If SETTING$(N)<>_FALSE$
   SETTING$(N)=_FALSE$
Else 
   SETTING$(N)=_TRUE$
End If 
Return 
'  
SETFILTER:
If SETTING$(10)=_FALSE$ Then Led Off Else Led On 
Return 
'
SETMON:
If SETTING$(11)<>_FALSE$
   MONHZ#=50.0 : CLOCK=3546895 : Rem pal
Else 
   MONHZ#=60.0 : CLOCK=3579545 : Rem ntsc 
End If 
Reserve As Chip Work 3,MFREQ/MONHZ#*_CH
Reserve As Chip Work 4,MFREQ/MONHZ#*_CH
P_STARTB1=Start(3) : P_STARTB2=Start(4)
If _INIT
   SETTING$(29)=_FALSE$ : Rem -0.2hz 
   _AMIHZ=False
   Gosub SETHZ
   Gosub DRWTIMENOW
End If 
Return 
'
SETLIGHTMODE:
If SETTING$(12)=_FALSE$
   _PANEC=$222
   Palette $0,_PANEC,$777,$222,$111,$444,$999,$AAA,,,,,,,,,,$DDD,$999,$111
Else 
   _PANEC=$599
   Palette $0,_PANEC,$DDD,$566,$111,$377,$FFF,$E70,,,,,,,,,,$DDD,$E11,$111
End If 
If _INIT Then Gosub R4INRESTORE
Return 
'
SETDEBUG:
If SETTING$(13)=_FALSE$ Then _DEBUG=False Else _DEBUG=True
If _INIT Then Gosub _TOASTFREE
Return 
'  
SETNETSYNC:
If BSD=False and SETTING$(14)<>_FALSE$
   SETTING$(14)=_FALSE$
   NETWORK=False
   Gosub NOBSD
   If _INIT
      Goto OPTIONSEXIT
   End If 
Else If BSD and SETTING$(14)=_FALSE$
   NETWORK=False
   If _INIT
      Gosub _STOP
   End If 
   Gosub SOCK_CLOSESYNC
Else If BSD
   If _INIT
      Gosub _STOP
   End If 
   Gosub SOCK_OPENSYNC
   NETWORK=True
Else 
   NETWORK=False
End If 
Return 
'
SETHIRES:
SB=Screen Base
If SETTING$(15)=_FALSE$
   If _INIT
      Doke SB+72,Deek(SB+72) xor Hires
   End If 
   SCR_R=Lowres
Else 
   If _INIT
      Doke SB+72,Deek(SB+72) or Hires
      Screen Offset 0,0,0
   End If 
   SCR_R=Hires
End If 
Return 
'  
'POSITION
'
SETLEVELMETER:
If SETTING$(17)=_FALSE$ Then _LEVEL=False Else _LEVEL=True
Return 
'  
SETMETRONOME:
If SETTING$(18)=_FALSE$
   _METRO=False
   If _INIT
      Goto METROC
   End If 
Else 
   _METRO=True
End If 
Return 
'
SETPREVIEW:
If SETTING$(19)=_FALSE$ Then _PREVIEW=False Else _PREVIEW=True
Return 
'
'ZOOMDRAW  
'
SETGRAPH:
If SETTING$(21)<>_FALSE$ Then _STEP=2 Else _STEP=1
Return 
'
'WHEELZOOM 
'
SETAREXXSYNC:
If REXX=False and SETTING$(23)<>_FALSE$
   SETTING$(23)=_FALSE$
   TXT1$="Arexx server not" : TXT2$="found or not responding" : Gosub _ERROR
   If _INIT
      Goto OPTIONSEXIT
   End If 
Else If REXX and Arexx Exist(OCTAMED_REXX$)
   OCTAMED=True
Else If SETTING$(23)<>_FALSE$
   OCTAMED=False
   SETTING$(23)=_FALSE$
   TXT1$="Octamed arexx port" : TXT2$="not available" : Gosub _ERROR
   If _INIT
      Goto OPTIONSEXIT
   End If 
Else 
   OCTAMED=False
End If 
Return 
'
'SCROLLER  
'
SETVST:
If BSD=False and SETTING$(25)<>_FALSE$
   SETTING$(25)=_FALSE$
   _VST=False
   Gosub NOBSD
   If _INIT
      Goto OPTIONSEXIT
   End If 
Else If BSD and SETTING$(25)=_FALSE$
   _VST=False
   If _INIT
      Gosub _STOP
   End If 
   Gosub SOCK_CLOSEVST
Else If BSD
   If _INIT
      Gosub _STOP
   End If 
   Gosub SOCK_OPENVST
   _VST=True
Else 
   _VST=False
End If 
Return 
'
'QUANTIZER 
'
SETALWAYSDRAW:
If SETTING$(27)=_FALSE$ Then SETTING$(20)=_FALSE$ : Rem zoom draw
Return 
'
'MAGIC 
'
SETPOINT2:
If SETTING$(29)=_FALSE$
   If _INIT
      MONHZ#=0.2+MONHZ#
      _TOAST$=Str$(MONHZ#)+_HZ$
   End If 
Else 
   MONHZ#=-0.2+MONHZ#
   _TOAST$=Str$(MONHZ#)+_HZ$
End If 
If _INIT
   _AMIHZ=False
   Gosub SETHZ
   Gosub DRWTIMENOW
End If 
Return 
'
ABOUT:
_TEXT=True : ABOUT=True
WTXT$="FxBox" : WX=149 : WY=28 : WX2=490 : WY2=127 : _COL=_WHITE : Gosub WIN
Set Text 4 : Text WX+4+48,WY+10,"powered by" : Set Text 0 : Paste Bob WX+141,WY,2
T$="Chip:"+Str$((Chip Free/1024))-_SPACE$+_KILO$
Ink _WHITE,_SHADOW
Text WX2-Text Length(T$)-3,WY+10,T$
Gosub HEADER
Ink _BLACK,_WHITE
Text 168,56,Mid$("$VER: Version 1.18 © 1991-2026 Marc Williams",7)
If REXX
   T$=FXBOX_REXX$
Else 
   T$="not available"
End If 
Text 224,72,"Arexx Port: "+T$
If OCTAMED
   If SETTING$(23)<>_FALSE$
      T$=OCTAMED_REXX$
   Else 
      T$="not active"
   End If 
Else 
   T$="not available"
End If 
Text 208,80,"Octamed Sync: "+T$
If NETWORK and BSD
   T$=_ADDRSYNC$+_COLON$+Str$(_PORTSYNC)-_SPACE$
Else If BSD
   T$="not active"
Else 
   T$="not available"
End If 
Text 240,88,"Net Sync: "+T$
If _VST
   T$=_ADDRVST$+_COLON$+Str$(_PORTVST)-_SPACE$
Else If BSD
   T$="not active"
Else 
   T$="not available"
End If 
Text 248,96,"VST Out: "+T$
BX=270 : BY=WY2-BUT_H-4
Paste Bob BX+42,BY+2,4
BX=0 : BXE=Int(Timer+MONHZ#)
Repeat 
   Inc BX
Until Timer>BXE
Text 232,64,"Benchmark:"+Str$(BX/1E+06)+" DSP MIPS"
Gosub WINWAITC
ABOUT=False
Return 
'
'    
'
'*****************************************************************************   
'*macros********************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'    .        :    :::.       .,-:::::::::::::..      ...      .::::::.
'    ;;,.    ;;;   ;;`;;    ,;;;'````' ;;;;``;;;;  .;;;;;;;.  ;;;`    `; 
'    [[[[, ,[[[[, ,[[ '[[,  [[[        \[[[,/[[[' ,[[     \[[,'[==/[[[[, 
'    $$$$$$$$"$$$c$$$cc$$$c $$$        "$$$$$$c   $$$,     $$$  '''    $ 
'   888 Y88" 888o 888   888,`88bo,__,o,,888b "88bo"888,_ _,88P,88b    dP 
'   MM  M'  "MMMM YMM   ""`   "YUMMMMMP"MMMM   "W"  "YMMMMMP"   "YMmMY"
'
'
'
CHECKFX:
M1=1
Repeat 
   M2=1
   Repeat 
      X$=Str$(M1)-_SPACE$+Str$(M2)-_SPACE$
      Restore 0+Val(X$)
      Read X$
      If Instr(X$,_»$)
         M3=1
         Repeat 
            X$=Str$(M1)+Str$(M2)+Str$(M3)-_SPACE$
            Restore 0+Val(X$)
            Read X$
            If X$<>_NULL$
               Read MENFX,G$
               If G$=T$
                  Return 
               End If 
            End If 
            Inc M3
         Until X$=_NULL$
         X$=_$
      Else If Mid$(X$,2,1)<>_MINUS$ and X$<>_NULL$
         Read MENFX,G$
         If G$=T$
            Return 
         End If 
      End If 
      Inc M2
   Until X$=_NULL$
   Inc M1
Until M1>_M1
MENFX=False
Return 
'
MACROSET:
If MACRO Then Return 
Screen Hide 2 : Screen 0
Return 
'
MACRORESTORE:
If MACRO Then Return 
Screen Show 2 : Screen 2
Return 
'
MACRO:
S$=FMACRO$(_M2SEL-1+MOFF)
MACROP:
PASS=0
_TOAST$=S$+_SPACE$ : Gosub T0AST
D$=MACRODIR$+S$+".asc"
If Not Exist(D$)
   T$=D$ : Goto MACROERROR
End If 
Gosub _UNDOSET
MACRO=True : _TEXT=True
_MODEP=_MODE
Trap Open In 1,D$
If Errtrap
   F$=D$ : Goto FILEERROR
End If 
If Not _REPEATING
   If Instr(_REPEAT$,S$)=False and MI=-1
      _REPEAT$=_REPEAT$+_SPACE$+S$
   End If 
End If 
Inc MI
MN(MI)=Lof(1)
If MN(MI)> Fn BUFFER Then Goto NOSPACE
MACROT$(MI)=Space$(MN(MI))
Sload 1 To Varptr(MACROT$(MI)),MN(MI) : Close 1 : MP(MI)=1
_TIMEDIVP#=_TIMEDIV#
Repeat 
   MA(MI)=Instr(MACROT$(MI),_NEWLINE$,MP(MI))
   If MA(MI)
      T$=Mid$(MACROT$(MI),MP(MI),MA(MI)-MP(MI))
      _TOAST$=T$+_SPACE$ : Gosub T0AST
      T$=Upper$(T$)
      Gosub CMD
   End If 
   MP(MI)=MA(MI)+1
   Gosub GVAL
Until Fn KEY_ESC or MA(MI)=False or MP(MI)>=MN(MI)
T$=Str$(_TIMEDIVP#) : Gosub TIMEDIV
_MODE=_MODEP
Dec MI
If MI=-1
   If Not _REPEATING
      MACRO=False
   End If 
   If _CHOICE=3
      RL=0 : XR=0 : XR2=0
      _CHOICE=False
      Gosub POSCAL
      Gosub HEADERCORDS
   End If 
   _TEXT=False
End If 
Return 
'
_MACROMENU:
MOFF=0
If Exist(MACRODIR$)
   Dir$=MACRODIR$
   MACRODIR$=Dir$
   D$=Dir First$(_$)
   TCOUNT=1
   If D$<>_$
      Repeat 
         I=Len(D$)-9
         Repeat 
            Exit If Mid$(D$,I,1)<>_SPACE$
            Dec I
         Until I<0
         D$=Upper$(Mid$(D$,2,1))+Mid$(D$,3,I-6)
         FMACRO$(TCOUNT)=D$
         Inc TCOUNT
         D$=Dir Next$
      Until D$=_$ or TCOUNT>_MR
      _MR=TCOUNT
   End If 
   TCOUNT=0
   Dir$=PROGDIR$
End If 
MACROMENU:
I=0
Repeat 
   MENM$(I)=Space$(17)
   Mid$(MENM$(I),1)=FMACRO$(I+MOFF)
   Inc I
Until I>18
Return 
'
MACRODOWN:
Inc MOFF
If MOFF>_MR-19
   MOFF=_MR-19
   Return 
End If 
Goto MACROMENU
'
MACROUP:
Dec MOFF
If MOFF<0
   MOFF=0
   Return 
End If 
Goto MACROMENU
'  
'
'
'***************************************************************************** 
'*sys************************************************************************* 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'    .::::::. -:.     ::-.: .::::::. 
'   ;;;`    `;';;.   ;;;;'';;;`    `;
'   '[==/[[[[,   '[[,[[['  '[==/[[[[,
'     '''    $     c$$"      '''    $
'   ,88b    dP   ,8P"`     ,88b    dP
'     "YMmMY"   mM"          "YMmMY" 
'
'
'
SIGN3:
If CN3>127 Then CN3=CN3-256
SIGN2:
If CN2>127 Then CN2=CN2-256
SIGN:
If CN>127 Then CN=CN-256
Return 
'
UNSIGN2:
If CN2<0 Then CN2=CN2+256
UNSIGN:
Gosub CHECKCLIP
If CN<0 Then CN=CN+256
Return 
'
FLOAT2:
If CN2>127 Then CN2=CN2-256
CN2#=CN2/127.0
FLOAT:
If CN>127 Then CN=CN-256
CN#=CN/127.0
Return 
'
UNFLOAT:
CN=Int(CN#*127.0)
Gosub CHECKCLIP
If CN<0 Then CN=CN+256
Return 
'
CHECKCLIP:
If CN>127
   CN=127
Else If CN<-127
   CN=-127
End If 
Return 
'  
UNCHECKCLIP:
If CN>255
   CN=255
Else If CN<0
   CN=0
End If 
Return 
'
SETHZ:
PBSIZE=BSIZE
If _AMIHZ
   PERIOD=CLOCK/FREQ
   FREQ=CLOCK/PERIOD
   BSIZE=Int(FREQ/MONHZ#)
   If FREQ>MFREQ
      FREQ=MFREQ
      BSIZE=Int(FREQ/MONHZ#)
      FREQ=BSIZE*MONHZ#
      PERIOD=CLOCK/FREQ
   End If 
Else 
   BSIZE=Int(FREQ/MONHZ#)
   FREQ=BSIZE*MONHZ#
   PERIOD=CLOCK/FREQ
End If 
Doke $DFF0A6,PERIOD
Doke $DFF0B6,PERIOD
Doke $DFF0C6,PERIOD
Doke $DFF0D6,PERIOD
Gosub DRWTIME
ROT#(0)=(((FREQ-601)/13.3)/(256+184))+8.5
If Fn MULTI Then Gosub CLREMPTY
_AMIHZ=True
Return 
'
AMIHZDIAL:
DIA_X=134
DIA_Y=WVE_H+33
Ellipse DIA_X,DIA_Y,15,9
Return 
'
FLSHSCR:
Colour _PANE,Colour(_HIGHLIGHT)
If Not BGPLYING
   Gosub TASK : Gosub TASK
   Goto FLSHSCRC
Else 
   _TOASTWAIT=1
End If 
Return 
'
FLSHSCRC:
Colour _PANE,_PANEC
Return 
'
GFXSET:
GFXX=Xgr : GFXY=Ygr
Return 
'
GFXRESTORE:
Gr Locate GFXX,GFXY
Return 
'  
PATSET:
If SCR_D=4 Then Set Pattern 2
Return 
'  
PATRESTORE:
If SCR_D=4 Then Set Pattern 0
Return 
'
TASK:
If Not BGPLYING Then Multi Wait 
Return 
'
SPIN:
_SPIN=Timer+4 : Repeat : Until Timer>_SPIN
Return 
'
TIME_ALL:
If T_LENGTH>0
   DIV#=FREQ
   DIV#=((DIV#/T_LENGTH)*60)*4
   If DIV#<32
      DIV#=FREQ
      DIV#=T_LENGTH/DIV#
      T$=" Secs"
      If DIV#>60
         ADV#=DIV#
         DIV#=DIV#/60
         DIV#=Int(DIV#)
         T$=" Mins"+Str$(Int(ADV#-(DIV#*60)))+T$
      End If 
      If DIV#>60
         ADV#=DIV#
         DIV#=DIV#/60
         DIV#=Int(DIV#)
         T$=" Hrs"+Str$(Int(ADV#-(DIV#*60)))+T$
      End If 
   Else If DIV#>320
      DIV#=FREQ
      DIV#=(T_LENGTH/DIV#)*1000
      T$=" Ms"
   Else 
      T$=_BPM$
   End If 
Else 
   DIV#=0
End If 
Return 
'
TIME_PLY:
If PTGL=TGL Then Return 
PTGL=TGL
If PLYING
   DIV#=(Timer-PLYTIME)/MONHZ#
Else 
   DIV#=(STPTIME-PLYTIME)/MONHZ#
End If 
ADV#=DIV#
DIV#=Int(DIV#/60)
ADV#=ADV#-(DIV#*60)
TIME$=Str$(ADV#)-_SPACE$
If ADV#<10
   TIME$=_FALSE$+TIME$
End If 
ADV#=DIV#
DIV#=Int(DIV#/60)
ADV#=ADV#-(DIV#*60)
TIME$=Str$(ADV#)-_SPACE$+":"+TIME$
If ADV#<10
   TIME$=_FALSE$+TIME$
End If 
TIME$=Str$(DIV#)-_SPACE$+":"+TIME$
If DIV#<10
   TIME$=_FALSE$+TIME$
End If 
TIME$=Left$(TIME$,11)
Ink _BLACK,_WHITE
Text 232,104,"Play Time: "+TIME$
Return 
'
_TGLDITHER:
Inc _DITHER
_TOAST$="Dither "
If _DITHER=2
   _TOAST$="HP-Tri "+_TOAST$
Else If _DITHER=3
   _TOAST$="LP "+_TOAST$
Else If _DITHER>3
   _DITHER=0
   _TOAST$="No "+_TOAST$
End If 
Return 
'
'
'
'***************************************************************************** 
'*mouse*********************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'    .        :      ...      ...    ::: .::::::. .,:::::: 
'    ;;,.    ;;;  .;;;;;;;.  ';;     ;;;;;;`    `;;;;;'''' 
'    [[[[, ,[[[[,,[[     \[[,[['     [[['[==/[[[[, [[cccc  
'    $$$$$$$$"$$$$$$,     $$$$$      $$$  '''    $ $$""""  
'   888 Y88" 888o"888,_ _,88P88    .d888,88b    dP 888oo,_ 
'   MM  M'  "MMMM  "YMMMMMP"  "YmmMMMM""  "YMmMY"  """"YUM 
'
'
'
SETZ0NES:
If RL=0
   Set Zone 73,0,HDR_H To SCR_W,WVE_H+HDR_H
   Reset Zone 74 : Reset Zone 75
   Reset Zone 76 : Reset Zone 77
Else If XR>1 and XR2>0
   Set Zone 73,0,HDR_H To XR-1,WVE_H+HDR_H
   Set Zone 74,XR,RNG_T+HDR_H To XR+12,RNG_B+HDR_H
   If XR+24<XR2
      Set Zone 75,XR+12,HDR_H To XR2-12,WVE_H+HDR_H
   Else 
      Reset Zone 75
   End If 
   If XR2>12 and XR2<SCR_W
      Set Zone 76,XR2-12,RNG_T+HDR_H To XR2+1,RNG_B+HDR_H
   Else 
      If RFIN-FIN=0
         If XR<614
            Set Zone 75,XR+12,HDR_H To 626,WVE_H+HDR_H
         Else 
            Reset Zone 75
         End If 
         Set Zone 76,627,RNG_T+HDR_H To SCR_W,RNG_B+HDR_H
      Else 
         Reset Zone 76
      End If 
   End If 
   Set Zone 77,0,HDR_H To SCR_W,WVE_H+HDR_H
Else If XR=0 and XR2>0 and XR2<25
   Reset Zone 73
   Set Zone 74,0,RNG_T+HDR_H To 6,RNG_B+HDR_H
   Reset Zone 75
   Set Zone 76,6,RNG_T+HDR_H To 12,RNG_B+HDR_H
   Set Zone 77,0,HDR_H To SCR_W,WVE_H+HDR_H
Else If XR=0 and XR2>24 and XR2<SCR_W-2
   Reset Zone 73
   If RST-ST=0
      Set Zone 74,0,RNG_T+HDR_H To 12,RNG_B+HDR_H
      Set Zone 75,12,HDR_H To XR2-12,WVE_H+HDR_H
   Else 
      Reset Zone 74
      Set Zone 75,0,HDR_H To XR2-12,WVE_H+HDR_H
   End If 
   Set Zone 76,XR2-12,RNG_T+HDR_H To XR2+1,RNG_B+HDR_H
   Set Zone 77,0,HDR_H To SCR_W,WVE_H+HDR_H
Else If XR2>0
   Reset Zone 73
   Set Zone 74,0,RNG_T+HDR_H To 12,RNG_B+HDR_H
   Set Zone 75,13,HDR_H To 628,WVE_H+HDR_H
   If((RFIN/CON#)-(FIN/CON#))<1
      Set Zone 76,628,RNG_T+HDR_H To SCR_W,RNG_B+HDR_H
   Else 
      Reset Zone 76
      If RST-ST=0
         Set Zone 75,13,HDR_H To SCR_W,WVE_H+HDR_H
      Else 
         Set Zone 75,0,HDR_H To SCR_W,WVE_H+HDR_H
      End If 
   End If 
   Set Zone 77,0,HDR_H To SCR_W,WVE_H+HDR_H
End If 
Return 
'
TESTMOVE:
Gosub MPOS
PX=CX : PY=CY
Gosub MKEY : MK=M
Repeat 
   Gosub GVAL
Until C>0 or MK-M or CY-PY or CX-PX
Return 
'
MUP:
Repeat 
   Gosub TASK
   Gosub MKEY
Until M=0
Return 
'
MKEY:
M=Mouse Key
If M=4
   M=0
Else If M=5 or Fn KEY_LEFTAMIGAALT
   M=1
Else If Fn KEY_RIGHTAMIGAALT
   M=2
End If 
Return 
'
MPOS:
If S>0
   If C>75 and C<80 : Rem emulate standard amiga keyboard mouse
      If( Fn KEY_LEFTAMIGA or Fn KEY_RIGHTAMIGA or Fn KEY_LEFTAMIGAALT or Fn KEY_RIGHTAMIGAALT)
         If Fn KEY_UPCUR
            Y Mouse=Y Mouse-1
         Else If Fn KEY_DOWNCUR
            Y Mouse=Y Mouse+1
         Else If Fn KEY_RIGHTCUR
            X Mouse=X Mouse+1
         Else If Fn KEY_LEFTCUR
            X Mouse=X Mouse-1
         End If 
      End If 
   End If 
End If 
CX=X Screen(X Mouse)
CY=Y Screen(Y Mouse)
Return 
'
SLOW:
If CX<PX
   X Mouse=X Mouse+1
Else 
   X Mouse=X Mouse-1
End If 
Return 
'
STICKY:
If CY<PY
   Y Mouse=Y Mouse+(PY-CY)
Else 
   Y Mouse=Y Mouse-(CY-PY)
End If 
If CX<PX
   X Mouse=X Mouse+((PX-CX)/2)
Else 
   X Mouse=X Mouse-((CX-PX)/2)
End If 
Return 
'
KEY:
K$=Inkey$
C=Scancode
Return 
'
GVAL:
Gosub TASK
Gosub MKEY
Z=Mouse Zone
S=Key Shift
Gosub KEY
'If C=95 : Rem page down 
'   TXT1$="Screen saved to" : TXT2$="RAM:screen_"+Str$(_GRAB)-_SPACE$+".iff" : 
'      Save Iff TXT2$
'      Inc _GRAB 
'   If _DIRECT 
'      Print TXT1$;_SPACE$;TXT2$ 
'   Else 
'      Gosub _NOTICE 
'   End If 
'End If  
Gosub MPOS
If SCR_R=Lowres Then Screen Offset 0,X Mouse-128,
If _DIRECT Then Gosub CLIMVE
If _TOAST$<>_$ Then Gosub T0AST
Return 
'
'
'
'***************************************************************************** 
'*choice********************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'     .,-::::::  ::   .:     ...      :::    .,-::::::.,:::::: 
'   ,;;;'````'  ,;;   ;;, .;;;;;;;.   ;;;  ,;;;'````' ;;;;'''' 
'   [[[        ,[[[,,,[[[,[[     \[[, [[[  [[[         [[cccc  
'   $$$        "$$$"""$$$$$$,     $$$ $$$, $$$         $$""""  
'   `88bo,__,o, 888   "88"888,_ _,88Po88888`88bo,__,o, 888oo,_ 
'     "YUMMMMMP MMM    YM  "YMMMMMP" MMM"MM  "YUMMMMMP """"YUM 
'
'
'
MCHOOSE:
_CHOICE=False
If G$=_$ Then Return 
PASS=0
If RL=0 and MENFX
   If _M1SEL=3 and _M2SEL=2 : Rem resample 
   Else If M1SEL=3 and _M2SEL=7 : Rem convert
   Else 
      RL=P_LENGTH
      _CHOICE=3
   End If 
End If 
_REPEATP$=_REPEAT$
If G$="_UNDO" Then _REPEAT$=_REPEATP$ Else _REPEAT$=G$
If _REPEAT$=_MACRO$ Then _REDRAW=True Else _REDRAW=False
Gosub G$
If _REDRAW
   MENFX=True : REQ=True
Else If MENFX and RL>1
   Inc PASS
   Gosub STEREOQUAD
End If 
If MENFX
   If _CHOICE=3
      RL=0 : XR=0 : XR2=0
      _CHOICE=False
      Gosub POSCAL
      Gosub HEADERCORDS
   End If 
   If REQ or FX=>FXE
      RENDERING=True
      Gosub _DRAW
      Gosub FRESHSEL
      Gosub CLRPOS
      RENDERING=False
   End If 
End If 
If Right$(_REPEAT$,4)<>"MENU" and REQ
   _TOAST$=_REPEAT$+_SPACE$
End If 
Return 
'
BCHOOSE:
PASS=0
If Z=58
   Goto _TGLOSCILLOSCOPE
Else If Z=59
   Goto _TGLOSCILLOMODE
End If 
If Z<_AB+2
   PZN=Z : Gosub PRESSBTN
   If Not _ON
      Return 
   End If 
End If 
If Z=1 and PLYING and CX>56
   _TOAST$="_STOP"
   Goto _STOP
Else If Z=15
   N=24 : Gosub SETSET : Gosub MUP
Else If P_LENGTH=0 and Fn MULTIOFF and Z<>12 : Rem paste    
   Goto FLSHSCR
End If 
On Z-69 Goto LEFTWAND,RIGHTWAND,MOVEWAND,RANGE,SELLEFT,RANGE,SELRIGHT,RANGE
If Z<_AB+2
   G$=BUTG$(Z-1)
   If Z=4 and RL=0 : Rem mix stereo quad
      RL=P_LENGTH
   End If 
   Gosub G$
   If Z=4 : Rem mix
      If RL>1
         Inc PASS
         Gosub STEREOQUAD
      End If 
   End If 
   If Z=4 or Z=5 : Rem mix and repeat 
      Gosub _DRAWALL
      Gosub CLRPOS
   End If 
   _TOAST$=G$
End If 
Return 
'
KCHOOSE:
PASS=0
If( Fn KEY_LEFTAMIGA or Fn KEY_RIGHTAMIGA) and RENDERING=False
   If C<5
      _M3SEL=C : Goto M0DEMENU
   Else If C=16
      Goto _QUIT
   Else If C=19
      Goto _REVERT
   Else If C=24
      Goto _OPEN
   Else If C=32
      Goto _APPEND
   Else If C=33
      Goto _SAVEAS
   Else If Fn KEY_D
      Goto NEWCLI
   Else If C=49
      Goto _UNDO
   Else If C=50
      Goto _CUT
   Else If C=51
      Goto _COPY
   Else If C=52
      Goto _PASTE
   End If 
Else If C=11
   Goto _PREVIOUS
Else If C=12
   Goto _NEXT
Else If C=74
   Add KMUL,-10
   If KMUL<0
      KMUL=0
   End If 
   B=KMUL
   If B<1
      B=1
   End If 
   C=B+79
   Goto FKEY
Else If C=94
   Add KMUL,10
   B=KMUL
   C=B+79
   Goto FKEY
Else If Fn KEY_SPACE
   Goto _STOP
Else If Fn KEY_BACKSPACE
   If PLYING
      If Fn MULTI
         T$="_REWINDALL"
      Else 
         T$="_REWIND"
      End If 
      If SETTING$(26)=_FALSE$
         Goto T$
      Else 
         T$=T$+_COMMA$+_NEWLINE$
         Goto ACTION
      End If 
   End If 
Else If Fn KEY_RETURN
   Goto _TGLLOOP
Else If Fn KEY_ESC
   Goto _QUIT
Else If C>28 and C<32
   C=C+51+KMUL
   Goto FKEY
Else If C>44 and C<48
   C=C+38+KMUL
   Goto FKEY
Else If C>60 and C<64
   C=C+25+KMUL
   Goto FKEY
Else If C>79 and C<90
   Goto FKEY
Else If C=122 or C=123
   Goto WHEELZOOM
Else If S-%1000000
   If P_LENGTH=0
      Goto FLSHSCR
   End If 
   Gosub PIANO
   If KEYC<>-1
      _TOAST$=KEYS$
      FREQ=KEYF
      Gosub SETHZ
      Gosub DIALMAIN
      If RENDERING
         Gosub DRWHZ
         Gosub DRWTIMENOW
      End If 
      If RL>0 and RNGEPLY
         Goto _PLAYRANGE
      Else If DISPPLY
         Goto _PLAYDISPLAY
      Else 
         Goto _PLAYALL
      End If 
   End If 
End If 
Return 
'
ACTION:
N=Instr(SEQ$,_NEWLINE$)
If N=0
   SEQ$=T$
Else If N=Len(SEQ$)
   SEQ$=SEQ$+T$
Else 
   SEQ$=Left$(SEQ$,N-1)+T$+Mid$(SEQ$,N)
End If 
Return 
'
PIANO:
If S=4 Then Restore KLO Else Restore KHI
Repeat 
   Read KEYC,KEYS$,KEYF
   If KEYC=C
      Return 
   End If 
Until KEYC=-1
Return 
'
ZCHOOSE:
If PZ=74
   Gosub UNR0LLLEFT
   PZ=0
Else If PZ=76
   Gosub UNR0LLRIGHT
   PZ=0
Else If Z=74
   Gosub R0LLLEFT
   PZ=Z
Else If Z=76
   Gosub R0LLRIGHT
   PZ=Z
End If 
Return 
'
'
'
'***************************************************************************** 
'*menu************************************************************************ 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'    .        :  .,::::::::.    :::. ...    :::  
'    ;;,.    ;;; ;;;;'''';;;;,  `;;;';;     ;;;  
'    [[[[, ,[[[[, [[cccc  [[[[[. '[[[['     [[[  
'    $$$$$$$$"$$$ $$""""  $$$ "Y$c$$$$      $$$  
'   888 Y88" 888o 888oo,_888    Y88 88    .d888    
'   MM  M'  "MMMM """"YUMMM     YM   "YmmMMMM""    
'
'
'  
INITMENU:
_BAR$=String$(_MINUS$,14)
BANKMENU$="BANKMENU"
M0DEMENU$="M0DEMENU"
RATEMENU$="RATEMENU"
_MACRO$="MACRO"
_RECORD$="Record Output "
If Not Fn A1200 Then _OSCILLOSCOPE$="Oscilloscope  " Else _OSCILLOSCOPE$=_NULL$
M2=1 : I=9
Repeat 
   MENB$(M2)="F"+Str$(M2)-_SPACE$+" -"+Space$(I)
   Inc M2
   If M2=10 Then Dec I
Until M2>10
Return 
'
DRWMENU:
If Not _MENUDO
   _MENUDO=True
   _M1SEL=0 : _M2SEL=0 : _M3SEL=0
   Gosub NUDGEPOS
   Gosub CLRPOS
End If 
If _MENUSUB
   Restore 0+Val(Str$(M1)-_SPACE$+Str$(M2-2)-_SPACE$)
   Gosub RMENU
   If _M1SEL=0 or CY<HDR_H+1 or CX<MENX(_M1SEL-1) or(CX>MENX(_M1SEL-1)-8 and CX<MENX(_M1SEL-1)+Text Length(S$)-8 and((CY-5)/8)-_M2SEL)
      _MENUSUB=False
      _SPIN=0
      If CY<HDR_H+1
         _MENUDRW=True
      Else 
         MENH=0
         _MENUDRW=False
      End If 
      _MENUBOX=True
      Gosub CLRMENU
      Goto MDO
   End If 
   If _MENUSUB
      _M3SEL=(CY-8)/8-_M2SEL+1
      If _M3SEL>_M3 or CX<SUBX or CX>SUBX+SUBW
         _M3SEL=0
      End If 
      Restore 0+Val(Str$(M1)-_SPACE$+Str$(_M2SEL)-_SPACE$)
      Gosub RMENU
      _L2=(MENX(M1-1)/8+Text Length(S$)/8)-4
      SUBX=(_L2*8)
      SUBY=(_M2SEL*8)-3
      M3=1
      SZ=1
      Repeat 
         Restore 0+Val(Str$(_M1SEL)-_SPACE$+Str$(_M2SEL)-_SPACE$+Str$(M3)-_SPACE$)
         Gosub RMENU
         If _M3SEL-M3
            If SETTING$(12)=_FALSE$
               Ink _BLACK,_WHITE
            Else 
               Ink _BLACK,_SHADOW
            End If 
         Else 
            If SETTING$(12)=_FALSE$
               Ink _WHITE,_BLACK
            Else 
               Ink _BLACK,_PANE
            End If 
         End If 
         If Len(S$)>1
            Text _L2*8+2,(_M2SEL+M3-1)*8+14,S$
            SUBW=Text Length(S$)+4
            Inc SZ
         End If 
         Inc M3
      Until S$=_NULL$
   End If 
   If SUBH=0
      SUBH=SZ*8
   End If 
   If _M3SEL<0 or _M3SEL>_M3
      _M3SEL=0
   End If 
   If SETTING$(12)=_FALSE$ : Rem dark mode
      Ink _WHITE
   Else 
      Ink _SHADOW
   End If 
   Box SUBX+1,SUBY+10 To SUBX+SUBW-2,SUBY+SUBH+3
   Ink _BLACK : Box SUBX,SUBY+9 To SUBX+SUBW-1,SUBY+SUBH+4
   Gosub TESTMOVE
   Return 
End If 
MDO:
If _MENUON and _MENUDO
   If CY<HDR_H
      _M1SEL=0
      _PM2SEL=-1
      M1=_M1
      Repeat 
         If CX>=MENX(M1-1) and CX<=MENX(M1)
            _M1SEL=M1
            Exit 
         End If 
         Dec M1
      Until M1<1
   End If 
   If M1>_M1 or M1=0
      M1=0
      MENH=0
      Return 
   End If 
   If _PM1SEL-_M1SEL
      Gosub CLRMENU
      _SPIN=0 : SUBH=0 : MENH=0
   End If 
   _PM1SEL=_M1SEL
   _PM2SEL=_M2SEL
   _M2SEL=(CY-5)/8
   Restore 0+M1
   Read S$
   If CY<HDR_H+2 or _M2SEL>M2-2 or CX<MENX(M1-1) or CX>(MENX(M1-1)+(Len(_BAR$)*8))
      _M2SEL=0
      _MENUDRW=False
   End If 
   _M3SEL=0
   _L1=(MENX(M1)/8)-Len(S$)
   MENX=(_L1*8)+2
   MENY=2
   M2=1
   SZ=1
   Repeat 
      If M1>0 and M2>0
         If M1=3 and M2=11 : Rem menu collision 
            S$=" Reverse        "
         Else If M1=3 and M2=12
            S$=" Invert         "
         Else If M1=3 and M2=13
            S$=_NULL$
         Else 
            Restore 0+Val(Str$(M1)-_SPACE$+Str$(M2)-_SPACE$)
            Gosub RMENU
         End If 
         If _M2SEL=M2 and Mid$(S$,2,1)<>_MINUS$
            If _PM2SEL-_M2SEL
               If SETTING$(12)=_FALSE$
                  Ink _WHITE,_BLACK
               Else 
                  Ink _BLACK,_PANE
               End If 
               Text MENX+2,(M2*8)+MENY+9,S$
            End If 
            If Instr(S$,_»$)
               _MENUSUB=True
            End If 
            If Instr(S$,"­") : Rem the other minus
               _MENUACTION=True
               Read MENFX,G$
               If _SPIN>0
                  Gosub G$
                  Repeat 
                     Gosub TASK
                     Gosub MKEY
                  Until Timer>_SPIN or M=0
               End If 
               Gosub SPIN
               _SPIN=Timer+8
               _MENUDRW=True
            Else 
               _MENUDRW=False
               _MENUACTION=False
               _SPIN=0
            End If 
         Else If _M2SEL=0
            If _PM2SEL=M2
               If SETTING$(12)=_FALSE$
                  Ink _BLACK,_WHITE
               Else 
                  Ink _BLACK,_SHADOW
               End If 
               Text MENX+2,(M2*8)+MENY+9,S$
               If SUBH>0
                  SUBH=0 : _MENUDRW=True
               End If 
               Gosub DRWMENUGFX
            End If 
            _SPIN=0
         Else If _PM2SEL=M2
            If SETTING$(12)=_FALSE$
               Ink _BLACK,_WHITE
            Else 
               Ink _BLACK,_SHADOW
            End If 
            Text MENX+2,(M2*8)+MENY+9,S$
            Gosub DRWMENUGFX
         End If 
         If MENH=0 or _MENUDRW
            If _M2SEL-M2 or Mid$(S$,2,1)=_MINUS$
               If SETTING$(12)=_FALSE$
                  Ink _BLACK,_WHITE
               Else 
                  Ink _BLACK,_SHADOW
               End If 
            Else 
               If SETTING$(12)=_FALSE$
                  Ink _WHITE,_BLACK
               Else 
                  Ink _BLACK,_PANE
               End If 
            End If 
            If Len(S$)>1
               Text MENX+2,(M2*8)+MENY+9,S$
               MENW=Text Length(S$)+4
               Inc SZ
            End If 
            Gosub DRWMENUGFX
         End If 
      End If 
      Inc M2
   Until S$=_NULL$
   If MENH=0
      _MENUBOX=True
   Else 
      _MENUBOX=False
   End If 
   If MENH=0
      MENH=SZ*8
   End If 
   If _MENUBOX
      If SETTING$(12)=_FALSE$
         Ink _WHITE
      Else 
         Ink _SHADOW
      End If 
      Box MENX+1,MENY+10 To MENX+MENW-2,MENY+MENH+3
      Draw MENX+1,MENY+9 To MENX+MENW-2,MENY+9
      Ink _BLACK
      Draw MENX+MENW-1,MENY+9 To MENX+MENW-1,MENY+MENH+4
      Draw MENX+1,MENY+MENH+4 To MENX+MENW-2,MENY+MENH+4
      Draw MENX,MENY+9 To MENX,MENY+MENH+4
   End If 
   If _MENUSUB=False and _MENUACTION=False
      Gosub TESTMOVE
   End If 
End If 
If Not _MENUON
   If OSCILLO
      RENDERING=True
      Gosub SH0WWAVE
      Gosub FRESHSEL
      RENDERING=False
   End If 
   M1=1
   If SETTING$(12)=_FALSE$
      Ink _WHITE
   Else 
      Ink _SHADOW
   End If 
   Bar 0,0 To SCR_W,10
   If SETTING$(12)=_FALSE$
      Ink _BLACK,_WHITE
   Else 
      Ink _BLACK,_SHADOW
   End If 
   Repeat 
      Restore 0+M1
      Read S$
      If Len(S$)>1
         MENX(M1)=Text Length(S$)+MENX(M1-1)
         Text MENX(M1-1)+7,8,S$
      End If 
      Inc M1
   Until S$=_NULL$
   _MENUON=True
   _PM2SEL=-1
End If 
Return 
'
DRWMENUGFX:
If Instr(S$,"­") : Rem the other minus
   If M2>1
      _BOB=6
   Else 
      _BOB=5
   End If 
   Paste Bob MENX-8+MENW/2,M2*8+5,_BOB
End If 
Return 
'
RMENU:
Read S$
If S$=_NULL$ or S$=_$ Then Return 
If Left$(S$,1)=_MINUS$ or Left$(S$,1)="­" : Rem the other minus
   S$=_SPACE$+S$+_SPACE$
Else 
   S$=_SPACE$+S$+_SPACER$
End If 
Return 
'  
DRWMENUC:
If _M1SEL>0
   If _M3SEL=0
      S$=Str$(_M1SEL)+Str$(_M2SEL)-_SPACE$
   Else 
      S$=Str$(_M1SEL)+Str$(_M2SEL)+Str$(_M3SEL)-_SPACE$
      Gosub CLRSUBMENU
   End If 
   If _M2SEL>0
      Restore 0+Val(S$)
      Read S$
      If Mid$(S$,2,1)=_MINUS$ or Instr(S$,_»$) or S$=_NULL$ or S$=_$
         MENFX=False : G$=_$
      Else If _M1SEL=3 and _M2SEL=11 : Rem menu collision 
         MENFX=True
         G$="_REVERSE"
      Else If _M1SEL=3 and _M2SEL=12 : Rem menu collision 
         MENFX=True
         G$="_INVERT"
      Else 
         Read MENFX,G$
      End If 
   Else 
      S$=_$ : MENFX=False : G$=_$
   End If 
   _CHOICE=True
End If 
_MENUON=False : _MENUDO=False : _MENUSUB=False
Gosub CLRMENU
Goto HEADER
'
CLRMENU:
If MENH>8
   If Fn MULTI
      Screen Copy 1,MENX,MENY-2,MENX+MENW,RNG_T To 0,MENX,HDR_H+MENY-2
      MENT=RNG_T-1 : MENB=RNG_B
   Else 
      MENT=MENY-2 : MENB=MENY+MENH-6
   End If 
   If XR<=MENX-1 and XR2>MENX and XR2>=MENX+MENW-1
      Screen Copy 1,MENX,MENT,MENX+MENW,MENB To 0,MENX,HDR_H+MENT,%110000
   Else If XR<=MENX-1 and XR2>MENX and XR2<=MENX+MENW-1
      Screen Copy 1,MENX,MENT,XR2,MENB To 0,MENX,HDR_H+MENT,%110000
      Screen Copy 1,XR2,MENT,MENX+MENW,MENB To 0,XR2,HDR_H+MENT
   Else If XR>=MENX-1 and XR2>MENX and XR2>=MENX+MENW-1
      Screen Copy 1,MENX,MENT,XR,MENB To 0,MENX,HDR_H+MENT
      Screen Copy 1,XR,MENT,MENX+MENW,MENB To 0,XR,HDR_H+MENT,%110000
   Else If XR>=MENX-1 and XR2>MENX and XR2<=MENX+MENW-1
      Screen Copy 1,MENX,MENT,XR,MENB To 0,MENX,HDR_H+MENT
      Screen Copy 1,XR,MENT,XR2,MENB To 0,XR,HDR_H+MENT,%110000
      Screen Copy 1,XR2,MENT,MENX+MENW,MENB To 0,XR2,HDR_H+MENT
   Else 
      Screen Copy 1,MENX,MENT,MENX+MENW,MENB To 0,MENX,HDR_H+MENT
   End If 
   If Fn MULTI
      Screen Copy 1,MENX,RNG_B,MENX+MENW,MENY+MENH-6 To 0,MENX,HDR_H+RNG_B
   End If 
   If RL>0
      Gosub DRWMARKS
   End If 
   If MENY+MENH+4>WVE_H+HDR_H
      Gosub CONTROLS
   End If 
   If CALC
      If MENX+MENW>CAL_X and((MENY+MENH+4)>CAL_Y or(MENB+HDR_H)>CAL_Y)
         Gosub CAL_BCK
      End If 
   End If 
End If 
MENH=0
CLRSUBMENU:
If SUBH>0
   If Fn MULTI
      Screen Copy 1,SUBX,SUBY-2,SUBX+SUBW,RNG_T To 0,SUBX,HDR_H+SUBY-2
      SUBT=RNG_T-1 : SUBB=RNG_B
   Else 
      SUBT=SUBY-2 : SUBB=SUBY+SUBH-6
   End If 
   If XR<=SUBX-1 and XR2>SUBX and XR2>=SUBX+SUBW-1
      Screen Copy 1,SUBX,SUBT,SUBX+SUBW,SUBB To 0,SUBX,HDR_H+SUBT,%110000
   Else If XR<=SUBX-1 and XR2>SUBX and XR2<=SUBX+SUBW-1
      Screen Copy 1,SUBX,SUBT,XR2,SUBB To 0,SUBX,HDR_H+SUBT,%110000
      Screen Copy 1,XR2,SUBT,SUBX+SUBW,SUBB To 0,XR2,HDR_H+SUBT
   Else If XR>=SUBX-1 and XR2>SUBX and XR2>=SUBX+SUBW-1
      Screen Copy 1,SUBX,SUBT,XR,SUBB To 0,SUBX,HDR_H+SUBT
      Screen Copy 1,XR,SUBT,SUBX+SUBW,SUBB To 0,XR,HDR_H+SUBT,%110000
   Else If XR>=SUBX-1 and XR2>SUBX and XR2<=SUBX+SUBW-1
      Screen Copy 1,SUBX,SUBT,XR,SUBB To 0,SUBX,HDR_H+SUBT
      Screen Copy 1,XR,SUBT,XR2,SUBB To 0,XR,HDR_H+SUBT,%110000
      Screen Copy 1,XR2,SUBT,SUBX+SUBW,SUBB To 0,XR2,HDR_H+SUBT
   Else 
      Screen Copy 1,SUBX,SUBT,SUBX+SUBW,SUBB To 0,SUBX,HDR_H+SUBT
   End If 
   If Fn MULTI
      Screen Copy 1,SUBX,RNG_B,SUBX+SUBW,SUBY+SUBH To 0,SUBX,HDR_H+RNG_B
   End If 
   If RL>0
      Gosub DRWMARKS
   End If 
   If SUBY+SUBH+4>WVE_H+HDR_H
      Gosub CONTROLS
   End If 
   If CALC
      If SUBX+SUBW>CAL_X and SUBY+SUBH+4>CAL_Y
         Gosub CAL_BCK
      End If 
   End If 
End If 
SUBH=0
Return 
'
M0DEMENU:
If RENDERING or REALTIME Then Return 
If _M3SEL=_MODE and _TEXT=False
   Gosub _TOASTMODE
   Return 
End If 
If _REC Then Gosub _TGLRECORD
If _M3SEL=4 and Fn MULTIOFF
   Gosub CLRPLY
   Bank Swap 5,B+7
   PBANK=B+7
   Gosub R4INSET
Else If Fn MULTI and _M3SEL-4
   If Length(B+7)=0
      Gosub CLRPLY
   End If 
   Bank Swap B+7,5
   PBANK=5
   Gosub R4INRESTORE
End If 
If B>1 and B<5 and Length(B+7)>0
   Swap ADVPOS#(B),ADVPOS#(1)
   Swap XP(B),XP(1)
   Swap XP(B),XP(1)
   Swap OFFSET(B),OFFSET(1)
End If 
_MODE=_M3SEL
Gosub _TOASTMODE
Gosub PBANKLEN
Gosub SETPBANK
Gosub BANKSET
Gosub _RANGENONE
RENDERING=True
Gosub CLRPOS
Gosub _DRAW
Gosub SETCORDS
RENDERING=False
Gosub DRWTIME
Gosub HEADERCORDS
If _VST Then Gosub SOCK_SENDVSTUP
Return 
'
_14BIT:
Volume PAULA(4),1
Volume PAULA(3),1
_TOAST$="14bit"
Return 
'
_TOASTMODE:
If Fn MONO
   _TOAST$=_TOAST$+"Mono "
   _CHANNELS=1
Else If Fn STEREO
   _TOAST$=_TOAST$+"Stereo "
   _CHANNELS=2
Else If Fn QUAD
   _TOAST$=_TOAST$+"Quad "
   _CHANNELS=4
Else If Fn MULTI
   _TOAST$=_TOAST$+"Multi "
   _CHANNELS=1
End If 
Goto T0AST
'
RATEMENU:
FREQ=Val(S$)
_AMIHZ=False
Gosub SETHZ
Ink _PANE : Gosub AMIHZDIAL : _HIGHDIAL=False
Goto DIALMAIN
'
'
'
'***************************************************************************** 
'*windows********************************************************************* 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'.::    .   .:: :::  ::.    :::.::::::-.      ...     .::    .   .:: .::::::.  
'';;,  ;;  ;;;' ;;;  ;;;;,  `;;;;;,   `';, .;;;;;;;.  ';;,  ;;  ;;;';;;`    `; 
' '[[, [[, [['  [[[   [[[[[. '[[`[[     [[,[[     \[[, '[[, [[, [[' '[==/[[[[, 
'   Y$c$$$c$P   $$$,  $$$ "Y$c$$ $$,    $$$$$,     $$$   Y$c$$$c$P    '''    $ 
'    "88"888   o88888888    Y88  888_,o8P'"888,_ _,88P    "88"888   ,88b    dP 
'     "M "M"   MMM"MMMM     YM  "MMMMP"`    "YMMMMMP"      "M "M"     "YMmMY"  
'
'
'
WIN:
Gosub NUDGEPOS
RENDERING=True
If _COL=_PANE Then Ink _SHADOW Else Ink _COL-1
Bar WX-4,WY-3 To WX-1,WY2+3
Bar WX2+1,WY-3 To WX2+4,WY2+3
Bar WX,WY-3 To WX2,WY-1
Bar WX,WY2+1 To WX2,WY2+3
Ink _BLACK
Box WX-5,WY-4 To WX2+5,WY2+4
Ink _COL
Bar WX,WY To WX2,WY2
Ink _SHADOW
Bar WX+1,WY+1 To WX2-1,WY+13
Ink _COL-1
Box WX+1,WY+1 To WX2-1,WY2-1
Ink _WHITE,_SHADOW
Text WX+4,WY+10,WTXT$
Return 
'
WINWAITC:
_COL=_WHITE
PTGL=1 : Rem show play time 
BTXT1$=_OK$ : BX=267 : BY=WY2-BUT_H-4 : BZN=50 : Gosub GRAYBTN
Repeat 
   Gosub GVAL
   If ABOUT and PLYTIME>0 and _AMIVER>3
      Gosub TIME_PLY
   End If 
   If(Z=50 or(SHORTCUTS and(Z=9 or Z=10))) and M=1 : Rem ntsc fix 
      PZN=Z : Gosub PRESS
      If _ON
         Exit 
      End If 
   End If 
Until Fn KEY_RETURN or Fn KEY_ESC
Reset Zone 50
WINC:
If _INIT Then Gosub SH0WWAVE
If _CHOICE-3 Then Gosub FRESHSEL
If Fn MULTI Then Gosub R4INSET
RENDERING=False
Gosub CLRPOS
_TEXT=False
Return 
'
FREQUEST:
If Fn A500 Then Gosub _STOP
FRQ=False : _TEXT=True
WX=100 : WY=21 : WX2=540 : WY2=174 : _COL=_WHITE : Gosub WIN
Paste Bob WX2-22,WY+4,4
Set Zone 73,WX2-22,WY+4 To WX2-10,WY+10
Ink _SHADOW
Draw 104,156 To 420,156
Set Zone 40,402,40 To 424,48
Set Zone 41,402,144 To 424,154
Set Zone 43,110,157 To 420,173
_COL=_WHITE
BX=432
BTXT1$=_OK$ : BY=39 : BZN=50 : Gosub GRAYBTN
BTXT1$="Parent" : Add BY,BUT_H : BZN=51 : Gosub GRAYBTN
BTXT1$="Volumes" : Add BY,BUT_H : BZN=52 : Gosub GRAYBTN
Gosub DEVBUT
BTXT1$=_CANCEL$ : Add BY,BUT_H+6 : BZN=58 : Gosub GRAYBTN
If FTOTAL=0 Then Gosub GDR
Gosub DRWDR
Gosub DRWSLI
CR$=_$ : TXT_X=0 : _MAX=128
Gosub DRWFILE
Repeat 
   Gosub TESTMOVE
   If(C=123 or Fn KEY_DOWNCUR or(Z=41 and M=1)) and FTOTAL>15 : Rem scroll down
      Repeat 
         Inc FPOS
         If C=123
            Add FPOS,4
         End If 
         If FPOS>FTOTAL-16
            FPOS=FTOTAL-16
         End If 
         Gosub DRWDR
         Gosub DRWSLI
         Gosub SPIN
         Gosub MKEY
      Until M-1
   Else If(C=122 or Fn KEY_UPCUR or(Z=40 and M=1)) and FTOTAL>15 : Rem scroll up
      Repeat 
         Dec FPOS
         If C=122
            Add FPOS,-4
         End If 
         If FPOS<0
            FPOS=0
         End If 
         Gosub DRWDR
         Gosub DRWSLI
         Gosub SPIN
         Gosub MKEY
      Until M-1
   Else If((Z>58 and Z<73) or Z=0) and PZN-Z : Rem filename rollover rollout
      Gosub DRWDR
      PZN=Z
   Else If Fn KEY_RETURN
      FRQ=True
      Exit 
   Else If M=1
      PZN=Z
      If Z=50 : Rem ok 
         PZN=Z : BY=39 : Gosub PRESS
         If _ON
            FRQ=True
            Exit 
         End If 
      Else If Z=43 : Rem text input 
         N=Len(F$)
         Repeat 
            T$=F$
            Gosub _TEXTINPUT
            F$=T$
            Ink _BG
            CR$=_SCORE$
            FOFF=Len(F$)-37
            Gosub DRWFILE
            Gosub TESTMOVE
         Until Fn KEY_RETURN or Fn KEY_ESC or M=1
         If Fn KEY_ESC
            Exit 
         End If 
         CR$=_$
         Gosub DRWFILE
      Else If Z=42 and FTOTAL>15 : Rem scroll bar 
         MY=CY-((FPOS*SLISIZE#)/14)
         Repeat 
            Gosub TESTMOVE
            FPOS=((CY-MY)/SLISIZE#)*14
            If FPOS<0
               FPOS=0
            End If 
            If FPOS>FTOTAL-16
               FPOS=FTOTAL-16
            End If 
            Gosub DRWDR
            Gosub DRWSLI
         Until M=0
      Else If Z=51 : Rem parent 
         PZN=Z : BY=53 : Gosub PRESS
         If _ON
            Parent 
            Gosub GDR
            Gosub DRWDR
            Gosub DRWSLI
         End If 
      Else If Z=52 : Rem volumes
         PZN=Z : BY=67 : Gosub PRESS
         If _ON
            Gosub VLUMES
            Gosub DRWDR
            Gosub DRWSLI
         End If 
      Else If Z>52 and Z<58 : Rem devices
         PZN=Z : BY=67+((Z-52)*BUT_H) : Gosub PRESS
         If _ON
            Trap Dir$=DEVS$(Z-52)
            If Errtrap
               Gosub FLSHSCR
            Else 
               Gosub GDR
               Gosub DRWDR
               Gosub DRWSLI
            End If 
         End If 
      Else If(Z=58 or Z=5) : Rem cancel and ntsc fix
         PZN=Z : BY=157 : Gosub PRESS
         If _ON
            Exit 
         End If 
      Else If Z>58 and Z<73 : Rem select file
         Gosub CHOFN
      Else If Z=73 : Rem reload 
         Gosub GDR
         Gosub DRWDR
         Gosub DRWSLI
         _TOAST$=_SPACE$+Dir$ : Gosub T0AST
      End If 
   Else If Fn KEY_ESC
      Exit 
   End If 
Until FRQ
I=40
Repeat 
   Reset Zone I
   Inc I
Until I>73
Gosub MAGICWAND
If FRQ and F$=_$
   FRQ=False
   Gosub FLSHSCR
Else 
   LF$=F$
End If 
Goto WINC
'
DEVBUT:
BY=67
I=1
Repeat 
   Add BY,BUT_H : Inc BZN
   BTXT1$=DEVS$(I) : Gosub GRAYBTN
   Inc I
Until I>5
Return 
'  
CHOFN:
Paper _WHITE : Pen _GRAY
TVDL$=FILES$(FPOS+Z-58)
I=Len(TVDL$)
Repeat 
   Exit If Mid$(TVDL$,I,1)<>_SPACE$
   Dec I
Until I<0
If TVDL$<>_$
   TVDC$=Left$(TVDL$,1)
   If I=0
      TVDF$=Mid$(TVDL$,2,Len(TVDL$))
   Else 
      TVDF$=Mid$(TVDL$,2,I-1)
   End If 
   If TVDC$="*" or TVDC$="?" or TVDC$="#"
      If TVDC$="?"
         DEVS$(5)=TVDF$
         BZN=52 : Gosub DEVBUT
      End If 
      Gosub MUP
      Gosub GVAL
      If Z>0
         If TVDL$=FILES$(FPOS+Z-58)
            Trap Dir$=TVDF$
            If Errtrap
               S$=WTXT$
               TXT1$="Error opening" : TXT2$='"'+TVDF$+'"' : Gosub _ERROR
               WTXT$=S$
               Dir$=PROGDIR$
               Pop : Goto FREQUEST
            Else 
               Gosub GDR
               Gosub DRWDR
               Gosub DRWSLI
            End If 
         End If 
      End If 
   Else 
      F$=TVDF$
      If S$=F$ : Rem double click 
         FRQ=True
      Else 
         S$=F$
         FOFF=Len(F$)-37
         Gosub DRWFILE
      End If 
      Gosub MUP
   End If 
End If 
Return 
'
DRWFILE:
Ink _BLACK,_WHITE
Text 120,167,Space$(37)
If FOFF<0 Then FOFF=0
Text 120,167,Mid$(F$,FOFF+1,Min(Len(F$)-FOFF,37))
Gr Writing %10
If N-FOFF>0
   Text 120,167,Space$(N-FOFF)+CR$+_SPACE$
Else 
   Text 120,167,CR$+_SPACE$
End If 
Gr Writing %1
Return 
'  
GDR:
Gosub CLRFILES
Gosub SETFILENAMELENGTH
D$=Dir First$(_$)
While D$<>_$
   If Left$(D$,1)<>"*"
      FILEL(TCOUNT)=Val(Right$(D$,8))
      I=Len(D$)-8
      Repeat 
         Exit If Mid$(D$,I,1)<>_SPACE$
         Dec I
      Until I<0
      If FILEL(TCOUNT)>1000000
         Trap Open In 1,Mid$(D$,2,I-1)
         If Not Errtrap
            FILEL(TCOUNT)=Lof(1)
            Close 1
         End If 
      End If 
   Else 
      I=Len(D$)
   End If 
   FILES$(TCOUNT)=Mid$(D$,0,I)
   D$=Dir Next$
   Inc TCOUNT
Wend 
FTOTAL=TCOUNT : Inc FTOTAL
Return 
'  
CLRFILES:
FPOS=0 : FTOTAL=0
I=1
Repeat 
   FILES$(I)=_$
   FILEL(I)=0
   Inc I
Until I>_FR
TCOUNT=1
Return 
'
VLUMES:
Gosub CLRFILES
D$=Dev First$("d:") : S$="?" : Gosub VTYPE
D$=Dev First$("a:") : S$="#"
VTYPE:
Repeat 
   FILES$(TCOUNT)=S$+D$-_SPACE$
   Inc TCOUNT
   D$=Dev Next$
Until D$=_$
FTOTAL=TCOUNT
Return 
'
DRWDR:
Pen _BLACK
TCOUNT=FPOS
I=5
Repeat 
   N=I+54
   Inc TCOUNT
   FILEL=FILEL(TCOUNT)
   FILES$=Space$(34)
   Mid$(FILES$,0)=FILES$(TCOUNT)
   D$=Left$(FILES$,1)
   If D$="*"
      S$=Zone$("(Dir)"+Mid$(FILES$,2),N)
   Else If D$="?"
      S$=Zone$("(Dev)"+Mid$(FILES$,2)+Space$(34-Len(FILES$)),N)
   Else If D$="#"
      S$=Zone$(_SPACE$+Mid$(FILES$,2)+Space$(38-Len(FILES$)),N)
   Else 
      If FILEL=0 and FILES$-_SPACE$=_$
         S$=Zone$(Space$(38),N)
      Else 
         If FILEL<1000
            S$=Str$(FILEL)-_SPACE$+"B"
         Else If FILEL<1000000
            S$=Str$(FILEL/1000)-_SPACE$+_KILO$
         Else 
            S$=Str$(FILEL/1000000)-_SPACE$+_MEGA$
         End If 
         S$=Zone$(FILES$+Space$(4-Len(S$))+S$,N)
      End If 
   End If 
   If TCOUNT=(FPOS+Z-58) and Len(FILES$-_SPACE$)>1 Then Paper _PANE Else Paper _WHITE
   Locate 13,I
   Print S$
   Inc I
Until I>18
Return 
'
DRWSLI:
If FTOTAL<15
   SLISIZE#=1.0
Else 
   SLISIZE#=(FTOTAL-2)/14.0
End If 
SLISIZE#=96/SLISIZE#
SLIPOS#=(SLISIZE#*FPOS)/14.0
Ink _BG
Bar 408,48 To 423,143
Ink _GRAY
Bar 410,Int(48+SLIPOS#) To 421,Int(48+SLIPOS#+SLISIZE#)
Set Zone 42,410,Int(48+SLIPOS#) To 421,Int(48+SLIPOS#+SLISIZE#)
Paste Bob 408,40,5
Paste Bob 408,144,6
Return 
'
_REQUEST:
RX=200 : RY=59
WTXT$="Request" : WX=RX : WY=RY-14 : WX2=RX+239 : WY2=104 : _COL=_WHITE : Gosub WIN
Ink _BG,_WHITE
Text RX+120-(Text Length(TXT1$)/2),RY+11,TXT1$
Text RX+120-(Text Length(TXT2$)/2),RY+21,TXT2$
_COL=_WHITE
BX=RX+12 : BY=RY+28 : BZN=48 : Gosub GRAYBTN
KT$=Lower$(Left$(BTXT1$,1))
BTXT1$=_CANCEL$ : BX=RX+125 : BZN=49 : Gosub GRAYBTN
Do 
   Gosub TESTMOVE
   PZN=Z
   If(M=1 and Z=48) or Lower$(K$)=KT$ or Fn KEY_RETURN
      PZN=Z : BX=RX+12 : Gosub PRESS
      If _ON
         REQ=True
         Exit 
      End If 
   Else If(M=1 and Z=49) or Lower$(K$)="c" or Fn KEY_ESC
      PZN=Z : BX=RX+125 : Gosub PRESS
      If _ON
         REQ=False
         Exit 
      End If 
   End If 
Loop 
Reset Zone 48 : Reset Zone 49
Goto WINC
'
_NOTICE:
If Not _INIT
   Amos To Front : View 
End If 
_TEXT=True
RX=216 : RY=59
WTXT$="Notice" : WX=RX-5 : WY=RY-14 : WX2=429 : WY2=104 : _COL=_WHITE : Gosub WIN
Ink _BG,_WHITE
Text RX+104-(Text Length(TXT1$)/2),RY+11,TXT1$
If Len(TXT2$)>25
   TXT2$='"'+_DOTS$+Right$(TXT2$,21)
End If 
Text RX+104-(Text Length(TXT2$)/2),RY+21,TXT2$
Goto WINWAITC
'  
ECHO_WIN:
WX=98 : WY=26 : WX2=536 : WY2=122 : _COL=_WHITE : Gosub WIN
Ink _PANE,_WHITE
Paper _WHITE
Pen _GRAY
Locate ,6 : Centre " 2nd:"+Str$(Length(PBANK)/2)+" 3rd:"+Str$(Length(PBANK)/3)+" 4th:"+Str$(Length(PBANK)/4)+" 5th:"+Str$(Length(PBANK)/5)
Locate ,7 : Centre " 6th:"+Str$(Length(PBANK)/6)+" 7th:"+Str$(Length(PBANK)/7)+" 8th:"+Str$(Length(PBANK)/7)+" 9th:"+Str$(Length(PBANK)/9)
Locate ,8 : Centre " 12th:"+Str$(Length(PBANK)/12)+" 16th:"+Str$(Length(PBANK)/16)+" 24th:"+Str$(Length(PBANK)/24)+" 32nd:"+Str$(Length(PBANK)/32)
Locate ,9 : Centre " 48th:"+Str$(Length(PBANK)/48)+" 64th:"+Str$(Length(PBANK)/64)+" 96th:"+Str$(Length(PBANK)/96)+" 128th:"+Str$(Length(PBANK)/128)
_EFFN=0
T$=_$
Repeat 
   If C>0 and C<11
      T$=T$+K$
      Inc _EFFN
   End If 
   If C=65 and _EFFN>0
      Dec _EFFN
      T$=Mid$(T$,0,_COUNT)
   End If 
   Ink _BG,_WHITE
   Text 226,89,"bytes:"+T$+_SCORE$+_SPACE$
   Gosub GVAL
Until Fn KEY_RETURN or Fn KEY_ESC
_EFFN=Val(T$)
Return 
'
_OPENFXBIGWIN:
REALTIME=True
_REPEAT$=CHAIN$(1)
RX=215 : RY=59
WTXT$=S$ : WX=RX-5 : WY=RY-15 : WX2=RX+215 : WY2=RY+94 : _COL=_WHITE : Gosub WIN
BTXT1$=_OK$ : BX=RX : BY=RY+77 : BZN=60 : Gosub BTN
BTXT1$=_CANCEL$ : BX=RX+107 : BZN=61 : Goto BTN
'
_OPENFXSMALLWIN:
REALTIME=True
_REPEAT$=CHAIN$(1)
FXRUN$=_$
RX=215 : RY=59
WTXT$=S$ : WX=RX-5 : WY=RY-15 : WX2=RX+215 : WY2=RY+25 : _COL=_WHITE : Gosub WIN
BTXT1$=_OK$ : BX=RX : BY=RY+8 : BZN=60 : Gosub BTN
BTXT1$=_CANCEL$ : BX=RX+107 : BZN=61 : Goto BTN
'
_OPENFXWIN:
REALTIME=True
_REPEAT$=CHAIN$(1)
RX=215 : RY=59
WTXT$=S$ : WX=RX-5 : WY=RY-15 : WX2=RX+215 : WY2=RY+45 : _COL=_WHITE : Gosub WIN
BTXT1$=_OK$ : BX=RX : BY=RY+28 : BZN=60 : Gosub BTN
BTXT1$=_CANCEL$ : BX=RX+107 : BZN=61 : Goto BTN
'
_CLOSEFXWIN:
If Not REQ Then _REPEAT$=_REPEATP$
Reset Zone 60
Reset Zone 61
Reset Zone 62
Goto DIALMAIN
'
_RUNFXWIN:
_EXIT=False
Repeat 
   Gosub GVAL
   If Z=60 and M=1
      PZN=Z : BX=RX : Gosub PRESS
      If _ON
         REQ=True
         _EXIT=True
      End If 
   Else If Z=61 and M=1 or Fn KEY_ESC
      PZN=Z : BX=RX+106 : Gosub PRESS
      If _ON
         REQ=False
         _EXIT=True
      End If 
   Else If Z=62 and M=1 and FXRUN$<>_$
      Repeat 
         Gosub FXSET
         Gosub FXRUN$
         Gosub FXDRW
      Until M=0
   Else If M=1
      Gosub DRWDIALS
   Else If C>0
      Gosub KCHOOSE
   End If 
Until _EXIT
Gosub MUP
Gosub _CLOSEFXWIN
Gosub WINC
REALTIME=False : CHAIN$(1)=_$ : _CHANNEL=1
If REQ
   PASS=0
   Gosub _UNDOSET
End If 
Return 
'
_TEXTINPUT:
If((C>0 and C<59) or Fn KEY_SPACE) and N<_MAX
   T$=Left$(T$,N)+K$+Right$(T$,Len(T$)-N)
   Inc N
End If 
If Fn KEY_LEFTCUR and N>0
   Dec N
Else If Fn KEY_BACKSPACE and N>0
   Dec N
   T$=Left$(T$,N)+Right$(T$,(Len(T$)-1)-N)
Else If Fn KEY_DEL and N<Len(T$)-1
   T$=Left$(T$,N)+Right$(T$,(Len(T$))-N-1)
Else If Fn KEY_DEL and N<Len(T$)
   T$=Left$(T$,N)
Else If Fn KEY_RIGHTCUR and N<Len(T$)
   Inc N
End If 
If TXT_X=0 Then Return 
Ink _BLACK,_WHITE
Text X+TXT_X-Text Length(TXT1$),TXT_Y,TXT1$+T$+_SPACER$
Gr Writing %10
Text X+TXT_X-Text Length(TXT1$),TXT_Y,Space$(Len(TXT1$))+Space$(N)+_SCORE$
Gr Writing %1
Return 
'
_RUNFXTEXTWIN:
_TEXT=True : _EXIT=False
T$=_$
TXT_X=340 : TXT_Y=Y+72 : N=0 : _MAX=9
Repeat 
   Gosub _TEXTINPUT
   Repeat 
      Gosub GVAL
      If Z=60 and M=1 or Fn KEY_RETURN
         PZN=Z : BX=RX+2 : Gosub PRESS
         If _ON
            REQ=True
            _EXIT=True
         End If 
      Else If Z=61 and M=1 or Fn KEY_ESC
         PZN=Z : BX=RX+108 : Gosub PRESS
         If _ON
            REQ=False
            _EXIT=True
         End If 
      End If 
   Until _EXIT or C
Until _EXIT
Gosub _CLOSEFXWIN
Goto WINC
'
FXDRW:
Ink _WHITE,_SHADOW
N=Text Length(Str$(FXSN)-_SPACE$)+Text Length(FXS$)
Text RX+204-N,RY-5,Space$(Len(Str$(FXSN)-_SPACE$))
Text RX+212-N,RY-5,Str$(FXSN)-_SPACE$+FXS$
Ink _BG
Bar RX+1,RY+6 To RX+209,RY+6+BUT_H
Ink _GRAY
Bar RX+1+FXN,RY+6 To RX+1+FXN+8,RY+6+BUT_H
Set Zone 62,RX+FXN,RY+6 To RX+FXN+10,RY+6+BUT_H
Return 
'
FXSET:
Gosub TESTMOVE
FXN=CX-224
If FXN=<0
   FXN=0
Else If FXN>199
   FXN=200
End If 
Return 
'  
SETREALTIME:
If REALTIME or MACRO
   FXSHOW=False
Else 
   FXSHOW=True
End If 
Return 
'  
'
'
'***************************************************************************** 
'*buttons********************************************************************* 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
' :::::::.   ...    :::::::::::::::::::::::    ...     ::.    :::. .::::::.  
'  ;;;'';;' ';;     ;;;;;;;;;;''';;;;;;;''' .;;;;;;;.  ;;;;,  `;;;;;;`    `; 
'  [[[__[[\.[['     [[[    [[        [[    ,[[     \[[, [[[[[. '[['[==/[[[[, 
'  $$""""Y$$$$      $$$    $$        $$    $$$,     $$$ $$$ "Y$c$$  '''    $ 
' _88o,,od8P88    .d888    88,       88,   "888,_ _,88P888    Y88 ,88b    dP 
' ""YUMMMP"  "YmmMMMM""    MMM       MMM     "YMMMMMP" MM     YM    "YMmMY"  
'
'
'
ALLBTN:
_COL=_PANE
BZN=1
Repeat 
   Gosub ZBTN
   Inc BZN
Until BZN>_AB
Gosub LPBTN
Goto LPMIXBTN
'
PLYBTN:
Gosub GFXSET
_COL=_WHITE
If RNGEPLY
   BZN=2 : Gosub ZBTN
End If 
If DISPPLY
   BZN=3 : Gosub ZBTN
End If 
BTXT1$="Play|Stop" : BZN=1 : BX=BUTX(BZN-1) : BY=BUTY(BZN-1) : Gosub BTN
Goto GFXRESTORE
'
GRAYBTN:
Ink _GRAY
Box BX,BY To BX+BUT_W,BY+BUT_H
Goto B1
'
ZBTN:
BTXT1$=BUTS$(BZN-1) : BX=BUTX(BZN-1) : BY=BUTY(BZN-1)
'
BTN:
Ink _BLACK
Box BX,BY To BX+BUT_W,BY+BUT_H
B1:
Gosub PATSET
Ink _COL+4,_GRAY
Box BX+1,BY+1 To BX+BUT_W-1,BY+BUT_H-1
Gosub PATRESTORE
Ink _COL,-1
Bar BX+2,BY+2 To BX+BUT_W-2,BY+BUT_H-2
Ink _BLACK,_COL
Text BX+((BUT_W-(Text Length(BTXT1$)))/2)+1,BY+BUT_H-4,BTXT1$
Set Zone BZN,BX,BY To BX+BUT_W,BY+BUT_H
Return 
'
PRESSBTN:
BX=BUTX(PZN-1) : BY=BUTY(PZN-1)
PRESS:
_ON=True
Gosub HIGHBTN
Repeat 
   If C=0 Then Gosub TESTMOVE Else Gosub MKEY
   If PZN<>Z and _ON
      _ON=False
      Gosub HIGHBTN
   Else If PZN=Z and _ON=False
      _ON=True
      Gosub HIGHBTN
   End If 
Until M=0
If Not _ON Then Return 
'
HIGHBTN:
Gr Writing %10
Bar BX,BY To BX+BUT_W,BY+BUT_H
Gr Writing %1
Return 
'
_TGLLOOP:
If _LP Then _LP=False Else _LP=True
LPBTN:
If _LP Then _COL=_WHITE Else _COL=_PANE
BZN=13 : Goto ZBTN
'
_TGLMIXLOOP:
If _LPMIX Then _LPMIX=False Else _LPMIX=True
LPMIXBTN:
If _LPMIX Then _COL=_WHITE Else _COL=_PANE
BZN=14 : Goto ZBTN
'
_REPEAT:
If _REPEAT$=_$
   TXT1$="No subroutine to" : TXT2$="repeat" : Goto _ERROR
End If 
PASS=0
Gosub _UNDOSET
REQ=True
T$=_REPEAT$
_TOAST$=T$
MACRO=True
_REPEATING=True
Gosub CMD
_REPEATING=False
MACRO=False
Return 
'  
'
'
'***************************************************************************** 
'*gadgets********************************************************************* 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'    .,-:::::/    :::.     ::::::-.   .,-:::::/  .,:::::::::::::::: .::::::. 
'   ;;-'````'     ;;`;;    ;;,   `';,;;-'````'   ;;;;'''';;;;;;;''';;;`    `;
'   [[   [[[[[[/ ,[[ '[[,  `[[     [[[[   [[[[[[/ [[cccc     [[    '[==/[[[[,
'   $$c.    "$$ c$$$cc$$$c  $$,    $$$$c.    "$$  $$""""     $$      '''    $
'   `Y8bo,,,o88  888   888, 888_,o8P'`Y8bo,,,o88  888oo,_    88,   ,88b    dP
'     `'YMUP"YMM YMM   ""` "MMMMP"`    `'YMUP"YMM """"YUM    MMM     "YMmMY" 
'
'
'
SCR0LL:
If _TEXT or SETTING$(24)=_FALSE$ Then Return 
Scroll 2
Return 
'
R4INBOW:
If Fn A500 Then Return 
Set Rainbow 0,_PANE,WVE_H+1,_$,_$,_$
Return 
'
R4INSET:
If Fn A500 Then Return 
WVE_D=WVE_H/4
BBARS=0 : CBARS=0
Repeat 
   Inc BBARS
   CCOLS=CBARS
   Repeat 
      If B=BBARS Then CCOL=Colour(_PANE)+$111 Else CCOL=Colour(_PANE)
      Rain(0,CCOLS)=CCOL
      Inc CCOLS
   Until CCOLS>CBARS+WVE_D
   Add CBARS,WVE_D
Until CBARS>WVE_D*3
Rainbow 0,0,56,WVE_H
Return 
'  
R4INRESTORE:
If Fn A500 Then Return 
If BBARS=0 Then Return 
CBARS=0
Repeat 
   Rain(0,CBARS)=_PANEC
   Inc CBARS
Until CBARS>WVE_H-1
Rainbow 0,0,56,WVE_H
Return 
'
T0ASTWAITER:
If _TOASTWAIT>0
   Inc _TOASTWAIT
   If _TOASTWAIT>5
      _TOASTWAIT=0
      Goto FLSHSCRC
   End If 
End If 
Return 
'
T0AST:
_T=Len(_TOAST$)
If Len(_TOAST$)>24
   _TOAST2$=Right$(_TOAST$,Len(_TOAST$)-24)
   _TOAST$=Left$(_TOAST$,24)
Else 
   _TOAST2$=_$
End If 
Repeat 
   Scroll 1
   Dec _T
Until _T<1
Gosub GFXSET
Ink _SHADOW,_PANE
Text TST_W-Text Length(_TOAST$),TST_T,_TOAST$
Gosub GFXRESTORE
_TOAST$=_TOAST2$
Return 
'
CONTROLS:
Cls _PANE,0,WVE_H+HDR_H To SCR_W,SCR_H
Gosub MAGICWAND
Gosub ALLBTN
If PLYING or BGPLYING
   Gosub PLYBTN
End If 
Gosub DRWTIMENOW
Gosub DRWHZ
Goto DIALMAIN
'
DIALMAIN:
I=0
DIA_X=120
DIA_Y=WVE_H+25
DIA_W=33
DIA_C=0
Repeat 
   Gosub DIALS
   Inc I : Inc DIA_C
Until I>1
Return 
'  
DIALRES:
I=2
DIA_X=228
DIA_Y=HDR_H+52
DIA_W=104
DIA_C=0
Ink _GRAY,_WHITE
Text X+267,Y+77,"Freq.        Q."
Repeat 
   Gosub DIALS
   Inc I : Inc DIA_C
Until I>3
Return 
'  
DIALCOMP:
I=4
DIA_X=228
DIA_Y=HDR_H+52
DIA_W=104
DIA_C=0
Ink _GRAY,_WHITE
Text X+267,Y+77,"Thres.       Slope"
Text X+267,Y+100,"Window       Ahead"
Text X+267,Y+123,"Attack       Re."
Repeat 
   Gosub DIALS
   If I=5 or I=7
      DIA_Y=DIA_Y+23
      DIA_C=DIA_C-2
   End If 
   Inc I : Inc DIA_C
Until I>9
Return 
'  
DIALT0NE:
I=10
DIA_X=228
DIA_Y=HDR_H+52
DIA_W=104
DIA_C=0
Ink _GRAY,_WHITE
Text X+267,Y+77,"Freq.        Amp."
Repeat 
   Gosub DIALS
   Inc I : Inc DIA_C
Until I>11
Return 
'
DIALS:
Paste Bob DIA_X+(DIA_W*DIA_C),DIA_Y,1
ROTX(I)=DIA_X+14+(DIA_W*DIA_C) : ROTY(I)=DIA_Y+8
ROTX2(I)=ROTX(I)+16 : ROTY2(I)=ROTY(I)+10
STRTX=ROTX(I) : STRTY=ROTY(I)
DESTX=STRTX+7.0*Cos(ROT#(I)) : DESTY=STRTY+5.0*Sin(ROT#(I))
Ink _SHADOW
Draw STRTX,STRTY To DESTX,DESTY
STRTX=STRTX+3.5*Cos(ROT#(I)) : STRTY=STRTY+2.5*Sin(ROT#(I))
Ink _BLACK
Draw STRTX,STRTY To DESTX,DESTY
Return 
'
DIALSET:
PY=CY
PX=CX
PROT#=ROT#(0) : Rem frequency dial click for AMIHZ 
Repeat 
   Gosub TESTMOVE
   If C>0
      Gosub KCHOOSE
   End If 
   If Fn KEY_SHIFT
      If CY>PY and _HIGHDIAL=False
         _INC#=0.0075
      Else 
         If _HIGHDIAL
            _INC#=0.0275
         Else 
            _INC#=0.01
         End If 
      End If 
   Else 
      _INC#=0.1
   End If 
   Gosub STICKY
   If CY<PY
      ROT#(_DIAL)=ROT#(_DIAL)+(_INC#*(PY-CY))
      If ROT#(_DIAL)>13.3
         ROT#(_DIAL)=13.3
      End If 
   Else If CY>PY
      ROT#(_DIAL)=ROT#(_DIAL)-(_INC#*((CY-PY)))
      If ROT#(_DIAL)<8.5
         ROT#(_DIAL)=8.5
      End If 
   End If 
   PY=CY
   PX=CX
   If _DIAL=1
      VOL=Int((ROT#(_DIAL)-8.5)*13.3334)
      Gosub SETVOL
   Else If _DIAL=0
      FREQ=(((ROT#(_DIAL)-8.5)*13.3)*(256+184))+601
      If M=0 and PROT#=ROT#(_DIAL)
         If _HIGHDIAL : Rem double click 
            _HIGHDIAL=False
            Ink _PANE
            _AMIHZ=False
         Else 
            _HIGHDIAL=True
            Ink _SHADOW
         End If 
      Else If _HIGHDIAL
         Ink _SHADOW
      Else 
         Ink _PANE
         _AMIHZ=False
      End If 
      Gosub AMIHZDIAL
      Gosub SETHZ
      If RENDERING
         Gosub DRWHZ
         Gosub DRWTIMENOW
      End If 
      Gosub DIALMAIN
   Else If _DIAL=3
      _RQ#=(ROT#(_DIAL)-8.5)/4.8
      Gosub RESET
      Gosub DIALRES
   Else If _DIAL=2
      RESN=Int(((ROT#(_DIAL)-8.5)/4.8)*200.0)
      Gosub RESET
      Gosub DIALRES
   Else If _DIAL=4
      _THRESHOLDN#=((ROT#(_DIAL)-8.5)/4.8)*100.0
      Gosub CPSET
      Gosub DIALCOMP
   Else If _DIAL=5
      _SLOPEN#=((ROT#(_DIAL)-8.5)/4.8)*100.0
      Gosub CPSET
      Gosub DIALCOMP
   Else If _DIAL=6
      _WINDOWN#=((ROT#(_DIAL)-8.5)/4.8)
      Gosub CPSET
      Gosub DIALCOMP
   Else If _DIAL=7
      _LOOKAHEADN#=((ROT#(_DIAL)-8.5)/4.8)*100.0
      Gosub CPSET
      Gosub DIALCOMP
   Else If _DIAL=8
      _ATTACKN#=((ROT#(_DIAL)-8.5)/4.8)*10.0
      Gosub CPSET
      Gosub DIALCOMP
   Else If _DIAL=9
      _RELEASEN#=((ROT#(_DIAL)-8.5)/4.8)*500.0
      Gosub CPSET
      Gosub DIALCOMP
   Else If _DIAL=10
      T0NEV#=Int((((ROT#(_DIAL)-8.5)/4.8)*(FREQ/8))+5)
      Gosub T0SET
      Gosub DIALT0NE
   Else If _DIAL=11
      T0NEA=((ROT#(_DIAL)-8.5)/4.8)*127
      Gosub DIALT0NE
   End If 
Until M=0
Return 
'
DRWDIALS:
_DIAL=0
Repeat 
   If Fn DIALZONE Then Goto DIALSET
   Inc _DIAL
Until _DIAL>1
If CHAIN$(1)="_RESONANTP"
   _DIAL=2
   Repeat 
      If Fn DIALZONE
         Goto DIALSET
      End If 
      Inc _DIAL
   Until _DIAL>3
Else If CHAIN$(1)="_COMPRESSORP"
   _DIAL=4
   Repeat 
      If Fn DIALZONE
         Goto DIALSET
      End If 
      Inc _DIAL
   Until _DIAL>9
Else If T0NEGENERATOR
   _DIAL=10
   Repeat 
      If Fn DIALZONE
         Goto DIALSET
      End If 
      Inc _DIAL
   Until _DIAL>11
End If 
Return 
'
SETVOL:
Volume PAULA(7),VOL
If M=0
   If VOL=64
      _TOAST$=_TOAST$+"Full "
   Else If VOL=0
      _TOAST$=_TOAST$+"Mute "
   End If 
End If 
Goto DIALMAIN
'  
PROBAR:
If FX=FX_START Then Return 
If FX>=FXE Then Return 
If REALTIME Then Return 
Gosub KEY
If Fn KEY_ESC
   FX=FXE : _STOP=True
   Return 
End If 
PRO#=FX-FX_START : PRO=Int((PRO#/FX_LENGTH)*128)
If OSCILLO
   If Not TGL
      PPRO=PRO-1
   End If 
End If 
If PPRO-PRO
   Ink _SHADOW
   Box 255,71 To 256+128,78
   Cls _SHADOW,256,72 To 256+PRO,78
   Cls _BLACK,256+PRO,72 To 256+128,78
   PPRO=PRO
End If 
Return 
'
METRO:
Cls _HIGHLIGHT,MET_X,MET_Y To MET_W,MET_H
Return 
'
METROC:
Cls _PANE,MET_X,MET_Y To MET_W,MET_H
Return 
'
LEV:
If RENDERING or _MENUDO or _DIRECT Then Return 
If Fn MULTI
   If PBANK-7>0 and PBANK-7<5
      OSC_START=P_BUF+(BSIZE*(PBANK-8))
   Else 
      Return 
   End If 
Else 
   OSC_START=P_BUF
End If 
PI=0
If OSCILLO
   Gosub OSC_BCK
   Ink _BRIGHT
   OI=0 : PI=0 : PPI#=0
   OSC#=256.0/BSIZE
   If Fn MONO or Fn MULTI
      If OSCILLO>2
         OSCILLO=1
      End If 
      '
      '********************
      ' oscilloscope 
      Repeat 
         OSC_X=Peek(OSC_START+PI)
         If OSC_X>127
            OSC_X=OSC_X-256
         End If 
         If OSC_X
            Ink _BRIGHT
         Else 
            Ink _GRAY
         End If 
         PPI#=PPI#+OSC#
         Inc PI
         Plot OI+OSC_W-128,(OSC_X/2)+77
         If OSC#>1.5 and POI<>OI
            Draw To OI+OSC_W-(128-OSC#),(OSC_X/2)+77
            POI=OI
         End If 
         OI=Int(PPI#)
      Until PI>=BSIZE
   Else 
      '  
      '********************
      ' x/y oscilloscope 
      Repeat 
         OSC_X=Peek(OSC_START+PI)
         If OSC_X>127
            OSC_X=OSC_X-256
         End If 
         OSC_Y=Peek(OSC_START+BSIZE+PI)
         If OSC_Y>127
            OSC_Y=OSC_Y-255
         End If 
         Gosub DRW_OSC
         PPI#=PPI#+OSC#
         OI=Int(PPI#)
         Inc PI
      Until PI>=BSIZE
   End If 
End If 
If _LEVEL
   If Fn A1200
      '  
      '********************
      ' peak level meter 
      LML=0
      PI=0
      Repeat 
         LMN=(Peek(OSC_START+PI)*32)/256
         If LMN>15
            LMN=LMN-32
         Else 
            LMN=-LMN
         End If 
         If LMN<LML
            LML=LMN
         End If 
         Add PI,10
      Until PI>LMC
   Else 
      '  
      '********************
      ' rms level meter  
      LMC=BSIZE
      If Fn STEREO
         LMC=LMC*2
      Else If Fn QUAD
         LMC=LMC*4
      End If 
      SUMM#=0.0
      PI=0
      Repeat 
         LMN#=Peek(OSC_START+PI)
         If LMN#>127
            LMN#=LMN#-256
         End If 
         LMN#=LMN#/127
         SUMM#=SUMM#+(LMN#*LMN#)
         Inc PI
      Until PI>LMC
      LML=-Int(Sqr(SUMM#/LMC)*16)
   End If 
   Gosub LEVC
   Ink _HIGHLIGHT
   DRWLEV:
   Gosub GFXSET
   Bar LEV_X,LEV_Y+LML To LEV_W,LEV_Y+1
   Gosub GFXRESTORE
End If 
Return 
'
LEVC:
Scroll 3
Return 
'
OSC_BCK:
_COL=_BLACK
OT$="×" : OSC_BZN=58 : OSC_BX=OSC_W+133 : OSC_BY=13 : Gosub OSC_TAB
OT$=Str$(OSCILLO)-_SPACE$ : OSC_BZN=59 : OSC_BY=30 : Gosub OSC_TAB
Cls _COL,OSC_W-128,13 To OSC_W+129,142
Return 
'
OSC_TAB:
Cls _COL,OSC_BX,OSC_BY To OSC_BX+23,OSC_BY+15
Ink _GRAY,_COL
Text OSC_BX+8,OSC_BY+10,OT$
Set Zone OSC_BZN,OSC_BX,OSC_BY To OSC_BX+23,OSC_BY+15
Return 
'
DRW_OSC:
If OSCILLO=1
   Plot OSC_Y+OSC_W,-(OSC_X/2)+77
Else If OSCILLO=2
   Plot OSC_X+OSC_W,-(OSC_Y/2)+77
Else If OSCILLO=3
   OSC_N=((OI+128))+64
   If OSC_X
      Ink _BRIGHT
   Else 
      Ink _GRAY
   End If 
   Plot OSC_N,(OSC_X/2)+77
   If OSC_Y
      Ink _BRIGHT
   Else 
      Ink _GRAY
   End If 
   Plot OSC_N,(OSC_Y/2)+77
Else If OSCILLO=4
   Plot -OSC_Y+OSC_W,(OSC_X/2)+77
Else If OSCILLO=5
   Plot -OSC_X+OSC_W,(OSC_Y/2)+77
End If 
Return 
'
_TGLOSCILLOSCOPE:
Gosub MUP
If OSCILLO
   OSCILLO=False
   Reset Zone 58
   Reset Zone 59
   RENDERING=True
   Gosub SH0WWAVE
   Gosub FRESHSEL
   Gosub CLRPOS
   RENDERING=False
Else 
   OSCILLO=1
   Gosub OSC_BCK
End If 
Return 
'
_TGLOSCILLOMODE:
Gosub MUP
Inc OSCILLO
If Fn MONO or Fn MULTI
   OSCILLO=1
Else 
   If OSCILLO>5
      OSCILLO=1
   End If 
End If 
Return 
'
_TGLCALCULATOR:
If CALC
   CALC=False
   RENDERING=True
   Gosub SH0WWAVE
   Gosub FRESHSEL
   Gosub CLRPOS
   RENDERING=False
Else 
   CALC=True
   Gosub CAL_BCK
End If 
Return 
'
CAL_BCK:
RENDERING=True
Gosub GFXSET
Ink _BLACK
Bar CAL_X,CAL_Y To CAL_X+CAL_W,CAL_Y+CAL_H
If Fn MULTI Then CI=Length(B+7) Else CI=Length(PBANK)
Gosub CAL_MUL
If CI>0
   Gosub CAL_LEN
   Ink _PANE,_BLACK
   Text CAL_X,CAL_Y+11,Str$(B)+Str$(CI)
   Text CAL_X,CAL_Y+19,Str$(B)+Str$(CDIV#)+_BPM$
End If 
CI=Length(LB+7)
Gosub CAL_MUL
If CI>0
   Gosub CAL_LEN
   Ink _SHADOW,_BLACK
   Text CAL_X,CAL_Y+29,Str$(LB)+_»$+Str$(CI)
   Text CAL_X,CAL_Y+37,Str$(LB)+_»$+Str$(CDIV#)+_BPM$
End If 
CHI_X=CAL_X+CAL_W-68
CHI_Y=CAL_Y+2
Paste Bob CHI_X,CHI_Y,7 : Rem 64x40 21x13x9        
POC=9 : NC=0
Repeat 
   CI=0
   Repeat 
      If POS=POC
         Ink _HIGHLIGHT
         Box CHI_X+(CI*21)+1,CHI_Y+(NC*13)+1 To CHI_X+(CI*21)+21-1,CHI_Y+(NC*13)+13-1
         Ink _BG
         Box CHI_X+(CI*21)+2,CHI_Y+(NC*13)+2 To CHI_X+(CI*21)+21-2,CHI_Y+(NC*13)+13-2
         Gosub GFXRESTORE
         RENDERING=False
         Return 
      End If 
      Inc CI
      Dec POC
   Until CI>2
   Inc NC
Until NC>2
Gosub GFXRESTORE
RENDERING=False
Return 
'
CAL_DRW:
POC=9 : NC=0
Repeat 
   CI=0
   Repeat 
      If M=1 and POS-POC
         If CX>CHI_X+(CI*21)+1 and CX<CHI_X+(CI*21)+21-1 and CY>CHI_Y+(NC*13)+2 and CY<CHI_Y+(NC*13)+13-2
            POS=POC
            Gosub CAL_BCK
            Gosub MUP
            Return 
         End If 
      End If 
      Inc CI
      Dec POC
   Until CI>2
   Inc NC
Until NC>2
Return 
'
CAL_LEN:
CDIV#=FREQ
CDIV#=((CDIV#/CI)*60)*4
Return 
'
CAL_MUL:
On POS Goto C1,C2,C3,C4,C5,C6,C7,C8,C9
C1: Return 
C2: CI=CI-(CI/4) : Return 
C3: CI=CI-(CI/3) : Return 
C4: CI=CI/4 : Return 
C5: CI=CI/3 : Return 
C6: CI=CI/2 : Return 
C7: CI=CI*4 : Return 
C8: CI=CI*3 : Return 
C9: CI=CI*2 : Return 
'
'
'
'***************************************************************************** 
'*network********************************************************************* 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'   ::.    :::..,::::::::::::::::.::    .   .::    ...     :::::::..  ::::::  .  
'   ;;;;,  `;;;;;;;'''';;;;;;;'''';;,  ;;  ;;;' .;;;;;;;.  ;;;;``;;;; ```;;; .;;,  
'    [[[[[. '[[ [[cccc     [[     '[[, [[, [[' ,[[     \[[,\[[[,/[[['    [[[[[/' 
'    $$$ "Y$c$$ $$""""     $$       Y$c$$$c$P  $$$,     $$$"$$$$$$c     _$$$$, 
'   888    Y88  888oo,_    88,       "88"888   "888,_ _,88P,888b "88bo "888"88o, 
'   MM     YM   """"YUM    MMM        "M "M"     "YMMMMMP" "MMMM   "W" MMM "MMP""  
'
'   BSDSocket.library Procedures originally by Internext Software (John Bintz) 
'
SOCK_SETIO:
Areg(0)=Varptr(T$)
Dreg(1)=Len(T$)
CONNECT=Lib Call(2,-$36)
Dreg(1)=$8004667E : Rem FIONBIO
Gosub SOCK_IO
Dreg(1)=$8004667D : Rem FIOASYNC 
SOCK_IO:
NB=1
Areg(0)=Varptr(NB)
NB=Lib Call(2,-$72)
Return 
'
SOCK_HOST:
NB=Lib Call(2,-$D2) : Rem HOSTINFO 
NB=Leek(Leek(Leek(NB+16))) : Rem HOSTADDRESS
T$=Chr$(16)+Chr$(2)+String$(_NULL$,14)
Loke Varptr(T$)+4,Leek(Varptr(NB))
Return 
'
SOCK_SEND:
Dreg(2)=0
NB=Lib Call(2,-$42)
Return 
'
SOCK_OPENSYNC:
Dreg(0)=2
Dreg(1)=2
Dreg(2)=0
SOCKSYNC=Lib Call(2,-$1E)
Areg(0)=Varptr(_ADDRSYNC$)
Gosub SOCK_HOST
Doke Varptr(T$)+2,Deek(Varptr(_PORTSYNC)+2)
Dreg(0)=SOCKSYNC
Gosub SOCK_SETIO
If CONNECT=-1
   TXT1$="Failed sync socket" : TXT2$=_$ : Gosub _NOTICE
   Gosub SOCK_CLOSESYNC
   SOCKSYNC=-1
End If 
Return 
'
SOCK_SENDSYNC:
Dreg(0)=SOCKSYNC
Areg(0)=Varptr(MSG$)
Dreg(1)=32
Goto SOCK_SEND
'
SOCK_CLOSESYNC:
If SOCKSYNC--1
   Dreg(0)=SOCKSYNC
   NB=Lib Call(2,-$78)
End If 
Return 
'  
SOCK_OPENVST:
Dreg(0)=2
Dreg(1)=2
Dreg(2)=0
SOCKVST=Lib Call(2,-$1E)
Areg(0)=Varptr(_ADDRVST$)
Gosub SOCK_HOST
Doke Varptr(T$)+2,Deek(Varptr(_PORTVST)+2)
Dreg(0)=SOCKVST
Gosub SOCK_SETIO
If CONNECT=-1
   TXT1$="Failed vst socket" : TXT2$=_$ : Gosub _NOTICE
   Gosub SOCK_CLOSEVST
   SOCKVST=-1
End If 
Return 
'
SOCK_SENDVSTUP:
VCH=_CHANNELS
If Fn MULTI Then VCH=4
VCH$=Str$(VCH)-_SPACE$
Dreg(0)=SOCKVST
Areg(0)=Varptr(VCH$)
Dreg(1)=1
Goto SOCK_SEND
'
SOCK_SENDVST:
Dreg(0)=SOCKVST
Areg(0)=P_BUF
Dreg(1)=BSIZE*VCH
Goto SOCK_SEND
'  
SOCK_CLOSEVST:
If SOCKVST--1
   Dreg(0)=SOCKVST
   NB=Lib Call(2,-$78)
End If 
Return 
'
'
'
'***************************************************************************** 
'*init************************************************************************ 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'    :::  ::.    :::. :::  ::::::::::  
'    ;;;  ;;;;,  `;;; ;;;  ;;;;;;;'''  
'    [[[   [[[[[. '[[ [[[      [[      
'    $$$,  $$$ "Y$c$$ $$$,     $$      
'   o88888888    Y88 o88888    88,     
'   MMM"MMMM     YM  MMM"MM    MMM     
'
'
'
_INIT:
_INIT=False
Restore DTA
I=1
Repeat 
   Read PAULA(I)
   Inc I
Until I>7
I=0
If Exist(CFGFILE$)
   Trap Open In 1,CFGFILE$
   If Not Errtrap
      N=Lof(1) : T$=Space$(N)
      Sload 1 To Varptr(T$),N : Close 1
      PX=1
      Repeat 
         CX=Instr(T$,_COMMA$,PX)
         MX=Instr(T$,_NEWLINE$,PX)
         If CX and CX<MX
            MX=CX
         End If 
         If MX
            SETTING$(I)=Mid$(T$,PX,MX-PX)
         End If 
         Inc MX : Inc I
         PX=MX
      Until PX>=N
   End If 
End If 
N=1
Repeat 
   If N<I
      Read T$
   Else 
      Read SETTING$(N)
   End If 
   Inc N
Until N>_OP
I=1
Repeat 
   DEVS$(I)=SETTING$(I)
   Inc I
Until I>4
RXDIR$=SETTING$(5)
_ADDRSYNC$=SETTING$(6)
_PORTSYNC=Val(SETTING$(7))
_ADDRVST$=SETTING$(8)
_PORTVST=Val(SETTING$(9))
_VST=SETTING$(25)<>_FALSE$
CN=Deek(Leek(4)+296) : Rem cpu
I=0 : N=0
Repeat 
   If Btst(I,CN) Then N=I+1
   Inc I
Until I>3
If N=0 : Rem 68000
   _AMIVER=1
Else If N=2 : Rem 68020
   _AMIVER=3
Else 
   _AMIVER=5
End If 
If Fast Free>0 Then Inc _AMIVER
If Fn A1200
   If Fn A500
      SCR_D=4
   End If 
   SETTING$(16)=_FALSE$
   SETTING$(17)=_FALSE$
   SETTING$(18)=_FALSE$
   SETTING$(19)=_FALSE$
   SETTING$(20)=_FALSE$
   SETTING$(21)=_TRUE$
   SETTING$(22)=_FALSE$
   SETTING$(24)=_FALSE$
   SETTING$(26)=_FALSE$
   SETTING$(27)=_FALSE$
End If 
If SCR_D=4
   _HIGHLIGHT=_GRAY
   _BLACK=_BG
   _SHADOW=_GRAY
End If 
If Ntsc
   SCR_H=200
   SETTING$(11)=_FALSE$
   SETTING$(24)=_FALSE$
End If 
HDR_H=11 : WVE_H=SCR_H-HDR_H-69 : WVE_W=SCR_W-1
_DSTEP=-WVE_H/8
POS_T=HDR_H+1
RNG_B=WVE_H
WND_Y=HDR_H+WVE_H+1 : WND_H=WND_Y+9
LEV_X=13 : LEV_W=LEV_X+15 : LEV_Y=HDR_H+WVE_H+28
TST_X=SCR_W-210 : TST_W=SCR_W-10
TST_Y=HDR_H+WVE_H+13 : TST_H=HDR_H+WVE_H+20 : TST_T=TST_Y+6
MET_X=SCR_W-30 : MET_W=SCR_W-14 : MET_Y=WVE_H+35 : MET_H=WVE_H+41
XD2=SCR_W : PXD2=XD2
OSC_W=SCR_W/2
CAL_X=424 : CAL_Y=WVE_H-40 : CAL_W=204 : CAL_H=44
'
'
'
'***************************************************************************** 
'*screen********************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'    .::::::.   .,-:::::::::::::..  .,::::::.,::::::::.    :::.  
'   ;;;`    `;,;;;'````' ;;;;``;;;; ;;;;'''';;;;'''';;;;,  `;;;  
'   '[==/[[[[,[[[        \[[[,/[[['  [[cccc  [[cccc  [[[[[. '[[  
'     '''    $$$$        "$$$$$$c    $$""""  $$""""  $$$ "Y$c$$  
'   ,88b    dP`88bo,__,o,,888b "88bo 888oo,_ 888oo,_888    Y88   
'     "YMmMY"   "YUMMMMMP"MMMM   "W" """"YUM """"YUMMM     YM    
'
'
'
Auto View Off : Every Off 
Screen Open 1,SCR_W,WVE_H,SCR_D,SCR_R : Screen Hide 1
Flash Off : Curs Off 
Screen Open 0,SCR_W,SCR_H,SCR_D,SCR_R
Flash Off : Curs Off 
Limit Mouse 128,44 To 448,44+SCR_H
Def Scroll 1,TST_X+8,TST_Y To TST_W+1,TST_H+1,-8,0
Def Scroll 2,TST_X,TST_Y To TST_W+1,TST_H+1,-1,0
Def Scroll 3,LEV_X,LEV_Y-17 To LEV_W+2,LEV_Y+1,0,1
Reserve Zone 79
Set Zone 15,TST_X,TST_Y To TST_W,TST_H
I=0
Repeat 
   Read BUTS$(I),BUTX(I),BUTY(I),BUTG$(I)
   BUTY(I)=SCR_H-BUTY(I)
   Inc I
Until I>_AB
I=0
Repeat 
   Read CMD$(I),CMDG$(I)
   Inc I
Until I>_CM
I=0
Repeat 
   Read FIBO(I)
   Inc I
Until I>15
I=0
Repeat 
   Read N,T$,N
   CHROM$(I)=Left$(T$,2)
   Inc I
Until I>13
If Arexx Exist("REXX")
   If Not REXX
      Trap Arexx Open FXBOX_REXX$
      If Not Errtrap and Arexx Exist(FXBOX_REXX$)
         REXX=True
      End If 
   End If 
End If 
'  
Gosub SETFILTER : Gosub SETMON : Gosub SETLIGHTMODE : Gosub SETDEBUG
Gosub SETNETSYNC : Gosub SETHIRES : Gosub SETLEVELMETER
Gosub SETMETRONOME : Gosub SETPREVIEW : Gosub SETGRAPH
Gosub SETAREXXSYNC : Gosub SETVST
Gosub SETPOINT2
'
Screen Display 0,128,-220,320,SCR_H
View : Multi Wait 
'
_INIT=True
Gosub R4INBOW
Gosub INITMENU
Gosub _MACROMENU
If Exist(SEQDIR$)
   Dir$=SEQDIR$
   SEQDIR$=Dir$
   Dir$=PROGDIR$
End If 
Gosub HEADER
Gosub _DRAW
Gosub MAGICWAND
Gosub SETHZ
Gosub DIALMAIN
Ink _SHADOW : Gosub AMIHZDIAL : _HIGHDIAL=True
Gosub ALLBTN
Auto View On : Amos To Front 
I#=-181 : DD#=0
Repeat 
   Screen Display 0,128,Int(I#),320,SCR_H
   Gosub TASK
   I#=I#+DD#
   DD#=DD#+0.75
Until I#>44
Goto _STOP
'
'
'
'***************************************************************************** 
'*error*********************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'
'   .,:::::::::::::..  :::::::..      ...     :::::::..
'   ;;;;'''';;;;``;;;; ;;;;``;;;;  .;;;;;;;.  ;;;;``;;;; 
'    [[cccc \[[[,/[[[' \[[[,/[[[' ,[[     \[[,\[[[,/[[[' 
'    $$"""" "$$$$$$c   "$$$$$$c   $$$,     $$$"$$$$$$c 
'    888oo,_,888b "88bo,888b "88bo"888,_ _,88P,888b "88bo
'    """"YUM"MMMM   "W""MMMM   "W"  "YMMMMMP" "MMMM   "W"
'
'
'
_ERROR: Gosub FLSHSCR : Goto _NOTICE
'
NOMATH: TXT1$="mathtrans.library" : TXT2$="is not available" : Goto _ERROR
'
NOCOPY: TXT1$="Need something in" : TXT2$="the copy buffer" : Goto _ERROR
'
NOUNDO: TXT1$="Nothing in the" : TXT2$="undo buffer" : Goto _ERROR
'
WRONGUNDO: TXT1$="Undo buffer from" : TXT2$="a different waveform" : Goto _ERROR
'
NOMEMORY: TXT1$="Not enough memory" : TXT2$="to complete" : Goto _ERROR
'
NOSPACE: TXT1$="Not enough space in" : TXT2$="the variable buffer" : Gosub _TOASTFREE : Goto _ERROR
'
NOBSD: TXT1$="BSD Library not found" : TXT2$="or not connected" : Goto _ERROR
'
CANTFIND: TXT1$="Can't find" : TXT2$='"'+F$+'"' : Goto _ERROR
'
FILEERROR: TXT1$="Error opening" : TXT2$='"'+F$+'"' : Goto _ERROR
'
MACROERROR: TXT1$="Macro failed" : TXT2$='"'+T$+'"' : Goto _ERROR
'
OVERWRITE: BTXT1$=_OK$ : TXT1$="File exists" : TXT2$="Overwrite?" : Goto _REQUEST
'
Procedure TR4P
   Shared _INIT,_EXIT,_DIRECT,PLYING,FX,FXE,_BG,_PANE,_WHITE,_GRAY,BUT_W,BUT_H,TXT2$
   If _INIT
      Sam Loop Off : Every Off 
   End If 
   _EXIT=True : PLYING=False
   FX=FXE
   If Errn=11
      TXT2$="out of variable space"
   Else If Errn=20
      TXT2$="division by zero"
   Else If Errn=23
      TXT2$="illegal function"
   Else If Errn=24
      TXT2$="not enough memory"
   Else If Errn=29
      TXT2$="overflow"
   Else If Errn=34
      TXT2$="type mismatch"
   Else If Errn=36
      TXT2$="bank not reserved"
   Else If Errn=40
      TXT2$="unknown subroutine"
   Else If Errn=81
      TXT2$="file not found"
   Else If Errn=82
      TXT2$="file name invalid"
   Else If Errn=84
      TXT2$="disk write protected"
   Else If Errn=88
      TXT2$="disk is full"
   Else If Errn>88 and Errn<92
      TXT2$="file protected"
   Else If Errn=94
      TXT2$="of an I/O error"
   Else If Errn=97
      TXT2$="file name too long"
   Else 
      TXT2$=Str$(Errn)
   End If 
   If _DIRECT
      Print "Error ";TXT2$
   Else 
      If Not _INIT
         Amos To Front : View 
      End If 
      RX=216 : RY=59
      WX=RX-5 : WY=RY-15 : WX2=RX+207+5 : WY2=RY+45
      Ink _GRAY
      Bar WX-5,WY-4 To WX2+5,WY2+4
      Ink _BG
      Box WX-5,WY-4 To WX2+5,WY2+4
      Ink _WHITE
      Bar WX,WY To WX2,WY2
      Ink _GRAY
      Bar WX+1,WY+1 To WX2-1,WY+13
      Box WX+1,WY+1 To WX2-1,WY2-1
      Ink _WHITE,_GRAY
      Text WX+4,WY+10,"Error"
      Ink _BG,_WHITE
      Text RX+18,RY+11,"Can't do that because"
      Text RX+103-(Text Length(TXT2$)/2),RY+21,TXT2$
      BX=RX+53 : BY=RY+28
      Ink _BG
      Bar BX,BY To BX+BUT_W,BY+BUT_H
      Ink _WHITE
      Bar BX+1,BY+1 To BX+BUT_W-1,BY+BUT_H-1
      Ink _GRAY
      Bar BX+2,BY+2 To BX+BUT_W-2,BY+BUT_H-2
      Ink _BG,_GRAY
      Text BX+(BUT_W/2)-8,BY+BUT_H-4,"Ok"
      Repeat 
         Multi Wait 
      Until Mouse Key=0
      Repeat 
         Multi Wait 
      Until Mouse Key=1
      Repeat 
         Multi Wait 
      Until Mouse Key=0
      Ink _PANE
      Bar WX-5,WY-4 To WX2+5,WY2+4
   End If 
   Resume Next 
End Proc
'
'
'
'***************************************************************************** 
'*data************************************************************************ 
'***************************************************************************** 
'***************************************************************************** 
'***************************************************************************** 
'
'   ::::::-.    :::.     ::::::::::  :::.        
'   ;;,   `';,  ;;`;;    ;;;;;;;'''  ;;`;;       
'   `[[     [[ ,[[ '[[,      [[     ,[[ '[[,     
'    $$,    $$c$$$cc$$$c     $$    c$$$cc$$$c    
'    888_,o8P' 888   888,    88,    888   888,   
'   "MMMMP"`   YMM   ""`     MMM    YMM   ""`    
'
'
'
DTA:
Data %1,%10,%100,%1000,%1001,%110,%1111
'
Data "DH0:","DH1:","DF0:","RAM:"
Data "SYS:REXXC/RX"
Data "127.0.0.255","7000"
Data "127.0.0.255","7002"
Data _FALSE$,_TRUE$,_TRUE$,_FALSE$
Data _FALSE$,_TRUE$,_TRUE$,_TRUE$
Data _TRUE$,_TRUE$,_TRUE$,_FALSE$
Data _TRUE$,_FALSE$,_TRUE$,_FALSE$
Data _FALSE$,_TRUE$,_FALSE$,_FALSE$
'
Data "Play All",5,17,"_PLAYALL"
Data "Play Range",110,17,"_PLAYRANGE"
Data "Play Display",215,17,"_PLAYDISPLAY"
Data "Mix",320,17,"_MIX"
Data "Repeat",425,17,"_REPEAT"
Data "Range None",530,17,"_RANGENONE"
Data "Range Disp",5,33,"_RANGEDISPLAY"
Data "Show Range",110,33,"_SHOWRANGE"
Data "Show All",215,33,"_SHOWALL"
Data "Cut",320,33,"_CUT"
Data "Copy",425,33,"_COPY"
Data "Paste",530,33,"_PASTE"
Data "Loop",215,52,"_TGLLOOP"
Data "Mix Loop",320,52,"_TGLMIXLOOP"
'
Data "B=","¯BANK"
Data "S=","¯SWAP"
Data "D=","¯DIV"
Data "M=","¯MODE"
Data "R=","¯RNG"
Data "FREQ=","¯FREQ"
Data "SIZE=","¯SIZE"
Data "VOL=","¯VOL"
Data "NAME=","¯NAME"
Data "(C)=","¯CPYR"
Data "AUTH=","¯AUTH"
Data "ANNO=","¯ANNO"
Data "WAND=","¯WAND"
Data "ZL=","¯ZOOML"
Data "ZR=","¯ZOOMR"
Data "CD..","¯CDDD"
Data "CD","¯CD"
Data "MD","¯MD"
Data "DIR","¯DIR"
Data "DEL","¯DEL"
Data "CLS","¯CLS"
Data "ED","¯ED"
Data "OPEN","¯OPEN"
Data "MACRO","¯MACRO"
Data "SEQ","¯SEQ"
Data "RX","¯RX"
Data "HELP SUBS","¯HELPSUBS"
Data "HELP 14BIT","¯HELP14"
Data "HELP DITHER","¯HELPDITHER"
Data "HELP","¯HELP"
Data "ENDCLI","¯EXIT"
Data "EXIT","¯EXIT"
Data "QUIT","¯QUIT"
'
Data -34,-21,-13,-8,-5,-3,-2,-1,0,1,2,3,5,8,13,21
'
Data 0,"B-0",3950
KLO:
Data 49,"C-1",4181
Data 33,"C#1",4430
Data 50,"D-1",4697
Data 34,"D#1",4971
Data 51,"E-1",5279
Data 52,"F-1",5593
Data 36,"F#1",5926
Data 53,"G-1",6279
Data 37,"G#1",6653
Data 54,"A-1",7046
Data 38,"A#1",7457
Data 55,"B-1",7901
Data 16,"C-2",8363
Data 2,"C#2",8860
Data 17,"D-2",9395
Data 3,"D#2",9943
Data 18,"E-2",10559
Data 19,"F-2",11186
Data 5,"F#2",11852
Data 20,"G-2",12559
Data 6,"G#2",13306
Data 21,"A-2",14092
Data 7,"A#2",14914
Data 69,"B-2",15838
'
KHI:
Data 49,"C-2",8363
Data 33,"C#2",8860
Data 50,"D-2",9395
Data 34,"D#2",9943
Data 51,"E-2",10559
Data 52,"F-2",11186
Data 36,"F#2",11852
Data 53,"G-2",12559
Data 37,"G#-2",13306
Data 54,"A-2",14092
Data 38,"A#2",14914
Data 55,"B-2",15838
Data 16,"C-3",16726
Data 2,"C#3",17720
Data 17,"D-3",18839
Data 3,"D#3",19886
Data 18,"E-3",21056
Data 19,"F-3",22372
Data 5,"F#3",23705
Data 20,"G-3",25031
Data 6,"G#3",26515
Data 21,"A-3",28185
Data -1,_$,-1
'
OPB:
Data "Filter","Filter On",5,21,59,"SETFILTER"
Data "60Hz","50Hz",110,21,60,"SETMON"
Data "Dark Mode","Light Mode",215,21,61,"SETLIGHTMODE"
Data "Debug","Debug On",320,21,62,"SETDEBUG"
Data "Net Sync","Net Sync On",5,38,63,"SETNETSYNC"
Data "Lowres","Hires",110,38,64,"SETHIRES"
Data "Position",_$,215,38,65,"_NOP"
Data "Level Meter",_$,320,38,66,"SETLEVELMETER"
Data "Metronome",_$,5,55,67,"SETMETRONOME"
Data "Realtime",_$,110,55,68,"SETPREVIEW"
Data "Zoom Draw",_$,215,55,69,"_NOP"
Data "Graph","Low Graph",320,55,70,"SETGRAPH"
Data "Wheel Zoom",_$,5,72,71,"_NOP"
Data "Arexx Sync",_$,110,72,72,"SETAREXXSYNC"
Data "Scroller",_$,215,72,73,"_NOP"
Data "VST","VST On",320,72,74,"SETVST"
Data "Quantizer",_$,5,89,75,"_NOP"
Data "Always Draw",_$,110,89,76,"SETALWAYSDRAW"
Data "Magic","More Magic",215,89,77,"_NOP"
Data "-0.2Hz",_$,320,89,78,"SETPOINT2"
Data _OK$,_$,162,109,79,"OPTIONSEXIT"
'
T0B:
Data _$,53,26,48,_$
Data "Square",0,42,60,"_SQRTONE"
Data "Sine",106,42,61,"_SINTONE"
Data "Pulse",0,58,58,"_PLSTONE"
Data "Cosine",106,58,59,"_COSTONE"
Data "Triangle",0,74,63,"_TRITONE"
Data "Sawtooth",106,74,62,"_SAWTONE"
Data _CANCEL$,53,90,65,"T0NEEXIT"
Data _$,53,90,13,"T0NEEXIT"
Data _$,53,90,14,"T0NEEXIT"
'
CUT:
Data "F1-F10 (and numpad)   Set bank, hold shift to swap"
Data "+\- (and numpad)      Previous / Next (numpad x10)"
Data "Amiga Key 1 2 3 4 d   Mono Stereo Quad Multi Direct"
Data "Amiga Key o a r s     Open Append Revert Save As"
Data "Amiga Key x c v z     Cut Copy Paste Undo"
Data "Esc                   No Close Cancel Quit"
Data "Space / Backspace     Stop / Rewind"
Data "Return                Loop on/off"
Data "Cursor Up / Down      Fine tune tone generator"
Data "Cursor Left / Right   Range resize by pixel"
Data "Left Shift            Time division range select"
Data "Left Shift            Fine move dials"
Data "Left Shift            Fine move zoom"
Data "Caps Lock             Realtime preview all channels"
Data "Caps Lock             Piano low octave"
'
HLP:
Data 17,"Set Bank: B=1..256"
Data "Swap Bank: S=1..256"
Data "Set Mode: M=1..4"
Data "Set Time Divisions: D=1.."+Str$(SCR_W)-_SPACE$
Data "Select Range: R=1..D"
Data "Set Frequency: FREQ=1.."+Str$(MFREQ)-_SPACE$
Data "Set Size: SIZE=1..2147483647"
Data "Set Volume: VOL=0..64"
Data "Iff Meta: NAME= (c)= AUTH= ANNO="
Data "Dos: CD, MD, DIR, DEL, ED, CLS"
Data "Open File: OPEN <filename>"
Data "Run Macro: MACRO <name>"
Data "Load Sequence: SEQ <filename>"
Data 'Arexx: RX "address '+FXBOX_REXX$+' (subroutine|command)"'
Data "Subroutines: HELP SUBS"
Data "14bit: HELP 14BIT"
Data "Dither: HELP DITHER"
'
H14:
Data 15,,"Converting to 14bit:",
Data "Press F1"
Data "File » Open As Raw"
Data "Process » Convert » 24bit / 32bit Float to 16bit (flip if necessary)"
Data "Process » Decode » Deinterleave 16bit (for stereo)"
Data "Process » Decode » Deinterleave 8bit"
Data "Shift range select the noise half, it can be either side"
Data "Process » Bitwise » Logical Shift Right, click Repeat, click Cut"
Data "Press F4, click Paste, shift range select the second half, click Cut"
Data "Press F3, click Paste, press F1, shift range select the second half"
Data "Click Cut, press F2, click Paste, press F1"
Data "File » Mode » 14bit",
'
D24:
Data 9,,"Dithering:",
Data "Automatic for Process » Convert » 24bit to ..."
Data "Utilities » Noise » Dither is the subroutine _DITHER"
Data "Macros » 16bit dither (flip with Process » Reverse)"
Data "_TGLDITHER to cycle between the options:"
Data "No Dither, Dither (Default), HP-Tri Dither, LP Dither",
'
1 Data "File    "
11 Data "New          ",0,"_NEW"
12 Data "Open         ",0,"_OPEN"
13 Data "Open As Raw  ",0,"_OPENASRAW"
14 Data "Append       ",0,"_APPEND"
15 Data "Revert       ",0,"_REVERT"
16 Data "Save As   »  "
161 Data "Iff",0,"_SAVEASIFF"
162 Data "Raw",0,"_SAVEAS"
163 Data _NULL$
17 Data "Save All  »  "
171 Data "Iff CAT  ",0,"_SAVEASCAT"
172 Data "AMOS Bank",0,"_SAVEASBANK"
173 Data _NULL$
18 Data _BAR$
19 Data "Mode      »  "
191 Data "Mono  ",0,M0DEMENU$
192 Data "Stereo",0,M0DEMENU$
193 Data "Quad  ",0,M0DEMENU$
194 Data "Multi ",0,M0DEMENU$
195 Data "14bit ",0,"_14BIT"
196 Data _NULL$
110 Data "Rate      »  "
1101 Data "8000 ",0,RATEMENU$
1102 Data "11025",0,RATEMENU$
1103 Data "12000",0,RATEMENU$
1104 Data "16000",0,RATEMENU$
1105 Data "22050",0,RATEMENU$
1106 Data "24000",0,RATEMENU$
1107 Data "32000",0,RATEMENU$
1108 Data "44100",0,RATEMENU$
1109 Data "48000",0,RATEMENU$
11010 Data "64000",0,RATEMENU$
11011 Data "96000",0,RATEMENU$
11012 Data _NULL$
111 Data _BAR$
112 Data "Properties   ",0,"_PROPERTIES"
113 Data _BAR$
114 Data "Quit         ",0,"_QUIT"
115 Data _NULL$
2 Data "Edit    "
21 Data "Cut          ",0,"_CUT"
22 Data "Crop         ",0,"_CROP"
23 Data "Copy         ",0,"_COPY"
24 Data "Paste        ",0,"_PASTE"
25 Data "Mix       »  "
251 Data "Mix       ",-1,"_MIX"
252 Data "Add       ",-1,"_ADD"
253 Data "Subtract  ",-1,"_SUBTRACT"
254 Data "Modulate  ",-1,"_MODULATE"
255 Data "Xor       ",-1,"_XOR"
256 Data _NULL$
26 Data _BAR$
27 Data "Size         ",-1,"_SIZE"
28 Data "Swap         ",0,"_SWAP"
29 Data "Frame     »  "
291 Data "Sample Left  ",-1,"_SAMPLELEFT"
292 Data "Sample Right ",-1,"_SAMPLERIGHT"
293 Data "Beat Left    ",-1,"_BEATLEFT"
294 Data "Beat Right   ",-1,"_BEATRIGHT"
295 Data "Time Division",-1,"_TIMEDIVISION"
296 Data _NULL$
210 Data _BAR$
211 Data "Range All    ",0,"_RANGEALL"
212 Data "Range Display",0,"_RANGEDISPLAY"
213 Data "Range None   ",0,"_RANGENONE"
214 Data _BAR$
215 Data "Clear Copy   ",0,"_CLEARCOPY"
216 Data "Swap Copy    ",0,"_COPYSWAP"
217 Data _BAR$
218 Data "Undo         ",0,"_UNDO"
219 Data _NULL$
3 Data "Process    "
31 Data "Volume    »  "
311 Data "Volume   ",-1,"_VOLUME"
312 Data "Fade In  ",-1,"_FADEIN"
313 Data "Fade Out ",-1,"_FADEOUT"
314 Data "Half     ",-1,"_HALF"
315 Data "Double   ",-1,"_DOUBLE"
316 Data "Maximize ",-1,"_MAXIMIZE"
317 Data "Silence  ",-1,"_SILENCE"
318 Data "DC Offset",-1,"_DCOFFSET"
319 Data _NULL$
32 Data "Resample  »  "
321 Data "by Rate    ",-1,"_BYRATE"
322 Data "by Size    ",-1,"_BYSIZE"
323 Data "by BPM     ",-1,"_BYBPM"
324 Data "Octave Up  ",-1,"_OCTAVEUP"
325 Data "Octave Down",-1,"_OCTAVEDOWN"
326 Data "Note Up    ",-1,"_NOTEUP"
327 Data "Note Down  ",-1,"_NOTEDOWN"
328 Data "Fine Up    ",-1,"_FINEUP"
329 Data "Fine Down  ",-1,"_FINEDOWN"
3210 Data _NULL$
33 Data "Filter    »  "
331 Data "High Pass ",-1,"_HIGHPASS"
332 Data "Low Pass  ",-1,"_LOWPASS"
333 Data "Band Pass ",-1,"_BANDPASS"
334 Data "Resonant  ",-1,"_RESONANT"
335 Data "Smooth    ",-1,"_SMOOTH"
336 Data "Waveshaper",-1,"_WAVESHAPER"
337 Data "Parabolic ",-1,"_PARABOLIC"
338 Data "Bezier    ",-1,"_BEZIER"
339 Data "Expand    ",-1,"_EXPAND"
3310 Data "Contract  ",-1,"_CONTRACT"
3311 Data _NULL$
34 Data "Distort   »  "
341 Data "Tube      ",-1,"_TUBE"
342 Data "Saturation",-1,"_SATURATION"
343 Data "Overdrive ",-1,"_OVERDRIVE"
344 Data "Alias     ",-1,"_ALIAS"
345 Data "Fold      ",-1,"_FOLD"
346 Data "2bit      ",-1,"_2BIT"
347 Data _NULL$
35 Data "Effects   »  "
351 Data "Phase      ",-1,"_PHASE"
352 Data "Simple Echo",-1,"_ECHO"
353 Data "Reverb     ",-1,"_REVERB"
354 Data "Tremolo    ",-1,"_TREMOLO"
355 Data "Compressor ",-1,"_COMPRESSOR"
356 Data _NULL$
36 Data "Bitwise   »  "
361 Data "Rotate Left        ",-1,"_ROTATELEFT"
362 Data "Rotate Right       ",-1,"_ROTATERIGHT"
363 Data "Shift Left         ",-1,"_SHIFTLEFT"
364 Data "Shift Right        ",-1,"_SHIFTRIGHT"
365 Data "Logical Shift Left ",-1,"_LOGICALSHIFTLEFT"
366 Data "Logical Shift Right",-1,"_LOGICALSHIFTRIGHT"
367 Data _NULL$
37 Data "Convert   »  "
371 Data "8bit Unsigned to 8bit    ",-1,"_8BITUNSIGNEDTO8BIT"
372 Data "16bit to 8bit            ",-1,"_16BITTO8BIT"
373 Data "16bit to 8bit Flip       ",-1,"_16BITTO8BITFLIP"
374 Data "24bit to 8bit            ",-1,"_24BITTO8BIT"
375 Data "24bit to 8bit Flip       ",-1,"_24BITTO8BITFLIP"
376 Data "24bit to 16bit           ",-1,"_24BITTO16BIT"
377 Data "24bit to 16bit Flip      ",-1,"_24BITTO16BITFLIP"
378 Data "32bit Float to 8bit      ",-1,"_32BITTO8BIT"
379 Data "32bit Float to 8bit Flip ",-1,"_32BITTO8BITFLIP"
3710 Data "32bit Float to 16bit     ",-1,"_32BITTO16BIT"
3711 Data "32bit Float to 16bit Flip",-1,"_32BITTO16BITFLIP"
3712 Data "32bit Float to 24bit     ",-1,"_32BITTO24BIT"
3713 Data "32bit Float to 24bit Flip",-1,"_32BITTO24BITFLIP"
3714 Data _NULL$
38 Data "Encode    »  "
381 Data "Interleave     ",-1,"_INTERLEAVE"
382 Data "Fibonacci Delta",-1,"_ENCODEFIBONACCIDELTA"
383 Data _NULL$
39 Data "Decode    »  "
391 Data "Deinterleave 8bit ",-1,"_DEINTERLEAVE"
392 Data "Deinterleave 16bit",-1,"_DEINTERLEAVE16BIT"
393 Data "Fibonacci Delta   ",-1,"_DECODEFIBONACCIDELTA"
394 Data _NULL$
310 Data _BAR$
'311 menu collision  
4 Data "Utilities    "
41 Data "Tone Generator",0,"T0NEG"
42 Data "Noise      »  "
421 Data "White ",-1,"_WHITENOISE"
422 Data "Dither",-1,"_DITHER"
423 Data "Crypto",-1,"_CRYPTO"
424 Data "Sin() ",-1,"_SINNOISE"
425 Data "Fill  ",-1,"_FILLNOISE"
' hidden items 
426 Data _$,-1,"_SQRTONE"
427 Data _$,-1,"_SINTONE"
428 Data _$,-1,"_PLSTONE"
429 Data _$,-1,"_COSTONE"
4210 Data _$,-1,"_TRITONE"
4211 Data _$,-1,"_SAWTONE"
4212 Data _$,-1,"_REVERSE"
4213 Data _$,-1,"_INVERT"
'
4214 Data _NULL$
43 Data _RECORD$,0,"_TGLRECORD"
44 Data "Direct Mode   ",0,"NEWCLI"
45 Data "Load Sequence ",0,"_OPENASSEQUENCE"
46 Data "Calculator    ",0,"_TGLCALCULATOR"
47 Data _OSCILLOSCOPE$,0,"_TGLOSCILLOSCOPE"
48 Data _NULL$
5 Data "Memory    "
51 Data MENB$(1),0,BANKMENU$
52 Data MENB$(2),0,BANKMENU$
53 Data MENB$(3),0,BANKMENU$
54 Data MENB$(4),0,BANKMENU$
55 Data MENB$(5),0,BANKMENU$
56 Data MENB$(6),0,BANKMENU$
57 Data MENB$(7),0,BANKMENU$
58 Data MENB$(8),0,BANKMENU$
59 Data MENB$(9),0,BANKMENU$
510 Data MENB$(10),0,BANKMENU$
511 Data _BAR$
512 Data "Previous     ",0,"_PREVIOUS"
513 Data "Next         ",0,"_NEXT"
514 Data _BAR$
515 Data "Erase All    ",0,"_ERASEALL"
516 Data _NULL$
6 Data "Help    "
61 Data "Shortcuts",0,"SHORTCUTS"
62 Data "Options  ",0,"OPTIONS"
63 Data "About    ",0,"ABOUT"
64 Data _NULL$
7 Data "Macros                "
71 Data "­­­­­­­­  ­­­­­­­­",0,"MACROUP" : Rem the other minus
72 Data MENM$(1),0,_MACRO$
73 Data MENM$(2),0,_MACRO$
74 Data MENM$(3),0,_MACRO$
75 Data MENM$(4),0,_MACRO$
76 Data MENM$(5),0,_MACRO$
77 Data MENM$(6),0,_MACRO$
78 Data MENM$(7),0,_MACRO$
79 Data MENM$(8),0,_MACRO$
710 Data MENM$(9),0,_MACRO$
711 Data MENM$(10),0,_MACRO$
712 Data MENM$(11),0,_MACRO$
713 Data MENM$(12),0,_MACRO$
714 Data MENM$(13),0,_MACRO$
715 Data MENM$(14),0,_MACRO$
716 Data MENM$(15),0,_MACRO$
717 Data MENM$(16),0,_MACRO$
718 Data MENM$(17),0,_MACRO$
719 Data MENM$(18),0,_MACRO$
720 Data "­­­­­­­­  ­­­­­­­­",0,"MACRODOWN" : Rem the other minus
721 Data _NULL$
8 Data _NULL$
'
_NOP:
Return 
